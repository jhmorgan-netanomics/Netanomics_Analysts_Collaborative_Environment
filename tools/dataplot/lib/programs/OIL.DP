. THIS IS THE DATAPLOT PROGRAM FILE     OIL.DP
. PURPOSE--DETERMINE PRODUCTION MIX OF 2 OILS TO MAXIMIZE PROFITS
. ANALYSIS TECHNIQUE--SIMPLEX METHOD IN LINEAR PROGRAMMING
.                     (4 VARIABLES, 10 CONSTRAINTS)
. APPLICATION--PRODUCTION OPTIMIZATION
. SOURCE--BRONSON, RICHARD, THEORY AND PROBLEMS OF
.         OPERATIONS RESEARCH (SCHAUM'S OUTLINE SERIES),
.         MCGRAW-HILL, 1982, PAGES 6, 7, 8, 40, AND 41.
. TO FIND--4 PRODUCTIONS SETTINGS--
.          X1 = NUMBER OF BARRELS OF DOMESTIC OIL TO BLEND INTO REG.  GAS.
.          X2 = NUMBER OF BARRELS OF FOREIGN  OIL TO BLEND INTO REG.  GAS.
.          X3 = NUMBER OF BARRELS OF DOMESTIC OIL TO BLEND INTO PREM. GAS.
.          X4 = NUMBER OF BARRELS OF FOREIGN  OIL TO BLEND INTO PREM. GAS.
. CONSTRAINTS ARE BASED ON--
.
.    COST (/BBL)        --DOMESTIC = $8       FOREIGN = $14
.    INVENTORY (BBL)    --DOMESTIC = 40,000   FOREIGN = 60,000
.    OCTANE RATING      --DOMESTIC = 87       FOREIGN = 98
.    VAPOR PRESSURE     --DOMESTIC = 25       FOREIGN = 15
.
.    REVENUE (/BBL)     --REGULAR  = $12      PREMIUM = $14
.    MAX DEMAND (BBL/WK)--REGULAR  = 100,000  PREMIUM = 20,000
.    MIN OCTANE         --REGULAR  = 88       PREMIUM = 93
.    MAX VAPOR PRESSURE --REGULAR  = 23       PREMIUM = 23
.
. NOTE--A   FEASIBLE SOLUTION IS 0, 50000, 0, 5000
.       (BUT WITH A LOSS    OF $155000)
.       THE OPTIMAL  SOLUTION IS 37727.3, 12272.7, 2272.7, 2727.3
.       (WITH A PROFIT OF $125000)
. DATE--OCTOBER 1987
.
. -----START POINT-----------------------------------
.
ECHO
DIMENSION 100 VARIABLES
.
.      STEP 1--
.      DEFINE THE COEFFICIENTS OF THE OBJECTIVE FUNCTION
.
SERIAL READ F
4 -3 6 -1
END OF DATA
.
.      STEP 2--
.      DEFINE THE CONSTRAINTS CONSISTING OF
.      LEFT-SIDE COEFFICIENTS
.      CODED INEQUALITY (< AS -1, = AS 0, > AS +1)
.      AND RIGHT-SIDE LIMITS.
.
READ MATRIX C
1 1 0 0     -1   100000
0 0 1 1     -1    20000
1 0 1 0     -1    40000
0 1 0 1     -1    60000
1 -10 0 0   -1        0
0 0 6 -5    -1        0
2 -8 0 0    -1        0
0 0 2 -8    -1        0
1 1 0 0     +1    50000
0 0 1 1     +1     5000
END OF DATA
PRINT F C
.
.      STEP 3--
.      GENERATE THE SIMPLEX SOLUTION
.
LET X = MATRIX SIMPLEX SOLUTION F C
LET PROFIT = VECTOR DOT PRODUCT F X
PRINT F X PROFIT
