. THIS IS DATAPLOT PROGRAM FILE    PLOTCOLOR.DP
. PURPOSE--GENERATE A SERIES OF COLOR PLOTS
. DATE--SEPTEMBER 1993
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
.
. ---------------------
. STEP 1--DEFINE BLACK/WHITE OR COLOR
. ---------------------
.
ECHO
.
LET COLOR = 0
.
IF COLOR = 0
   LET STRING BACK = WHITE
   LET STRING FRONT = BLACK
END IF
.
IF COLOR = 1
   DEVICE 2 COLOR ON
   . LET STRING BACK = CYAN
   . LET STRING FRONT = YELLOW
   LET STRING BACK = BLUE
   LET STRING FRONT = ORANGE
END IF
.
. ------------------------------------------
. STEP 2--SET BACKGROUND & FOREGROUND COLORS
. ------------------------------------------
.
IF COLOR = 1
   BOX FILL COLOR ^BACK
   BOX FILL PATTERN SOLID
   BOX COLOR ^BACK
END IF
.
BOX SHADOW HW 0 0
. .
FRAME COLOR ^FRONT
TIC COLOR ^FRONT
TIC LABEL COLOR ^FRONT
.
TITLE COLOR ^FRONT
LABEL COLOR ^FRONT
LEGEND COLOR ^FRONT
.
CHARACTER COLOR ^FRONT ALL
LINE COLOR ^FRONT ALL
SPIKE COLOR ^FRONT ALL
BAR COLOR ^FRONT ALL
COLOR ^FRONT
.
. -----------------------------------------
. STEP 3--DEFINE PLOT SIZE AND SCRIPT SIZES
. -----------------------------------------
.
PRE-ERASE OFF
WINDOW CORNER COORDINATES 30 30 75 80
FRAME CORNER COORDINATES 15 20 85 88
TITLE SIZE 6
LABEL SIZE 3
TIC LABEL SIZE 2
TITLE DISPLACEMENT 3
Y1LABEL DISPLACEMENT 6
X3LABEL AUTOMATIC
.
THICKNESS .25
LEGEND 1 COORDINATES 17 84
LEGEND 2 COORDINATES 17 81
LEGEND SIZE 3.5
.
. FEEDBACK OFF
. PRINTING OFF
.
. --------------------------
. STEP 4--DEFINE PLOT MACROS
. --------------------------
.
LET STRING S1 = DATAPLOT.DP
LET STRING S2 = DERIVPLO.DP
LET STRING S3 = BOXPLOT.DP
LET STRING S4 = BOXPLOT2.DP
LET STRING S5 = MATHOPER.DP
LET STRING S6 = LAGPLOT.DP
LET STRING S7 = SPECPLOT.DP
LET STRING S8 = RESPLOT.DP
LET STRING S9 = FFTPLOT.DP
LET STRING S10 = YOUDPLOT.DP
LET STRING S11 = ROOTPLOT.DP
LET STRING S12 = STARPLOT.DP
LET STRING S13 = MEANPLOT.DP
LET STRING S14 = SDPLOT.DP
LET STRING S15 = SPIKEPLO.DP
LET STRING S16 = TEXASMAP.DP
LET STRING S17 = QUANPLOT.DP
LET STRING S18 = PDFPLOT.DP
LET STRING S19 = WEIBPLOT.DP
LET STRING S20 = HIST.DP
LET STRING S21 = BIHIST.DP
LET STRING S22 = PROFPLOT.DP
LET STRING S23 = GANOVA3.DP
LET STRING S24 = MULTITRA.DP
LET STRING S25 = HIST2.DP
.
. ----------------------
. STEP 5--GENERATE PLOTS
. ----------------------
.
LET MINCOUNT=1
LET MAXCOUNT=1
LOOP FOR COUNTER = MINCOUNT 1 MAXCOUNT
   ERASE
   BOX 0 0 100 100
   LET STRING MARK = ^S^COUNTER
   CALL ^S^COUNTER
   CALL MARK2.DP
.    RESET VARIABLES CLSB LIMITS
END LOOP
