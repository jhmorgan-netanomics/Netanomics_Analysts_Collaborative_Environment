. THIS IS THE DATAPLOT PROGRAM FILE     CHWIRUT1.DP
. PURPOSE--NON-LINEAR FIT
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
.
SKIP 25
READ CHWIRUT1.DAT Y X
.
MULTIPLOT CORNER COORDINATES 10 10 90 85
LABEL SIZE 4
TITLE SIZE 4
TITLE OFFSET 2
.
. ----------PAGE 1----------
.
MULTIPLOT 2 3
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
XLABEL PLOT Y X
PLOT Y X
.
XLABEL
TITLE 2. MODEL FITTING (Y = EXP/LINEAR)
PLOT
CAPTURE JUNK1.
LET FUNCTION F = EXP(-ALPHA*X)/(A+B*X)
LET ALPHA = .1
LET A = .01
LET B = .01
FIT Y = F
LET PRED1 = PRED
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION (Y = EXP/LINEAR)
CHAR X BLANK
LINES BLANK SOLID
XLABEL PLOT Y PRED VERSUS X
PLOT Y PRED VERSUS X
.
XLABEL PLOT RES X
PLOT RES X
.
XLABEL NORMAL PROBABILITY PLOT RES
NORMAL PROBABILITY PLOT RES
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ULTRASONIC CALIBRATION
.
. ----------PAGE 2----------
.
MULTIPLOT 2 2
.
TITLE 1. MODEL SELECTION
XLABEL X
Y1LABEL Y
PLOT Y X
LET Y2 = SQRT(Y)
Y1LABEL SQRT(Y)
PLOT Y2 X
LET Y3 = LOG(Y)
Y1LABEL LOG(Y)
PLOT Y3 X
LET Y4 = 1/Y
Y1LABEL 1/Y
PLOT Y4 X
Y1LABEL
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ULTRASONIC CALIBRATION
TEXT WHAT TRANSFORMATION FOR Y?
.
. ----------PAGE 3----------
.
MULTIPLOT 2 3
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
XLABEL PLOT Y2 X
PLOT Y2 X
.
XLABEL
TITLE 2. MODEL FITTING (SQRT(Y))
PLOT
CAPTURE JUNK2.
LET FUNCTION F = EXP(-ALPHA*X)/(A+B*X)
LET ALPHA = .1
LET A = .01
LET B = .01
FIT Y2 = F
LET PRED2 = PRED
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION (SQRT(Y))
CHAR X BLANK
LINES BLANK SOLID
XLABEL PLOT Y2 PRED VERSUS X
PLOT Y2 PRED VERSUS X
.
XLABEL PLOT RES X
PLOT RES X
.
XLABEL NORMAL PROBABILITY PLOT RES
NORMAL PROBABILITY PLOT RES
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ULTRASONIC CALIBRATION
.
. ----------PAGE 4----------
.
MULTIPLOT OFF
TITLE COMPARISON OF 2 MODELS (ULTRASONIC CALIBRATION)
XLABEL PLOT Y PRED1 PRED2B VERSUS X
CHAR X BLANK BLANK
LINES BLANK SOLID DOTTED
LET PRED2B = PRED2**2
PLOT Y PRED1 PRED2B VERSUS X
.
