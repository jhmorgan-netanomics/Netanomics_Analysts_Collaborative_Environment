.  Demonstrate canonical correlation
.  PERFORM A CANONICAL CORRELATION ANALYSIS.  THIS EXAMPLE IS TAKEN
.  FROM THE IMSL STAT/LIBRARY MANUAL.  THIS MACRO CAN START FROM 
.  EITHER A VARIANCE-COVARIANCE MATRIX OR A CORRELATION MATRIX.
.
FEEDBACK OFF
.
DIMENSION 200 COLUMNS
.  READ THE COVARIANCE MATRICES (PARTITIONED INTO S11, S12, S22)
READ MATRIX S
 1.0000  0.1839  0.0489  0.0186  0.0782  0.1147  0.2137  0.2742
 0.1839  1.0000  0.2220  0.1861  0.3355  0.1021  0.4105  0.4043
 0.0489  0.2220  1.0000  0.2707  0.2302  0.0931  0.3240  0.4047
 0.0186  0.1861  0.2707  1.0000  0.2950 -0.0438  0.2930  0.2407
 0.0782  0.3355  0.2302  0.2950  1.0000  0.2087  0.2995  0.2863
 0.1147  0.1021  0.0931 -0.0438  0.2087  1.0000  0.0760  0.0702
 0.2137  0.4105  0.3240  0.2930  0.2995  0.0760  1.0000  0.6247
 0.2742  0.4043  0.4047  0.2407  0.2863  0.0702  0.6247  1.0000
END OF DATA
.
.  CALCULATE MATRIX DIMENSIONS, SPLIT INTO S11, S12, S22
.
LET P1 = 6
LET P2 = 2
.
.  Remainder of macro is not dependent on the particualar data set
.  being used.
.
LET N = P1 + P2
LET P21 = P1 + 1
LET TAG = SEQUENCE 1 1 P1
LET TAG2 = SEQUENCE 1 1 P2
LET SA = MATRIX DEFINITION S1 P1 P1
LET SB = MATRIX DEFINITION S^P21 P1 P2
LET SD = MATRIX DEFINITION S^P21 N P2 P21
.
.  CALCULATE C = (S11**(-1/2))'S12(S22**(-1/2))
.  CHOLESKY DECOMPOSITION IS THE MATRIX SQUARE ROOT
.
LET RA = CHOLESKY DECOMP SA
LET RAINV = TRIANGULAR INVERSE RA
LET RAINVT = MATRIX TRANSPOSE RAINV
LET RD = CHOLESKY DECOMP SD
LET RDINV = TRIANGUILAR INVERSE RD
LET CZ = MATRIX MULTIPLY RAINVT SB
LET C = MATRIX MULTIPLY CZ RDINV
.
.  USE SINGULAR VALUE DECOMPOSITION TO COMPUTE CANONICAL CORRELATIONS
.  AND CANONICAL COEFFICIENTS.  CALCULATE WILK'S LAMBDA.
.
LET LEV SV REV = SINGULAR VALUE DECOMPOSITION C
LET TEMPV = 1 - SV*SV FOR I = 1 1 P2
LOOP FOR K = 1 1 P2
    LET A = PRODUCT TEMPV SUBSET TAG2 >= K
    LET WILK(K) = A
END OF LOOP
.
.  CALCULATE CANONICAL COEFFICENTS
.
LET GAMMA1 = MATRIX MULTIPLY RAINV LEV
LET GAMMA2 = MATRIX MULTIPLY RDINV REV
.
.  CALCULATE CORRELATIONS BETWEEN VARIABLES AND CANONICAL VARIABLES
.
LET RA = MATRIX TRANSPOSE RA
LET RA = MATRIX MULTIPLY RA LEV
LET TEMP = MATRIX DIAGONAL SA
LET TEMP = 1/TEMP
LET SADIAG = DIAGONAL MATRIX TEMP
LET SADIAG = MATRIX MULTIPLY SADIAG RA
LET COEFR1 = MATRIX DEFINITION SADIAG1 P1 P2
DELETE TEMP
LET TEMP = MATRIX DIAGONAL SD
LET TEMP = 1/TEMP
LET SDDIAG = DIAGONAL MATRIX TEMP
LET RD = MATRIX TRANSPOSE RD
LET RD = MATRIX MULTIPLY RD REV
LET COEFR2 = MATRIX MULTIPLY SDDIAG RD
CAPTURE ZZZZJUNK.DAT
PRINT " "
PRINT "CANANICAL CORRELATIONS AND WILKS LAMBDA"
PRINT SV WILK FOR I = 1 1 P2
PRINT " "
PRINT "GROUP 1 CANNONICAL COEFFICIENTS"
PRINT GAMMA1
PRINT " "
PRINT "GROUP 2 CANNONICAL COEFFICIENTS"
PRINT GAMMA2
PRINT " "
PRINT "CORRELATIONS BETWEEN GROUP 1 VARIABLES AND "
PRINT "AND GROUP 1 CANONICAL SCORES"
PRINT COEFR1
PRINT " "
PRINT "CORRELATIONS BETWEEN GROUP 2 VARIABLES AND "
PRINT "AND GROUP 2 CANONICAL SCORES"
PRINT COEFR2
END OF CAPTURE
.
FEEDBACK ON
LIST ZZZZJUNK.DAT
