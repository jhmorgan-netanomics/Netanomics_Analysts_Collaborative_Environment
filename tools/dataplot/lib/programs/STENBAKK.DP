. THIS IS DATAPLOT PROGRAM FILE     STENBAKK.DP
. ANALYSIS OF JERRY STENBAKKEN A TO D CONVERTER TEST DEVICE
. JERRY STENBAKKEN (2440)
. JANUARY 1990
. VERSION OF DATAPLOT NEEDED--90.2 OR HIGHER
.
. -----START POINT-----
.
DIMENSION 20 VARIABLES
DEVICE 2 TEKT 4014
X3LABEL AUTOMATIC
.
. STEP 1--READ IN THE DATA
.
SET READ FORMAT F10.0
SKIP 25
READ STENBAKK.DAT Y
.
. STEP 2--CREATE 10 BINARY VARIABLES
.
LET INDEX = 1 1 1024
LOOP FOR K = 1 1 10
LET X^K = BINPAT(INDEX,K)
END OF LOOP
.
. STEP 3--PLOT THE RAW DATA AND DEX STAT PLOTS
.
MULTIPLOT 4 1
PLOT Y
DEX MEAN PLOT Y X1 TO X10
DEX SD PLOT Y X1 TO X10
DEX SN PLOT Y X1 TO X10
.
. STEP 4--GENERATE BOX PLOTS
.
MULTIPLOT 3 4
LOOP FOR K = 1 1 10
BOX PLOT Y X^K
END OF LOOP
.
. STEP 5--CARRY OUT YATES ANALYSIS
.
CAPTURE STENBAK2.OUT
.
YATES OUTPUT 3
YATES T CUTOFF 3
YATES Y
.
. STEP 6--CARRY OUT FITS AND PLOT RESIDUALS
.
MULTIPLOT 4 1
LET S = STANDARD DEVIATION Y
LEGEND 1 S = ^S
PLOT Y
YLIMITS FREEZE
.
FIT Y X9 X8
LEGEND 1 ^RESSD
PLOT RES
.
FIT Y X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
LEGEND 1 RESSD = ^RESSD
PLOT RES
.
LET X11 = X8*X9
LET X12 = X8*X10
LET X13 = X9*X10
LET X14 = X8*X9*X10
FIT Y X1 X2 X3 X4 X5 X6 X7 X8 X9 X10 X11 X12 X13 X14
LEGEND 1 RESSD = ^RESSD
PLOT RES
.
. STEP 7--WRITE OUT FACTOR VALUES YIELDING EXTREME VALUES OF Y
.
SET WRITE DECIMALS 2
WRITE Y X1 TO X5 SUBSET Y > 1.5
WRITE Y X6 TO X10 SUBSET Y > 1.5
.
END OF CAPTURE
.
LIST STENBAK2.OUT
.
. STEP 8--FORM AN ORTHOGONAL FRACTIONAL FACTORIAL SUBSET OF Y
.         CONSISTING OF ONLY N = 64 OBSERVATIONS
.         (THIS WILL BE A 2**(10-4) DESIGN)
.
DELETE X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
SKIP 100
SET READ FORMAT
READ 2TO10M4.DAT X1 TO X10
LOOP FOR K = 1 1 10
LET X^K = 0 SUBSET X^K -1
END LOOP
LET SUM = 0 FOR I = 1 1 64
LOOP FOR K = 1 1 10
LET FUDGE = 2**(K-1)
LET SUM = SUM+FUDGE*X^K
END LOOP
LET INDEX2 = SUM+1
LET Y2 = RANDOM SAMPLE Y INDEX2
.
. STEP 9--ANALYZE THE FRACTIONAL FACTORIAL SUBSET
.
MULTIPLOT 4 1
YLIMITS
PLOT Y2
DEX MEAN PLOT Y2 X1 TO X10
YATES Y2
TAB X1
TAB X8
FIT Y X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
LEGEND 1 ^RESSD
PLOT RES
