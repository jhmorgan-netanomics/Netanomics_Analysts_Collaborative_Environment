. THIS IS DATAPLOT PROGRAM LINUT2.DP
. GENERATE PHD PLOTS FOR KER-CHAU LI TECHNOMETRICS PAPER
. FOR THE CLEMENT NUT EXAMPLE
. DATE--NOVEMBER 1992
.
. -----START POINT----------
.
DIMENSION 50 VARIABLES
DEVICE 2 POSTSCRIPT
.
. Step 1--Read in the data
 
SKIP 25
READ LINUT.DAT Y X1 X2 X3
.
SKIP 0
READ MATRIX B
0 6.75 17.75
6.75 0 1
17.75 1 0
END OF DATA
.
. Step 2--Compute pHd's
.
LET V = EIGENVECTORS B
LET E = EIGENVALUES B
.
LET W1 = V3
LET W2 = V1
LET W3 = V2
LET F(1) = E(3)
LET F(2) = E(1)
LET F(3) = E(2)
.
LET W = MATRIX DEFINITION W1 3 3
.
SKIP 25
READ NUT.DAT Y X1 X2 X3
LET X = MATRIX DEFINITION X1 8 3
LET PHD = MATRIX MULTIPLICATION X W
.
. Step 3--Compute residuals from main effects model
.
LET PRED = 101.5+0.5*(-2*X1+2.5*X2-7.5*X3)
LET RES = Y-PRED
.
. Step 4--Generate plot 1--residuals versus factors
.
MULTIPLOT 2 3
FEEDBACK OFF
CHAR BLANK ALL
CHAR SIZE 4 ALL
LINES SOLID ALL
LABEL SIZE 4
FRAME CORNER COORDINATES 0 20 100 90
LEGEND 1 COORDINATES 5 85
LEGEND 1 SIZE 4
TIC LABELS SIZE 4
Y1LABEL Residuals
LET TAG1 = PATTERN 1 1 2 2 3 3 4 4
LET TAG2 = PATTERN 1 2 1 2 3 4 3 4
LET TAG3 = PATTERN 1 2 3 4 1 2 3 4
X1LABEL X1; PLOT RES X1 TAG1
Y1LABEL ; Y1TICS OFF; Y1TIC LABELS OFF
X1LABEL X2; PLOT RES X2 TAG2
X1LABEL X3; PLOT RES X3 TAG3
.
MULTIPLOT OFF
JUSTIFICATION CENTER
HW 2.5 1.5
MOVE 50 90
TEXT Automotive Fracture Force Study
MOVE 50 50
HW 2 1
TEXT Figure 4. Scatter plot of residuals (with pairs connected)
TEXT           Automotive fracture force example.
.
. Step 5--Generate plot 2--residuals versus pHd's
MULTIPLOT 2 3
CHAR - +
CHAR SIZE 4 ALL
LINES BLANK ALL
LABEL SIZE 4
FRAME CORNER COORDINATES 0 20 100 90
LEGEND 1 COORDINATES 5 85
LEGEND 1 SIZE 4
TIC LABELS SIZE 4
Y1LABEL Residuals ; X1Label pHd1
LEGEND 1 X1; PLOT RES PHD1 X1
Y1LABEL ; Y1TICS OFF; Y1TIC LABELS OFF
LEGEND 1 X2; PLOT RES PHD1 X2
LEGEND 1 X3; PLOT RES PHD1 X3
Y1LABEL Residuals ; X1Label pHd2
Y1TICS ON; Y1TIC LABELS ON
LEGEND 1 X1; PLOT RES PHD2 X1
Y1LABEL ; Y1TICS OFF; Y1TIC LABELS OFF
LEGEND 1 X2; PLOT RES PHD2 X2
LEGEND 1 X3; PLOT RES PHD2 X3
.
MULTIPLOT OFF
JUSTIFICATION CENTER
HW 2.5 1.5
MOVE 50 90
TEXT Automotive Fracture Force Study
MOVE 50 15
HW 2 1
TEXT Figure 7. Plots of residuals versus pHd's (with each factor coded).
TEXT           Automotive fracture force example.
.
. Step 4--Generate plot 3--pHd scatter plots
.
MULTIPLOT 2 3
FRAME CORNER COORDINATES 0 20 100 90
LEGEND 1 COORDINATES 5 85
Y1TICS ON; Y1TIC LABELS ON
Y1Label pHd1 ; X1Label pHd2
LEGEND 1 X1; PLOT PHD1 PHD2 X1
Y1LABEL ; Y1TICS OFF; Y1TIC LABELS OFF
LEGEND 1 X2; PLOT PHD1 PHD2 X2
LEGEND 1 X3; PLOT PHD1 PHD2 X3
.
MULTIPLOT OFF
JUSTIFICATION CENTER
HW 2.5 1.5
MOVE 50 90
TEXT Automotive Fracture Force Study
HW 2 1
MOVE 50 50
TEXT Figure 8. pHd scatter plots (with each factor coded).
TEXT           Automotive fracture force example.
