. THIS IS THE DATAPLOT PROGRAM FILE     BERGER1.DP
. PURPOSE--DO TRANSFORMATIONS & FITS FOR ALASKA PIPELINE DATA
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
FEEDBACK OFF
.
SKIP 25
READ BERGER1.DAT X Y
.
MULTIPLOT CORNER COORDINATES 10 10 90 85
MULTIPLOT 2 3
LABEL SIZE 4
TITLE SIZE 4
TITLE OFFSET 2
.
. ----------PAGE 1----------
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
XLABEL PLOT Y X
PLOT Y X
.
XLABEL
TITLE 2. MODEL FITTING
PLOT
CAPTURE JUNK1.
LINEAR FIT Y X
LET PRED1 = PRED
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION
CHAR X BLANK
LINES BLANK SOLID
XLABEL PLOT Y PRED VERSUS X
PLOT Y PRED VERSUS X
.
XLABEL PLOT RES X
PLOT RES X
.
XLABEL NORMAL PROBABILITY PLOT RES
NORMAL PROBABILITY PLOT RES
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ALASKA PIPELINE CALIBRATION
.
. ----------PAGE 2----------
.
MULTIPLOT 2 2
TITLE 1. MODEL SELECTION
XLABEL X
.
Y1LABEL Y
PLOT Y X
.
LET Y2 = SQRT(Y)
Y1LABEL SQRT(Y)
PLOT Y2 X
.
LET Y2 = LOG(Y)
Y1LABEL LOG(Y)
PLOT Y2 X
.
LET Y2 = 1/Y
Y1LABEL 1/Y
PLOT Y2 X
.
Y1LABEL
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ALASKA PIPELINE CALIBRATION
TEXT WHAT TRANSFORMATION FOR Y?
.
. ----------PAGE 3----------
.
MULTIPLOT 2 2
TITLE 1. MODEL SELECTION
LET Y2 = LOG(Y)
Y1LABEL LOG(Y)
.
XLABEL X
PLOT Y2 X
.
LET X2 = SQRT(X)
XLABEL SQRT(X)
PLOT Y2 X2
.
LET X2 = LOG(X)
XLABEL LOG(X)
PLOT Y2 X2
.
LET X2 = 1/X
XLABEL 1/X
PLOT Y2 X2
.
XLABEL
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT ALASKA PIPELINE CALIBRATION
TEXT WHAT TRANSFORMATION FOR X?
.
. ----------PAGE 4----------
.
MULTIPLOT 2 2
LABEL SIZE 4
TITLE SIZE 4
TITLE OFFSET 2
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
XLABEL PLOT Y2 X2
LET Y2 = LOG(Y)
LET X2 = LOG(X)
PLOT Y2 X2
.
XLABEL
TITLE 2. MODEL FITTING
PLOT
CAPTURE JUNK2.
LINEAR FIT Y2 X2
LET PRED2 = PRED
LET PRED3 = EXP(PRED2)
LET RES3 = Y-PRED3
LET RES3SQ = RES3*RES3
LET SUM = SUM RES3SQ
LET N = NUMBER Y
LET RATIO = SUM/(N-2)
LET RESSD3 = SQRT(RATIO)
WRITE " "
WRITE "RESSD BACK IN ORIGINAL UNITS = ^RESSD3 "
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION
CHAR X BLANK
LINES BLANK SOLID
XLABEL PLOT Y2 PRED VERSUS X2
PLOT Y2 PRED VS X2
.
XLABEL PLOT RES X2
PLOT RES X2
.
XLABEL NORMAL PROBABILITY PLOT RES
NORMAL PROBABILITY PLOT RES
.
. ----------PAGE 5----------
.
MULTIPLOT OFF
TITLE COMPARISON OF 2 MODELS (ALASKA PIPELINE CALIB.)
Y1LABEL Y
XLABEL X
CHAR X BLANK BLANK
LINES BLANK SOLID DOTTED
LET PRED2B = EXP(PRED2)
PLOT Y PRED1 PRED2B VERSUS X
.
