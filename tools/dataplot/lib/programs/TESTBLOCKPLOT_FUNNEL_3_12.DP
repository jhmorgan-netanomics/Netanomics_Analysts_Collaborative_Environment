. This is dataplot macro    testblockplot_funnel_3_12.dp   4/10/19
. Purpose: Do block plot (k = 1+2 = 3, n = 12) for DEX Class funnel example
. Design: Randomized Block Design (k=3,n=12) (and == Full Factorial Design)
.         Primary Factor:    Ball Size  (2)
.         Robustness Factor: Funnel     (2)
.                            Ramp       (3)
. Date: 3/10/19

.
. -----start point-----
.
let string header = DEX Class Funnel Traversal Time (k=3,n=12) (Comparative)
let string headhw1 = 3 2
let string headhw2 = 2.6 1.3
let string headhw3 = 2.6 1.3
let string currdate = 3/10/19
.
let pagecoun = 0
let string trailer = testblockplot_funnel_3_12.dp
let string trailer2 = Datafile = funnel_3_12.dat
.
. -----Step 1: Read in the data-----
.
let k = 3
let n = 12
.
skip 25
read funnel_3_12.dat y x1 x2 x3
stat v
.
. -----Step 2: Define strings-----
.
let string sty  = Traversal Time
let string stx1 = Ball Size
let string stx2 = Funnel
let string stx3 = Ramp
.
let string stxsh1 = Ball
let string stxsh2 = Funnel
let string stxsh3 = Ramp
.
. -----Step 3: Define needed vectors-----
.
let x1c = code x1
let x2c = code x2
let x3c = code x3
.
. -----Step 10: Do preliminary dex scatter plots and dex mean plots-----
.
let string header2 = Q. What is the Relative Importance of the Factors?
call dexsp.dp
call annotate.dp
.
call dexmp.dp
call annotate.dp
.
. -----Step 11: Generate 6 => 4 block plots for each of the k=3 primary factors-----
.
feedback off
.
let numrob = k-1
loop for case = 1 1 k
   delete xprim; let xprim = x^case
   let string stprim = ^stx^case
   let string header2 = Q. Does X^case = ^stx^case  Have an Effect on ^sty?
   .
   if case = 1
      let rid1 = 2; let rid2 = 3
      char bl all; char 1 2; char color blue red ^darkgree
      lines solid all; lines bl bl
      let string stlegend = X^case: #Components (2 levels: 1, 2)
      let string stevent = W2 < W1; let lower = 2; let upper = 1
      let string corncol = blue
   end if
   .
   if case = 2
      let rid1 = 1; let rid2 = 3
      char bl all; char 1 2; char color blue red ^darkgree
      lines solid all; lines bl bl
      let string stlegend = X^case: #Components (2 levels: 1, 2)
      let string stevent = Pl2 < Pl1; let lower = 2; let upper = 1
      let string corncol = red
   end if
   .
   if case = 3
      let rid1 = 1; let rid2 = 2
      char bl all; char 1 2 3; char color blue red ^darkgree
      lines solid all; lines bl bl bl
      let string stlegend = X^case: #Components (3 levels: 1, 2, 3)
      let string stevent = Sp2 < Sp1; let lower = 2; let upper = 1
      let string corncol = ^darkgree
   end if
   .
      delete tag cond
   loop for jrob = 1 1 numrob
      delete xrob^jrob; let xrob^jrob = x^rid^jrob
   end loop
   .
   if numrob = 1; let tag = x^rid1&c; end if
   if numrob = 2; let tag = 100*(x^rid1&c-1) + x^rid2&c; end if
   .
   let tag = code tag
   let condp = tag
   delete cond; let cond = condp
   x2label Robustness Conditions (X^rid1,X^rid2): (^stx^rid1,^stx^rid2)
   .
   let dcondp = distinct condp
   let ndcondp = number dcondp
   .
   y1label ^sty
   x3label ^stbott
   xlim 1 ndcondp
   x1tics off
   x1tic labels off
   y1tic offset units screen; y1tic offset 5 20
   x1tic offset units screen; x1tic offset 5 5
   if ndcondp <= 5; x1tic offset units screen; x1tic offset 10 10; end if
   .
   . call blockplot_6plots.dp
   call blockplot_4plots.dp
   .
   x1tic offset units screen; x1tic offset 5 5
end loop

