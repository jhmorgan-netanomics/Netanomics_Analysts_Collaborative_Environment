. THIS IS THE DATAPLOT PROGRAM FILE     SCHAUER3.DP
. PURPOSE--GENERATE SCATTER PLOT WITH MEAN LINE
. EPR BONE DOSIMETRY
. DAVE SCHAUER, NIST
. DATE--JUNE 1993
.
. -----START POINT-----
.
. DEVICE 2 POSTSCRIPT
LET STRING MARK = SCHAUER3.DP
.
. ------------------------
. STEP 1--READ IN THE DATA
.
SKIP 50
READ SCHAUER.DAT SIGNAL MASS BONE CALCDOSE ESTDOSE ...
   ENERGY SUBJECT THICKNESS
RETAIN SIGNAL MASS BONE CALCDOSE ESTDOSE ENERGY THICKNESS SUBJECT ...
   SUBSET SUBJECT 1
.
. ------------------------------------
. STEP 2--COMPUTE TRANSFORMED VARIATES
.
LET Y = ((SIGNAL/MASS)/CALCDOSE)/16
LET ENERGY2 = 1 SUBSET ENERGY 34 SUBSET THICKNESS 2
LET ENERGY2 = 2 SUBSET ENERGY 34 SUBSET THICKNESS 1
LET ENERGY2 = 3 SUBSET ENERGY 34 SUBSET THICKNESS .5
LET ENERGY2 = 4 SUBSET ENERGY 138 SUBSET THICKNESS 1
LET ENERGY2 = 5 SUBSET ENERGY 662 SUBSET THICKNESS 1
LET ENERGY2 = 6 SUBSET ENERGY 1250 SUBSET THICKNESS 1
.
. ---------------------------
. STEP 3--EXTRACT MEAN VALUES
.
MEAN PLOT Y ENERGY2
LET YMEAN = YPLOT
LET XMEAN = XPLOT
RETAIN YMEAN XMEAN FOR I = 1 1 6
.
. ---------------------------------
. STEP 4--PLOT THE DATA & MEAN LINE
.
TITLE SLC()UBJECT 1: 47-Y MALE FEMUR
Y1LABEL NLC()ORMALIZED UC()EPR SLC()IGNAL UC()ILC()NTENSITY
XLABEL ELC()NERGY (KEUC()V)
X2LABEL (LC()BONE THICKNESS IN PARENTHESES)
X1TIC LABEL FORMAT ALPHA
X1TIC LABEL CONTENTS 34(2) 34(1) 34(.5) 138(1) 662(1) 1250(1)
CHAR X CIRCLE
LINES BLANK SOLID
CHAR SIZE 1 1
CHAR FILL ON ON
YLIMITS .5 1
.
PLOT Y ENERGY2 AND
PLOT YMEAN XMEAN
CALL MARK.DP
