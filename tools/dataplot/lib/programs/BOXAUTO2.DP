. THIS IS THE DATAPLOT PROGRAM FILE     BOXAUTO2.DP
. PURPOSE--ANALYZE BOX, HUNTER, & HUNTER AUTO EMISSIONS EXPERIMENT
.          (PAGE 247-250)
.
. -----START POINT---------------
.
. DEVICE 2 POSTSCRIPT
FEEDBACK OFF
. PROMPT ON
TIC OFFSET UNITS SCREEN
TIC OFFSET 10 10
.
LET STRING ST1 = STEP 1:
LET STRING ST2 = PREVIEW DATA
CALL BOXAUTO3.DP
.
ER
WRITE "STEP 1: PREVIEW DATA"
WRITE " "
WRITE " "
LIST BOXAUTO2.DAT
.
SKIP 30
READ BOXAUTO.DAT Y ADDITIVE CAR DRIVER
.
LET STRING ST1 = STEP 2:
LET STRING ST2 = GANOVA
CALL BOXAUTO3.DP
.
LEGEND 1 JUSTIFICATION CENTER
LEGEND 1 COORDINATES 50 84
LEGEND 1 STEP 2: GANOVA
X3LABEL AUTOMATIC
CHAR 1 2 3 4
LINES BLANK ALL
MULTIPLOT 1 2
PLOT Y ADDITIVE CAR
PLOT Y ADDITIVE DRIVER
PAUSE
MULTIPLOT 1 2
PLOT Y CAR ADDITIVE
PLOT Y CAR DRIVER
PAUSE
MULTIPLOT 1 2
PLOT Y DRIVER ADDITIVE
PLOT Y DRIVER CAR
PAUSE
MULTIPLOT OFF
.
LET STRING ST1 = STEP 3:
LET STRING ST2 = MEANS ANALYSIS
CALL BOXAUTO3.DP
.
LEGEND 1 STEP 3: MEANS ANALYSIS
CHAR CIRCLE BLANK
CHAR FILL ON OFF
LINES BLANK SOLID
SPIKE ON OFF
LET YMEAN = MEAN Y
SPIKE BASE YMEAN
DEX MEAN PLOT Y ADDITIVE CAR DRIVER
PAUSE
LINES BLANK ALL
SPIKE OFF ALL
.
LET STRING ST1 = STEP 4:
LET STRING ST2 = ANOVA
CALL BOXAUTO3.DP
.
ER
WRITE "STEP 4: ANOVA"
ANOVA Y CAR ADDITIVE DRIVER
PAUSE
.
LET STRING ST1 = STEP 5:
LET STRING ST2 = RESIDUAL ANALYSIS
CALL BOXAUTO3.DP
.
LEGEND 1 STEP 5: RESIDUAL ANALYSIS
CHAR 1 2 3 4
MULTIPLOT 2 2
PLOT RES ADDITIVE
PLOT RES CAR
PLOT RES DRIVER
PAUSE
MULTIPLOT OFF
CHAR X
LEGEND 1
4-PLOT RES
PAUSE
CHAR
LINES SOLID
GRID ON
TUKEY PPCC PLOT RES
PAUSE
ER
.
EXIT
