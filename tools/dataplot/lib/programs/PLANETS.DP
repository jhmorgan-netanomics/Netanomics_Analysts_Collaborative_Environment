. THIS IS THE DATAPLOT PROGRAM FILE     PLANETS.DP
. PURPOSE--GENERATE MATRIX OF SCATTER PLOTS
.          FOR PLANETARY DATA
. DATE--MARCH 1992
.
. -----START POINT---------------
.
. DIMENSION 20 VARIABLES
. DEVICE 2 POSTSCRIPT
. LET STRING MARK = PLANETS.DP
.
. -------------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
COLUMN LIMITS 8 132
READ PLANETS.DAT Y1 TO Y10
.
. -------------------------------
. STEP 2--DEFINE NAMES OF THE VARIABLES
.
LET STRING S1 = 1. DISTANCE
LET STRING S2 = 2. DIAMETER
LET STRING S3 = 3. PERIOD
LET STRING S4 = 4. SPEED
LET STRING S5 = 5. DAY
LET STRING S6 = 6. MASS
LET STRING S7 = 7. DENSITY
LET STRING S8 = 8. GRAVITY
LET STRING S9 = 9. ESC. VEL.
LET STRING S10 = 10. TILT
.
LET N = NUMBER Y1
LET INDEX = SEQUENCE 1 1 N
.
. -------------------------------
. STEP 3--SET UP THE MULTIPLOT
DEVIOCE 1 FONT SIMPLEX
MULTIPLOT CORNER COORDINATES 5 5 95 93
MULTIPLOT 10 10
FRAME CORNER COORINDATES 0 0 100 100
TICS OFF
TIC LABELS OFF
LINES
FEEDBACK OFF
.
. -------------------------------
. STEP 4--GENERATE NAMES ON THE DIAGONAL
.
LEGEND 1 COORDINATES 50 50
LEGEND 1 JUSTIFICATION CENTER
LEGEND 1 SIZE 15
CHAR
LOOP FOR K = 1 1 10
MULTIPLOT 10 10 K K
LEGEND 1 ^S^K
PLOT Y1
END LOOP
LEGEND 1
.
. -------------------------------
. STEP 5--GENERATE MATRIX OF SCATTER PLOTS
.
CHAR H V E M J S U N P
CHAR SIZE 10 ALL
LOOP FOR K = 1 1 9
LET KP1=K+1
LOOP FOR L = KP1 1 10
MULTIPLOT 10 10 K L
PLOT Y^K Y^L INDEX
END LOOP
END LOOP
.
. -------------------------------
. STEP 6--WRITE A TITLE FOR THE PAGE
.
MULTIPLOT OFF
HW 3 2
MOVE 50 95
JUSTIFICATION CENTER
TEXT ANALYSIS OF PLANETARY DATA
.
. CALL MARK.DP
RESET PLOT CONTROL
