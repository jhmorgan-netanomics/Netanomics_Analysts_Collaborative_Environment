. THIS IS THE DATAPLOT PROGRAM FILE     FILTER.DP
. PURPOSE--ASSESS THE STABILITY OF A LINEAR RECURSIVE DIGITAL FILTER
.          Y(I) = SUM(1,M) C(J)*X(I-J)   +   SUM(1,N) D(J)*Y(I-J)
. ANALYSIS TECHNIQUE--COMPLEX ROOTS OF POLYNOMIAL
. APPLICATION--DIGITAL FILTER DESIGN
. SOURCE (FOR PROBLEM)--PRESS, WILLIAM H, FLANNERY, BRIAN, P.,
.                       TEUKOLSKY, SAUL A., AND VETTERLING, WILLIAM T.,
.                       NUMERICAL RECIPES--THE ART OF SCIENTIFIC COMPUTING,
.                       CAMBRIDGE UNIVERSITY PRESS, 1986, PAGES 439-440.
. NOTE--IN A STABLE FILTER, THE OUTPUT WILL EVENTUALLY STOP
.       (AFTER M STEPS) WHEN THE INPUT STOPS.
.       IN AN UNSTABLE FILTER, STOPPING THE INPUT
.       IN AN UNSTABLE FILTER, THE OUTPUT MAY GROW EXPONENTIALLY
.       EVEN AFTER STOPPING THE INPUT.
. NOTE--A FILTER IS STABLE IF AND ONLY IF
.       THE N COMPLEX ROOTS OF
.       X**N - D1*X**(N-1) - D2*X**(N-2) - ... - DN = 0
.       ALL FALL INSIDE OR ON THE UNIT CIRCLE.
. NOTE--FOR TESTING PURPOSES, THE FILTER 16 8 4 2 1
.       WILL PROVE TO BE UNSTABLE.
.       AND EXAMPLE OF A STABLE FILTER IS 1 -1 .4
. DATE--OCTOBER 1987
.
. -----START POINT-----------------------------------
.
ECHO
.
. STEP 1--
. DEFINE THE FILTER COEFFICIENTS
. 16 IS THE WEIGHT FOR Y(I-1), 8 IS THE WEIGHT FOR Y(I-2), ETC.
.
SERIAL READ D
16 8 4 2 1
END OF DATA
.
. STEP 2--
. REARRANGE TO FORM A POLYNOMIAL WITH COEFFICIENTS -1 -2 -4 -8 -16 +1
.
LET N = NUMBER D
LOOP FOR I = 1 1 N
LET IREV = N-I+1
LET DIREV = D(IREV)
LET P(I) = DIREV
END OF LOOP
LET P = -P
LET NP1 = N+1
LET P(NP1) = 1
PRINT P
.
. STEP 3--
. COMPUTE THE COMPLEX ROOTS
.
LET X Y = COMPLEX ROOTS P
PRINT X Y
.
. STEP 4--
. PLOT THE ROOTS AND A UNIT CIRCLE
.
LINES BLANK SOLID SOLID
CHAR X BLANK BLANK
PLOT Y X AND
PLOT SQRT(1-X**2) FOR X = -1 .01 1 AND
PLOT -SQRT(1-X**2) FOR X = -1 .01 1
