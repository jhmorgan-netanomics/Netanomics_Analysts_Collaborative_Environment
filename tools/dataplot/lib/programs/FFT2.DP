. THIS IS THE DATAPLOT PROGRAM FILE     FFT2.DP
. PURPOSE--TRANSFORM AND DECONVOLVE A OUTPUT CORRUPTED SIGNAL
.          AND AN INSTRUMENT'S REPONSE FUNCTION
.          SO AS TO DETERMINE THE UNCORRUPTED INPUT SIGNAL.
. ANALYSIS TECHNIQUE--FFT, CONVOLUTION, DECONVOLUTION
.
. -----START POINT-----------------------------------
.
ECHO
DIMENSION 20 VARIABLES
.
.      STEP 1--
.      DEFINE THE PURE SIGNAL
.
LET X = SEQUENCE 1 1 256
LET PURE = 0 FOR I = 1 1 256
LET PURE = 1 FOR I = 100 1 120
LET PURE = 1 FOR I = 130 1 150
.
.      STEP 2--
.      DEFINE THE INSTRUMENT (= BOX) RESPONSE FUNCTION
.
LET WIDTH = 20
LET BOX = EXP(-((X-128)/WIDTH)**2)
.
.      STEP 3--
.      COMPUTE THE OBSERVED RESPONSE.
.      AS A CONVOLUTION OF PURE AND BOX.
.      PLOT ALL SO FAR.
.
LET CORRUPTED = CONVOLUTION PURE BOX
DELETE CORRUPTED FOR I = 1 1 128
DELETE CORRUPTED FOR I = 257 1 1000
MULTIPLOT 2 2
PLOT PURE
PLOT BOX
PLOT CORRUPTED
.
.      STEP 4--
.      PRIOR TO THE FFT,
.      SPLIT THE BOX AND WRAP IT AROUND
.
LET BOX2 = EXP(-(X/WIDTH)**2) SUBSET X <= 128
LET BOX2 = EXP(-((X-256)/WIDTH)**2) SUBSET X > 128
PLOT BOX2
.
.      STEP 5--
.      COMPUTE THE FFT OF THE RESPONSE
.      COMPUTE THE FFT OF THE BOX
.
LET U1 V1 = FFT CORRUPTED
LET U2 V2 = FFT BOX
MULTIPLOT 2 2
PLOT U1
PLOT V1
PLOT U2
PLOT V2
.
.      STEP 6--
.      CARRY OUT A COMPLEX DIVISION
.      OF THE RESPONSE FFT BY THE
.      BOX FFT
.
LET PROD2 = U2*V2
LET U3 V3 = COMPLEX DIVISION U1 V1 U2 V2 EXCEPT PROD2 = 0
.
.      STEP 7--
.      TRANSFORM BACK
.
LET XPRIME YPRIME = INVERSE FFT U3 V3
.
.      STEP 8--
.      PLOT AND COMPARE
.
MULTIPLOT 2 2
PLOT CORRUPTED
PLOT PURE
PLOT XPRIME
PLOT YPRIME
