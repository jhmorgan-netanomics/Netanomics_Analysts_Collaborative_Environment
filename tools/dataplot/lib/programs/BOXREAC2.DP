. THIS IS THE DATAPLOT PROGRAM FILE     BOXREAC2.DP
. EFFICIENCY OF CHEMICAL REACTOR
. BOX, HUNTER, & HUNTER, P. 376-390
. COMPLETE ANALYSIS OF 2**(5-1) DESIGN
. INPUT DATA FILE--BOXREAC2.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. -----START POINT----------
.
DIMENSION 40 VARIABLES
DEVICE 2 POSTSCRIPT
.
. STEP 1--READ IN THE DATA
.
SKIP 25
READ BOXREAC2.DAT Y X1 X2 X3 X4 X5 RUNSEQ
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
ER
MULTIPLOT OFF
X3LABEL
LET NUMFAC = 5
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 50 80
LET STRING S0 = % REACTED
LET STRING S1 = FEED RATE (X1)
LET STRING S2 = CATALYST (X2)
LET STRING S3 = AGITATION RATE (X3)
LET STRING S4 = TEMPERATURE (X4)
LET STRING S5 = CONCENTRATION (X5)
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 3 4
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 3 ALL; LABEL SIZE 3; TIC LABEL SIZE 3
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3 X1
BLOCK PLOT Y X4 X5 X1
BLOCK PLOT Y X1 X3 X2
BLOCK PLOT Y X4 X5 X2
BLOCK PLOT Y X1 X2 X3
BLOCK PLOT Y X4 X5 X3
BLOCK PLOT Y X2 X3 X4
BLOCK PLOT Y X1 X5 X4
BLOCK PLOT Y X1 X3 X5
BLOCK PLOT Y X2 X4 X5
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
ER
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B2 = COEF(1)
LET B4 = COEF(2)
LET B24 = COEF(3)
LET B45 = COEF(4)
LET B5 = COEF(5)
LET RESSD = RESSD2(5)
LET PRED = MU+0.5*(B1*X1+B2*X2)
LET PRED = MU + 0.5*(B2*X2+B4*X4+B24*X2*X4+B45*X4*X5+B5*X5)
LET RES = Y-PRED
ER
SET WRITE DECIMALS 2
WRITE X1 X2 X3 X4 X5 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU+0.5*(^B2*X2+^B4*X4+^B24*X2*X4+^B45*X4*X5+^B5*X5)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
LABEL SIZE 3
PLOT RES RUNSEQ
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 50 60 70 80 90 100
LET U1 = X2
LET U2 = X4
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL CATALYST (X2)
Y1LABEL TEMPERATURE (X4)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
ER
LET YSORT = SORTC Y X1 X2 X3 X4 X5 RUNSEQ
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 X5 RUNSEQ YSORT
WRITE OUT3. X1 X2 X3 X4 X5 RUNSEQ YSORT
.
