. THIS IS THE DATAPLOT PROGRAM FILE     DEXSCREE.DP
. PURPOSE--CARRY OUT GRAPHICAL ANALYSIS
.          OF "SCREENING" DESIGNS
. THIS PROGRAM WILL--
.   1) READ IN DATA
.   2) GENERATE MAIN EFFECTS SCATTER AND MEAN PLOTS
.   3) GENERATE MAIN AND 2-FACTOR MEAN PLOTS
.   4) GENERATE POST-FIT CONTOUR PLOT
. DATE--DECEMBER 1989
.
. ------START POINT------------------------
.
DIMENSION 20 VARIABLES
DEVICE 2 POSTSCRIPT
FEEDBACK OFF
.
. ----------------------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
READ BOXREAC2.DAT Y X1 TO X5
.
. ----------------------------------------
. STEP 2--GENERATE THE MAIN EFFECTS SCATTER PLOT
.         AND MAIN EFFECTS MEAN PLOT
.
X3LABEL AUTOMATIC
MULTIPLOT 4 1
LINES BLANK ALL
CHAR X BLANK
DEX SCATTER PLOT Y X1 TO X5
LINES SOLID ALL
CHAR BLANK ALL
DEX MEAN PLOT Y X1 TO X5
MULTIPLOT OFF
X3LABEL
.
. -------------------------------------
. STEP 3--GENERATE THE MAIN AND 2-FACTOR MEAN PLOTS
.
LET NUMFAC = 5
LET STRING STAT = MEAN
LET DP = 2
YLIMITS 40 100
CHAR X BLANK
LINES SOLID DOTTED
FONT SIMPLEX
LEGEND SIZE 5
CALL DEXSTAT2.DP
FONT
.
. -----------------------------------------------
. STEP 4--GENERATE A CONTOUR PLOT
.
LET U1 = X2
LET U2 = X4
LET Y2 = SEQUENCE 40 5 100
XLABEL X2
Y1LABEL X4
CALL DEXCONT.DP
.
