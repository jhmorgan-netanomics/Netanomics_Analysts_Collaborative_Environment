. THIS IS THE DATAPLOT PROGRAM FILE     POLYROOTS2.DP
. PURPOSE--DETERMINE THE COMPLEX ROOTS OF A SUM OF 20 QUINTICS
.          WHERE THE K-TH QUINTIC IS OF THE FORM (1 + (1/K)*X)**5
. ANALYSIS TECHNIQUE--POLYNOMIAL ARITHMETIC +
.                     LAGUERRE'S METHOD FOR FINDING COMPLEX ROOTS
.                     OF COMPLEX POLYNOMIALS
.                     (ALTHOUGH IN THIS CASE, THE POLYNOMIAL IS REAL).
. APPLICATION--FILTER DESIGN, PROBABILITY THEORY
. SOURCE (FOR ALGORITHM)--PRESS, WILLIAM H, FLANNERY, BRIAN, P.,
.                         TEUKOLSKY, SAUL A., AND VETTERLING, WILLIAM T.,
.                         NUMERICAL RECIPES--THE ART OF SCIENTIFIC COMPUTING,
.                         CAMBRIDGE UNIVERSITY PRESS, 1986, PAGES 263-266.
. NOTE--FOR TESTING PURPOSES--
.       THE RESULTING POLYNOMIAL HAS COEFFICIENTS--
.             0.2000000E+02
.             0.1798870E+02
.             0.1596163E+02
.             0.1200868E+02
.             0.5411423E+01
.             0.1036926E+01
.       WITH COMPLEX ROOTS--
.            -0.2387324E+01  0.0000000E+00 (DOUBLE ROOT)
.            -0.1639799E+01 -0.1343364E+01
.            -0.1639799E+01  0.1343364E+01
.             0.2241036E+00 -0.1322019E+01
.             0.2241036E+00  0.1322019E+01
.
. -----START POINT-----------------------------------
.
ECHO
DIMENSION 100 VARIABLES
.
LET N = 20
.
.      STEP 1--
.      DEFINE THE 20 MONOMIALS
.
ECHO OFF
FEEDBACK OFF
LOOP FOR K = 1 1 N
LET M^K (1) = 1
LET M^K (2) = 1/K
END OF LOOP
FEEDBACK ON
PRINT M1 M2 M3 M4 M5
.
.      STEP 2--
.      DEFINE THE 20 POLYNOMIALS
.
FEEDBACK OFF
LOOP FOR K = 1 1 N
LET P^K = POLYNOMIAL MULTIPLICATION M^K M^K
LET P^K = POLYNOMIAL MULTIPLICATION P^K M^K
LET P^K = POLYNOMIAL MULTIPLICATION P^K M^K
LET P^K = POLYNOMIAL MULTIPLICATION P^K M^K
END OF LOOP
FEEDBACK ON
PRINT P1 P2 P3 P4 P5
.
.      STEP 3--
.      COMPUTE THE SUM OF THE 20 POLYNOMIALS
.
LET PSUM = DATA 0 0 0 0
FEEDBACK OFF
LOOP FOR K = 1 1 N
LET PSUM = POLYNOMIAL ADDITION PSUM P^K
END OF LOOP
ECHO ON
FEEDBACK ON
PRINT PSUM
.
.      STEP 4--
.      PLOT THE FUNCTION
.
LET X = SEQUENCE -5 .1 5
LET Y = POLYNOMIAL EVALUATION PSUM X
PLOT Y X
.
.      STEP 5--
.      FIND THE COMPLEX ROOTS
.
LET X2 Y2 = COMPLEX ROOTS PSUM
.
.      STEP 6--
.      PLOT THE COMPLEX ROOTS
.
CHAR X
LINES
GRID ON
PLOT Y2 X2
