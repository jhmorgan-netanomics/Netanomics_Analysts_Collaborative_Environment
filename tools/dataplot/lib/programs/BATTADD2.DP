. THIS IS THE DATAPLOT PROGRAM FILE     BATTADD2.DP
. PURPOSE--ANALYSIS #2 OF BATTERY ADDITIVE DATA
. NIST EDA CLASS
. APRIL 1992
.
. -----START POINT----------
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = BATTADD2.DP
.
. --------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
READ BATTADD.DAT Y ADDITIVE BATTERY
.
. ------------------------------
. STEP 2--DO BLOCK PLOT FOR ADDITIVE
.
FEEDBACK OFF
TITLE BATTERY ADDITIVE ANALYSIS
TITLE DISPLACEMENT 2
Y1LABEL WATT-HOUR
XLABEL BATTERY
X3LABEL AUTOMATIC
.
CHAR BL ALL
LINES SO ALL
CHAR 1 2 3
LINES BL BL BL
XLIMITS 19 30
YLIMITS 0 15
BLOCK PLOT Y BATTERY ADDITIVE
MOVE 50 7
JUST CENTER
LET TAILPROB=ROUND(TAILPROB,4)
TEXT TAIL PROBABILITY = ^TAILPROB
CALL MARK.DP
.
. ------------------------------
. STEP 3--DO BLOCK PLOT FOR ADDITIVE
.         (ADDITIVES 1 AND 2 ONLY)
CHAR 1 2 BL
LINES BL BL SOLID
BLOCK PLOT Y BATTERY ADDITIVE SUBSET ADDITIVE 1 2
MOVE 50 7
JUST CENTER
LET TAILPROB=ROUND(TAILPROB,4)
TEXT TAIL PROBABILITY = ^TAILPROB
CALL MARK.DP
.
. ------------------------------
. STEP 4--GENERATE BINOMIAL PROBABILITIES
.
LET N = TRIALS
LET K = SEQUENCE 0 1 N
LET PROB = GAMMA(N+1)/(GAMMA(K+1)*GAMMA(N-K+1))
LET PROB = PROB*0.5**N
Y1LABEL PROBABILITY OF K SUCCESSES
XLABEL NUMBER OF SUCCESSESS (K)
SPIKE ON
CHAR
LINES
XLIMITS 0 N
YLIMITS
PLOT PROB K
CALL MARK.DP
.
. ------------------------------
. STEP 5--GENERATE NORMAL PROBABILITY PLOTS
.
LET Y1 = Y
LET Y2 = Y
RETAIN Y1 SUBSET ADDITIVE 1
RETAIN Y2 SUBSET ADDITIVE 2
LET DEL = Y1-Y2
.
T TEST Y1 Y2
.
CHAR X
LINES SOLID
SPIKE OFF
XLIMITS
YLIMITS
YLABEL
XLABEL
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT Y1
NORMAL PROBABILITY PLOT Y2
NORMAL PROBABILITY PLOT DEL
MULTIPLOT OFF
CALL MARK.DP
