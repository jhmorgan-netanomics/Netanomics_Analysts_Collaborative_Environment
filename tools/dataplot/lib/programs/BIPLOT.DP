. Generate a biplot (derived from the singular value factorization)
. SOURCE: "THE BIPLOT AS A DIAGNOSTIC TOOL FOR MODELS OF TWO-WAY
.         TABLES", BRANDU, GABRIEL, TECHNOMETRICS, FEB. 1978.
. DATA IS YIELDS OF COTTON, ROWS ARE VARIETY, COLUMNS ARE CENTER
DIMENSION 100 COLUMNS
READ MATRIX X
 1.55 1.26 1.41  1.78
 3.39 3.47 2.82  3.89
 1.95 1.91 1.74  2.29
10.47 9.12 9.55 17.78
 1.45 1.51 1.41  1.70
 3.72 3.55 3.09  4.27
 4.47 4.07 3.98  4.47
END OF DATA
.
.  To use your own data, replace the above matrix read with your
.  own.  The remainder of the code should be independent of the 
.  particular data set used.
.
PROBE IOPSY1
IF PROBEVAL = 1
   DEVICE 1 X11
END OF IF
DEVICE 2 POSTSCRIPT
.
LET N = SIZE X1
. FEEDBACK OFF
LET P = MATRIX NUMBER OF COLUMNS X
LOOP FOR K = 1 1 P
    LET X^K = LOG10(X^K)
END OF LOOP
LET SUM1 = MATRIX SUM X
LET GMEAN = SUM1/(N*P)
LET X = MATRIX SUBTRACTION X GMEAN
LET U S V = SINGULAR VALUE FACTORIZATION X
LET DENOM = MATRIX EUCLIDEAN NORM X
LET S1 = S(1)
LET S2 = S(2)
LET GF = (S1**2 + S2**2)/DENOM**2
.
LET B = MATRIX TRANSPOSE V 
LET U1 = U1*SQRT(S1)
LET U2 = U2*SQRT(S2)
LET B1 = B1*SQRT(S1)
LET B2 = B2*SQRT(S2)
.
LET TAG = SEQUENCE 1 1 N
LET TAG2 = SEQUENCE 1 1 P
CHARACTER CIRCLE SQUARE
CHARACTER FILL SOLID ALL
LINE BLANK ALL
TITLE BIPLOT
X1LABEL GOODNESS OF FIT = ^GF
LEGEND FILL SOLID
LEGEND FONT SIMPLEX
LEGEND 1 SQUA() - COLUMN MARKERS
LEGEND 2 CIRC() - ROW MARKERS
.
PLOT U2 U1 AND
PLOT B2 B1 
LEGEND 1
LEGEND 2
LIMITS FREEZE
PRE-ERASE OFF
CHARACTER 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20
CHARACTER OFFSET 1.2 0 ALL
PLOT U2 U1 TAG
PLOT B2 B1 TAG2
