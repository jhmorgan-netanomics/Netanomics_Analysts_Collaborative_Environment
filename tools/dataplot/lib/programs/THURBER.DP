. THIS IS THE DATAPLOT PROGRAM FILE     THURBER.DP
. PURPOSE--MERGING LOCAL FITS
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
FEEDBACK OFF
.
SKIP 25
READ THURBER.DAT Y X
.
MULTIPLOT CORNER COORDINATES 10 10 90 85
MULTIPLOT 2 3
LABEL SIZE 4
TITLE SIZE 4
TITLE OFFSET 2
.
. ----------PAGE 1----------
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
XLABEL PLOT Y X
PLOT Y X
.
XLABEL
TITLE 2. MODEL FITTING
PLOT
CAPTURE JUNK1.
LINEAR FIT Y X SUBSET X < -2
LET INT = A0
LET SLOPE = A1
LET FUNCTION F1 = INT+SLOPE*X
CONSTANT FIT Y X SUBSET X > 1.5
LET CONSTANT = A0
LET FUNCTION F2 = CONSTANT
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION
CHAR X BLANK BLANK
LINES BLANK DOTTED DOTTED
PLOT Y X AND
PLOT F1 FOR X = -3 .1 0 AND
PLOT F2 FOR X = 0 .1 3
.
TITLE WEIGHTING FUNCTIONS
CHAR BLANK ALL
LINES SOLID ALL
PLOT 1-NORCDF((X-0)/.1) FOR X = -3 .1 3 AND
PLOT 1-NORCDF((X-0)/.5) FOR X = -3 .1 3 AND
PLOT 1-NORCDF((X-0)/1) FOR X = -3 .1 3 AND
PLOT 1-NORCDF((X-0)/2) FOR X = -3 .1 3 AND
PLOT 1-NORCDF((X-0)/3) FOR X = -3 .1 3
.
MULTIPLOT OFF
JUST CENTER
MOVE 50 92
HW 2.5 1.5
TEXT EXAMPLE
TEXT SEMICONDUCTOR ELECTRON MOBILITY
.
. ----------PAGE 2----------
.
MULTIPLOT 2 3
LABEL SIZE 4
TITLE SIZE 4
TITLE OFFSET 2
.
TITLE 1. MODEL SELECTION
CHAR X
LINES
PLOT Y X
.
XLABEL
TITLE 2. MODEL FITTING
PLOT
CAPTURE JUNK2.
LET FUNCTION W = 1-NORCDF((X-MU)/SIGMA)
LET MU = 0
LET SIGMA = 2
LET FUNCTION F = W*F1 + (1-W)*F2
FIT ITERATIONS 20
FIT Y = F
END OF CAPTURE
.
MULTIPLOT 2 3 4
TITLE 3. MODEL VALIDATION
CHAR X BLANK
LINES BLANK SOLID
XLABEL
PLOT Y X AND
PLOT F FOR X = -3 .1 3
.
XLABEL PLOT RES X
PLOT RES X
.
XLABEL NORMAL PROBABILITY PLOT RES
NORMAL PROBABILITY PLOT RES
.
