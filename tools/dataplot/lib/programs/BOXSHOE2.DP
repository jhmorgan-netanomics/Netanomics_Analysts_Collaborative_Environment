. THIS IS THE DATAPLOT PROGRAM FILE     BOXSHOE2.DP
. ANALYSIS #2 OF BOX BOYS SHOES EXAMPLE
. NIST EDA CLASS
. APRIL 1992
.
. -----START POINT----------
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = BOXSHOE2.DP
.
. --------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
READ BOXSHOES.DAT Y MATERIAL BOY
.
. ------------------------------
. STEP 2--DO BLOCK PLOT FOR MATERIAL
.
FEEDBACK OFF
TITLE BOYS SHOE MATERIAL ANALYSIS
TITLE DISPLACEMENT 2
Y1LABEL WEAR
XLABEL BOY
X3LABEL AUTOMATIC
.
CHAR BL ALL
LINES SO ALL
CHAR 1 2
LINES BL BL
BLOCK PLOT Y BOY MATERIAL
MOVE 50 7
JUST CENTER
LET TAILPROB=ROUND(TAILPROB,4)
TEXT TAIL PROBABILITY = ^TAILPROB
CALL MARK.DP
.
. ------------------------------
. STEP 3--GENERATE BINOMIAL PROBABILITIES
.
LET N = TRIALS
LET K = SEQUENCE 0 1 N
LET PROB = GAMMA(N+1)/(GAMMA(K+1)*GAMMA(N-K+1))
LET PROB = PROB*0.5**N
Y1LABEL PROBABILITY OF K SUCCESSES
XLABEL NUMBER OF SUCCESSESS (K)
SPIKE ON
CHAR
LINES
XLIMITS 0 N
YLIMITS
PLOT PROB K
CALL MARK.DP
.
. ------------------------------
. STEP 4--GENERATE NORMAL PROBABILITY PLOTS
.
LET Y1 = Y
LET Y2 = Y
RETAIN Y1 SUBSET MATERIAL 1
RETAIN Y2 SUBSET MATERIAL 2
LET DEL = Y1-Y2
.
T TEST Y1 Y2
.
CHAR X
LINES SOLID
SPIKE OFF
XLIMITS
YLIMITS
YLABEL
XLABEL
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT Y1
NORMAL PROBABILITY PLOT Y2
NORMAL PROBABILITY PLOT DEL
MULTIPLOT OFF
CALL MARK.DP
