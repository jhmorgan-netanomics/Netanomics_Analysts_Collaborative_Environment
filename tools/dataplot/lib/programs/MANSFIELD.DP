. THIS IS THE DATAPLOT PROGRAM FILE     MANSFIELD.DP
. SEMICONDUCTOR BOND STRENGTH
. BOX, HUNTER, & HUNTER, P. 424-429
. COMPLETE ANALYSIS OF 2**(8-4) DESIGN
. INPUT DATA FILE--MANSFIELD.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. -----START POINT----------
.
DIMENSION 40 VARIABLES
DEVICE 2 POSTSCRIPT
.
. STEP 1--READ IN THE DATA (N=16)
.
SKIP 25
READ MANSFIELD.DAT Y X1 X2 X3 X4 X5 X6 X7 X8
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5 X6 X7 X8
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5 X6 X7 X8
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
ER
MULTIPLOT OFF
X3LABEL
LET NUMFAC = 8
LET DP = 0
LET STRING STAT = MEAN
YLIMITS 600 800
LET STRING S0 = BOND STRENGTH
LET STRING S1 = SURF. ROUGH. (X1)
LET STRING S2 = CLEAN. AGENT (X2)
LET STRING S3 = GLUE DILUTION (X3)
LET STRING S4 = MOLD. COND. (X4)
LET STRING S5 = GLUE-MOLD TIME (X5)
LET STRING S6 = BLAST-GLUE TIME (X6)
LET STRING S7 = PRE-HEAT TIME (X7)
LET STRING S8 = PROT. COAT (X8)
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 4 6
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 10 ALL; LABEL SIZE 10; TIC LABEL SIZE 10
X3LABEL AUTOMATIC
MEAN BLOCK PLOT Y X2 X3 X1
MEAN BLOCK PLOT Y X4 X5 X1
MEAN BLOCK PLOT Y X6 X7 X1
MEAN BLOCK PLOT Y X1 X3 X2
MEAN BLOCK PLOT Y X4 X5 X2
MEAN BLOCK PLOT Y X6 X7 X2
MEAN BLOCK PLOT Y X1 X2 X3
MEAN BLOCK PLOT Y X4 X5 X3
MEAN BLOCK PLOT Y X6 X7 X3
MEAN BLOCK PLOT Y X1 X2 X4
MEAN BLOCK PLOT Y X3 X5 X4
MEAN BLOCK PLOT Y X6 X7 X4
MEAN BLOCK PLOT Y X1 X2 X5
MEAN BLOCK PLOT Y X3 X4 X5
MEAN BLOCK PLOT Y X6 X7 X5
MEAN BLOCK PLOT Y X2 X4 X5
MEAN BLOCK PLOT Y X1 X2 X6
MEAN BLOCK PLOT Y X3 X4 X6
MEAN BLOCK PLOT Y X5 X7 X6
MEAN BLOCK PLOT Y X1 X2 X7
MEAN BLOCK PLOT Y X3 X4 X7
MEAN BLOCK PLOT Y X5 X6 X7
MEAN BLOCK PLOT Y X1 X2 X8
MEAN BLOCK PLOT Y X3 X4 X8
MEAN BLOCK PLOT Y X5 X6 X8
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
ER
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B56 = COEF(1)
LET B6 = COEF(3)
LET B5 = COEF(6)
LET RESSD = RESSD2(3)
LET PRED = MU + 0.5*(B6*X6 + B5*X5 + B56*X5*X6)
LET RES = Y-PRED
ER
. SET WRITE DECIMALS 2
SET WRITE FORMAT 8F6.0,3F9.2
WRITE X1 X2 X3 X4 X5 X6 X7 X8 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 X6 X7 X8 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU + 0.5*(^B6*X6 + ^B5*X5 + ^B56*X5*X6)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
LABEL SIZE 3
. PLOT RES RUNSEQ
PLOT
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = SEQUENCE 500 20 900
LET U1 = X6
LET U2 = X5
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL BLAST-TO-GLUE TIME (X6)
Y1LABEL GLUE-TO-MOLD TIME (X5)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
ER
LET YSORT = SORTC Y X1 X2 X3 X4 X5 X6 X7 X8
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 X5 X6 X7 X8 YSORT
WRITE OUT3. X1 X2 X3 X4 X5 X6 X7 X8 YSORT
.
. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.
. STEP 11--RE-READ IN THE DATA (N=16) (TO REESTABLISH YATES ORDER)
.
SKIP 25
READ MANSFIELD.DAT Y X1 X2 X3 X4 X5 X6 X7 X8
.
. STEP 12--SPLIT ON SELECTED INDIVIDUAL FACTORS
.
YLIMITS 600 800
MULTIPLOT 3 2
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 -1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 +1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X6 -1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X6 +1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X8 -1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X8 +1
MULTIPLOT OFF
.
. STEP 13--SPLIT ON PAIRS OF FACTORS
.
MULTIPLOT 2 2
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 +1 SUBSET X6 -1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 +1 SUBSET X6 +1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 -1 SUBSET X6 -1
   DEX MEAN PLOT Y X1 TO X8 SUBSET X5 -1 SUBSET X6 +1
MULTIPLOT OFF
