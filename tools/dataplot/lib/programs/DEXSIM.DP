. THIS IS THE DATAPLOT PROGRAM FILE     DEXSIM.DP
. PURPOSE--CARRY OUT EXPERIMENTAL SIMULATION
. DATE--JULY 1989
.
. -----START POINT-----
.
DEVICE 2 TEKT 4014
DIMENSION 20 VARIABLES
FEEDBACK OFF
.
. ---------------------------------
. STEP 1--
. DEFINE THE MODEL
.
LET B0 = 71.25
LET B1 = 23
LET B2 = -5
LET B3 = 1.5
LET B12 = 1.5
LET B13 = 10
LET B23 = 0
LET B123 = 0.5
.
LET FUNCTION F0 = B0
LET FUNCTION F1 = B1*X1+B2*X2+B3*X3
LET FUNCTION F2 = B12*X1*X2+B13*X1*X3+B23*X2*X3
LET FUNCTION F3 = B123*X1*X2*X3
LET FUNCTION F = F0+0.5*(F1+F2+F3)
.
LET ZCONST=1.0
LET NLOOP = 100
.
. ---------------------------------
. STEP 2--
. GENERATE 1-FAT ESTIMATES OF B1
.
WRITE 'GENERATING 1-FACTOR-AT-A-TIME ESTIMATES . . .'
READ X1 X2 X3
-1 -1 -1
+1 -1 -1
-1 +1 -1
-1 -1 +1
END OF DATA
LET N = NUMBER X1
.
LOOP FOR K = 1 1 NLOOP
LET Y = F
LET Z = NORMAL RANDOM NUMBERS FOR I = 1 1 N
LET Z=ZCONST*Z
LET Y2 = Y+Z
LET Y21 = Y2(1)
LET Y22 = Y2(2)
LET Y23 = Y2(3)
LET Y24 = Y2(4)
LET MUHAT1(K) = (Y21+Y22+Y23+Y24)/4
LET B1HAT1(K) = Y22-Y21
LET B2HAT1(K) = Y23-Y21
LET B3HAT1(K) = Y24-Y21
END LOOP
.
. ---------------------------------
. STEP 3--
. GENERATE 2**(3-1) ESTIMATES OF B1
.
WRITE 'GENERATING 2**(3-1) ESTIMATES . . .'
READ X1 X2 X3
-1 -1 +1
+1 -1 -1
-1 +1 -1
+1 +1 +1
END OF DATA
LET N = NUMBER X1
.
LOOP FOR K = 1 1 NLOOP
LET Y = F
LET Z = NORMAL RANDOM NUMBERS FOR I = 1 1 N
LET Z=ZCONST*Z
LET Y2 = Y+Z
LET YBARL = MEAN Y2 SUBSET X1 -1
LET YBARH = MEAN Y2 SUBSET X1 +1
LET B1HAT2(K) = YBARH-YBARL
LET YBARL = MEAN Y2 SUBSET X2 -1
LET YBARH = MEAN Y2 SUBSET X2 +1
LET B2HAT2(K) = YBARH-YBARL
LET YBARL = MEAN Y2 SUBSET X3 -1
LET YBARH = MEAN Y2 SUBSET X3 +1
LET B3HAT2(K) = YBARH-YBARL
END LOOP
.
. ---------------------------------
. STEP 4--
. PLOT OUT THE ESTIMATES
.
LET W1 = SEQUENCE 1 1 NLOOP
LET W2 = W1+NLOOP
LINES SOLID DOTTED
MULTIPLOT 2 3
LEGEND 1 B1 = ^B1
PLOT B1HAT1 VS W1 B1HAT2 VS W2
LEGEND 1 B2 = ^B2
PLOT B2HAT1 VS W1 B2HAT2 VS W2
LEGEND 1 B3 = ^B3
PLOT B3HAT1 VS W1 B3HAT2 VS W2
LEGEND 1 B1 = ^B1
BIHISTOGRAM B1HAT1 B1HAT2
LEGEND 1 B2 = ^B2
BIHISTOGRAM B2HAT1 B2HAT2
LEGEND 1 B3 = ^B3
BIHISTOGRAM B3HAT1 B3HAT2
