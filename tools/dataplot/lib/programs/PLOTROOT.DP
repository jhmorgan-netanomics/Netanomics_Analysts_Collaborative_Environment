. THIS IS THE DATAPLOT PROGRAM FILE     PLOTROOTS.DP
. PURPOSE--PLOT OUT THE COMPLEX ROOTS FROM THE FAMILY OF FUNCTIONS
.          K + 1*X + 1*X**2
. ANALYSIS TECHNIQUE--COMPLEX ROOTS + PLOT
.
. -----START POINT-----------------------------------
.
. ECHO
FEEDBACK OFF
DIMENSION 20 VARIABLES
.
.      STEP 1--
.      DEFINE THE BASE POLYNOMIAL
.      (IN THIS CASE, 1 + 1*X + 1*X**2)
.      THIS BASE POLYNOMIAL WILL BE UPDATED LATER
.
SERIAL READ P
1 1 1
END OF DATA
.
.      STEP 2--
.      DEFINE DUMMY VARIABLES
.      (NEEDED LATER)
.
LET X2 = DATA -999 -999
LET Y2 = DATA -999 -999
LET D2 = DATA -999 -999
.
.      STEP 3--
.      EXECUTE A LOOP.
.      FOR EACH ITERATION, CHANGE THE BASE POLYNOMIAL
.      TO K + 1*X + 1*X**2
.      COMPUTE THE 2 COMPLEX ROOTS.
.      STORE THESE 2 ROOTS.
.
LOOP FOR K = 1 1 10
LET P(1) = K
LET X Y = COMPLEX ROOTS P
LET D = K FOR I = 1 1 2
APPEND X X2
APPEND Y Y2
APPEND D D2
END OF LOOP
.
.      STEP 4--
.      PLOT THE ROOTS
.
FEEDBACK ON
CHAR 1 2 3 4 5 6 7 8 9 0
LINES BLANK ALL
PLOT Y2 X2 D2 EXCEPT D2 = -999
PLOT Y2 D2 D2 EXCEPT D2 = -999
