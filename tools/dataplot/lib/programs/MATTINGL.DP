. THIS IS THE DATAPLOT PROGRAM FILE     MATTINGL.DP
. PURPOSE--YOUDEN PLOT FOR INTERLAB STUDY ON FLOW METER CALIBRATION
. GEORGE MATTINGLY  3/93
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = MATTINGL.DP
.
. -----------------------------------
. STEP 1--DEFINE LAB IDENTIFIERS
. -----------------------------------
.
READ STRING S1 TO S18
B C D F G H I J K L M N O P Q R S *
.
. -----------------------------------
. STEP 2--READ IN THE DATA
. -----------------------------------
SKIP 25
READ MATTINGL.DAT Y1 Y2 LAB
.
. -----------------------------------
. STEP 3--GENERATE A YOUDEN PLOT
. -----------------------------------
.
Y1LABEL S/N 3082
X1LABEL S/N 37846
X3LABEL AUTOMATIC
TITLE AN 8 - 4 DOWNSTREAM METERS - HIGH FLOW
CHAR B C D F G H I J K L M N O P Q R S *
LINES BLANK ALL
YOUDEN PLOT Y1 Y2 LAB
CALL MARK.DP
.
. ------------------------------------------------------
. STEP 4--GENERATE A BLOCK PLOT OF THE RAW DATA
. ------------------------------------------------------
.
ER
LET Y = Y1
EXTEND Y Y2
LET LAB2 = LAB
EXTEND LAB2 LAB
LET METER2 = SEQUENCE 1 18 1 2
.
Y1LABEL FLOW
X1LABEL LABORATORY
X2LABEL ORDERED ALPHABETICALLY BY LABORATORY ID
LEGEND 1 1 = METER 3082
LEGEND 2 2 = METER 37846
LEGEND 1 COORINDATES 17 87
LEGEND 2 COORDINATES 17 84
CHAR BLANK ALL
LINES SOLID ALL
CHAR 1 2
LINES BLANK BLANK
X1TICS OFF
X1TIC LABELS OFF
BLOCK PLOT Y LAB2 METER2
.
FEEDBACK OFF
JUSTIFICATION CENTER
LOOP FOR K = 1 1 18
MOVEDATA K 27850
LET L = LAB2(K)
TEXT ^S^L
END LOOP
FEEDBACK ON
CALL MARK.DP
.
. ------------------------------------------------------
. STEP 5--GENERATE A BLOCK PLOT OF RESIDUALS FROM LOCAL MEAN
. ------------------------------------------------------
.
ER
LET M1 = MEAN Y SUBSET METER2 1
LET M2 = MEAN Y SUBSET METER2 2
LET RES = Y-M1 SUBSET METER2 1
LET RES = Y-M2 SUBSET METER2 2
Y1LABEL RESIDUAL (= FLOW - METER MEAN)
X2LABEL ORDERED BY LOCATION
BLOCK PLOT RES LAB2 METER2
.
FEEDBACK OFF
JUSTIFICATION CENTER
LOOP FOR K = 1 1 18
MOVEDATA K -110
LET L = LAB2(K)
TEXT ^S^L
END LOOP
FEEDBACK ON
CALL MARK.DP
.
. ------------------------------------------------------
. STEP 6--GENERATE AN ORDERED BLOCK PLOT OF RESIDUALS FROM LOCAL MEAN
.         WITH ORDERING BASED ON LOCATION
. ------------------------------------------------------
.
ER
LET LAB3 = LAB2
LET RES3 = SORTC RES LAB3
LET LAB4 = DISTINCT LAB3
LET LAB5 = COCODE LAB2 LAB4
BLOCK PLOT RES LAB5 METER2
.
FEEDBACK OFF
JUSTIFICATION CENTER
LOOP FOR K = 1 1 18
MOVEDATA K -110
LET L = LAB4(K)
TEXT ^S^L
END LOOP
FEEDBACK ON
CALL MARK.DP
.
. ------------------------------------------------------
. STEP 7--GENERATE AN ORDERED BLOCK PLOT OF RESIDUAL FROM LOCAL MEAN
.         WITH ORDERING BASED ON SIGNED (= DIRECTIONAL) VARIATION
. ------------------------------------------------------
.
ER
LET LAB6 = LAB2
LET DEL = Y1-Y2
LET DEL2 = DEL
EXTEND DEL2 DEL
LET DEL6 = SORTC DEL2 LAB6
LET LAB7 = DISTINCT LAB6
LET LAB8 = COCODE LAB2 LAB7
X2LABEL ORDERED BY SIGNED VARIATION
BLOCK PLOT RES LAB8 METER2
.
FEEDBACK OFF
JUSTIFICATION CENTER
LOOP FOR K = 1 1 18
MOVEDATA K -110
LET L = LAB7(K)
TEXT ^S^L
END LOOP
FEEDBACK ON
CALL MARK.DP
.
. ------------------------------------------------------
. STEP 8--GENERATE AN ORDERED BLOCK PLOT OF RESIDUAL FROM LOCAL MEAN
.         WITH ORDERING BASED ON MAGNITUDE OF VARIATION
. ------------------------------------------------------
.
ER
LET LAB6 = LAB2
LET DEL = (Y1-M1)-(Y2-M2)
LET DEL2 = DEL
EXTEND DEL2 DEL
LET ABSDEL2 = ABS(DEL2)
LET ABSDEL6 = SORTC ABSDEL2 LAB6
LET LAB7 = DISTINCT LAB6
LET LAB8 = COCODE LAB2 LAB7
X2LABEL ORDERED BY MAGNITUDE OF VARIATION
BLOCK PLOT RES LAB8 METER2
.
FEEDBACK OFF
JUSTIFICATION CENTER
LOOP FOR K = 1 1 18
MOVEDATA K -110
LET L = LAB7(K)
TEXT ^S^L
END LOOP
FEEDBACK ON
CALL MARK.DP
.
