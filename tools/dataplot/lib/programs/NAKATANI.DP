. This is Dataplot program      NAKATANI.DP
. Non-linear fit of Nakatani Data (General)
. Date--January 1998
.
. -----start point-----
.
DEVICE 1 X11
DEVICE 2 POSTSCRIPT
.
. ----- STEP 0--DEFINE DATA FILE, MODEL, & STARTING VALUES
.
LET MODEL = 2
.
IF MODEL = 1
   LET FUNCTION DATAFILE = NAKATAN1.DAT
   LET FUNCTION OUTFILE = NAKATAN1.OUT
   LET SKIPTEXT = 25
   LET OMITBEGI = 8
   LET OMITEND = 0
   LET ORDERIQ = 1
   LET STRING WEIGHTED = 0
END IF
.
IF MODEL = 2
   LET FUNCTION DATAFILE = NAKATAN2.DAT
   LET FUNCTION OUTFILE = NAKATAN2.OUT
   LET SKIPTEXT = 25
   LET OMITBEGI = 8
   LET OMITEND = 2
   LET ORDERIQ = 1
   LET STRING WEIGHTED = 0
END IF
.
IF MODEL = 3
   LET FUNCTION DATAFILE = NAKATAN3.DAT
   LET FUNCTION OUTFILE = NAKATAN3.OUT
   LET SKIPTEXT = 25
   LET OMITBEGI = 8
   LET OMITEND = 0
   LET ORDERIQ = 1
   LET STRING WEIGHTED = 0
END IF
.
. ----- STEP 1--READ IN THE DATA
.
SKIP ^SKIPTEXT
IF ORDERIQ = 1
   READ ^DATAFILE Y X SDY
END IF
IF ORDERIQ = 0
   READ ^DATAFILE X Y SDY
END IF
LET N = NUMBER Y
LET N2 = N-OMITEND+1
IF OMITEND > 0
   DELETE Y X SDY FOR I = N2 1 N
END IF
IF OMITBEGI > 0
   DELETE Y X SDY FOR I = 1 1 OMITBEGI
END IF
LET N = NUMBER Y
.
. ----- STEP 2--PLOT THE DATA
.
TITLE ^DATAFILE
X3LABEL AUTOMATIC
CHARACTERS X BLANK
LINES BLANK SOLID
MULTIPLOT 2 2
PLOT Y X
.
. ----- STEP 3--SELECT THE MODEL, DEFINE STARTING VALUES, FIT THE DATA
.
IF MODEL = 1
   LET FUNCTION DENOM = 1 + (X*A2)**2
   LET FUNCTION F = (A1/DENOM) + A3

   LET A1 = 80
   LET A2 = 100
   LET A3 = 3
END IF

IF MODEL = 2
   LET FUNCTION F1A = 3*(SIN(ABS(A3)*X) - X*ABS(A3)*COS(ABS(A3)*X))
   LET FUNCTION F1B = ABS((A3)*X)**3
   LET FUNCTION F1 = F1A / F1B
   LET FUNCTION F2 = 0.0058*F1**2
   LET FUNCTION F3A = (X*ABS(A3))**ABS(A1)
   LET FUNCTION F3B = (1 + 1/(X*ABS(A2))**2) ** ((ABS(A1)-1)/2)
   LET FUNCTION F3 = F3A * F3B
   LET FUNCTION F4A = 1 + ABS(A1)*GAMMA(ABS(ABS(A1)-1))
   LET FUNCTION F4B = SIN((ABS(A1)-1)*ARCTAN(X*ABS(A2)))/F3
   LET FUNCTION F4 = F4A * F4B
   LET FUNCTION F = 0.0476 * F2 * F4

   LET A1 = 3
   LET A2 = 60
   LET A3 = 2
END IF
.
FIT Y = F
.
SET WRITE DECIMALS 7
LET N = NUMBER Y
WRITE ^OUTFILE N A1 A2 A3 RESSD
.
. ----- STEP 4--DO POST-FIT GRAPHICAL ANALYSIS
.
PLOT Y PRED VERSUS X
PLOT RES X
NORMAL PROBABILITY PLOT RES
MULTIPLOT OFF
.
. ----- STEP 5--WRITE OUT CONCLUSIONS
.
WRITE N A1 A2 A3 RESSD
