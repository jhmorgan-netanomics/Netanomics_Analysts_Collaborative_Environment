.  Perform a Fisher's dsicriminant analysis on Iris data.
.
.  READ DATA,  3 GROUPS (N1=N2=N3=3), 2 VARIABLES
. FEEDBACK OFF
DIMENSION 200 COLUMNS
SKIP 25
READ IRIS.DAT SEPLENG SEPWIDTH PETLENG PETWIDTH TAG
SKIP 0
LET NTOT = SIZE SEPLENG
LET X = MATRIX DEFINITION SEPLENG NTOT 4
LET P = MATRIX NUMBER OF COLUMNS X
.
LET GROUPID = DISTINCT TAG
LET NG = SIZE GROUPID
LET XMGRAND = MATRIX COLUMN MEANS X
.
.  CALCULATE B0 = SUM (I=1,NG) (XBARi - XBARALL)(XBARi-XBARALL)'
.
LET DIAG = 0 FOR I = 1 1 P
LET B0 = DIAGONAL MATRIX DIAG
.
LOOP FOR K = 1 1 NG
   LET N^K = SIZE TAG SUBSET TAG = K
   LET XMEANI = MATRIX COLUMN MEANS X SUBSET TAG = K
   LET XMEANI= XMEANI - XMGRAND
   LET B0TEMP = VECTOR TIMES TRANSPOSE XMEANI
   LET B0 = MATRIX ADDITION B0 B0TEMP
END OF LOOP
.
.  CALCULATE Spooled = (N1-1)S1 + ... + (Ng-1)Sg)/(N1+...+ Ng - g)
.  W = Spooled WITHOUT THE DENOMINATOR TERM
LET WINVB = DIAGONAL MATRIX DIAG
LOOP FOR K = 1 1 NG
    LET SI = VARIANCE-COVARIANCE MATRIX X SUBSET TAG = K
    LET ATEMP = N^K - 1
    LET SI = MATRIX MULTIPLICATION SI ATEMP
    LET WINVB = MATRIX ADDITION WINVB SI
END OF LOOP
LET DENOM = 1/(NTOT - NG)
LET SPOOL = MATRIX MULTIPLY WINVB DENOM
LET WINVB = MATRIX INVERSE WINVB
LET WINVB = MATRIX MULTIPLICATION WINVB B0
.
.  COMPUTE EIGENVALUES AND SORT IN DECREASING ORDER
.  COMPUTE EIGENVECTORS, ONLY KEEP REAL COMPONENT, SORT
.
LET INDX = SEQUENCE 1 1 P
LET E = MATRIX EIGENVALUES WINVB
RETAIN E FOR I = 1 1 P
LET ESORT = SORTC E INDX
LET REVERSE = SEQUENCE P 1 1
LET REVERSE = SORTC REVERSE ESORT INDX
LET EV = MATRIX EIGENVECTORS WINVB
print ev
pause
LET EVECT = DIAGONAL MATRIX DIAG
.  NORMALIZE L'SpooledL =1
.  DIST = L'SpooledL, MULTIPLY EIGENVECTOR BY 1/SQRT(DIST)
LOOP FOR K = 1 1 P
    LET LTAG = INDX(K)
    RETAIN EV^K  FOR I = 1 1 P
    LET EVECT^LTAG = EV^K
    LET DIST = QUADRATIC FORM SPOOL EVECT^LTAG
    LET EVECT^LTAG = (1/SQRT(DIST))*EVECT^LTAG
END OF LOOP
.  PLOT FIRST 2 DISCRIMINANTS
LET ZY = LINEAR COMBINATION X EVECT1
LET ZX = LINEAR COMBINATION X EVECT2
DEVICE 1 OFF
MEAN PLOT ZY TAG
LET GMEANY = YPLOT
MEAN PLOT ZX TAG
LET GMEANX = YPLOT
RETAIN GMEANX GMEANY SUBSET TAGPLOT = 1
DEVICE 1 ON
Y1LABEL FIRST DISCRIMINANT
X1LABEL SECOND DISCRIMINANT
CHARACTER CIRCLE SQUARE TRIANGLE
LINE BLANK ALL
LEGEND 1 CIRC() - SPECIES 1
LEGEND 2 SQUA() - SPECIES 2
LEGEND 3 TRIA() - SPECIES 3
LEGEND FONT DUPLEX
LEGEND SIZE 1.2
TITLE PLOT FIRST 2 DISCRIMINANT FUNCTIONS
PLOT ZY ZX TAG
CAPTURE ZZZZJUNK.DAT
PRINT "FISHER's DISCRIMINANT ANALYSIS"
PRINT " "
PRINT " "
PRINT "B0 MATRIX (= between group sums of cross-products):"
PRINT B0
PRINT " "
PRINT " "
PRINT "POOLED VARIANCE-COVARIANCE MATRIX:"
PRINT SPOOL
PRINT " "
PRINT " "
PRINT "EIGENVALUES:"
PRINT ESORT
PRINT " "
PRINT " "
PRINT "COLUMNS ARE THE DISCRIMINANT FUNCTIONS:"
PRINT EVECT
PRINT " "
PRINT " "
PRINT "GROUP MEANS:"
PRINT GMEANX GMEANY
END OF CAPTURE
LIST ZZZZJUNK.DAT
