. THIS IS THE DATAPLOT PROGRAM FILE     HOTELL2.DP
. PURPOSE--CARRY OUT A LEAST SQUARES LINEAR FIT,
.          COMPUTE UPPER AND LOWER 95% (SIMULTANEOUS)
.          HOTELLING CONFIDENCE LIMITS
.          FOR THE PREDICTED VALUES
.          OVER THE RANGE XMIN TO XMAX (ANALYST-SPECIFIED),
.          AND THEN GENERATE THE PLOT.
.
. INPUT --Y    = RESPONSE VARIABLE
.         X    = INDEPENDENT VARIABLE
.         XMIN = DESIRED LOWER RANGE FOR THE CONFIDENCE LIMITS
.         XMAX = DESIRED UPPER RANGE FOR THE CONFIDENCE LIMITS
.         XINC = DESIRED INCREMENT FOR COMPUTED X VALUES
.
. OUTPUT--YUPPER = COMPUTED UPPER CONFIDENCE LIMITS
.       --YLOWER = COMPUTED LOWER CONFIDENCE LIMITS
.
. MARCH 1997
.
. -----START POINT-----------------------------------
.
LINEAR FIT Y X
.
LET N = NUMBER X
LET NM2 = N-2
LET TERM1 = 1/N
.
LET XBAR = MEAN X
LET XSD = SD X
LET DENOM = XSD*XSD*NM2
.
LET XDUMMY = SEQUENCE XMIN XINC XMAX
LET PREDDUMM = A0+A1*XDUMMY
.
LET DEL = XDUMMY-XBAR
LET NUMER = DEL*DEL
LET TERM2 = NUMER/DENOM
.
LET SDPRED = RESSD*SQRT(TERM1+TERM2)
.
LET K = SQRT(2*FCDF(.95,2,NM2))
LET YUPPER = PREDDUMM + K*SDPRED
LET YLOWER = PREDDUMM - K*SDPRED
.
CHAR X BLANK BLANK BLANK
LINES BLANK SOLID DOT DOT
PLOT Y VS X AND
PLOT PREDDUMM YUPPER YLOWER VS XDUMMY
