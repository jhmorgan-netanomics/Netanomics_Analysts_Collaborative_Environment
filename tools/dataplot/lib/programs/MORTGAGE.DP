. THIS IS THE DATAPLOT PROGRAM FILE     MORTGAGE.DP
. PURPOSE--GENERATE MORTGAGE INFORMATION
. INPUT = PRIN ($)
.         PERC (= PERCENTAGE RATE PER YEAR--0 < RATE < 100)
.         NUMYEAR = NUMBER OF YEARS
. OUTPUT = MONTHLY PAYMENTS
. DATE--MARCH 1992
. EXAMPLE OF USE--
.    LET PRIN = 150000
.    LET PERC = 7.0
.    LET NUMYEAR = 30
.    CALL MORTAGE.DP
.
. -----START POINT---------------
.
FEEDBACK OFF
.
SET IO TERMINAL
WRITE "PRINCIPLE = ?"
READ PARAMETER PRIN
WRITE "INTEREST (AS A %) = ?"
READ PARAMETER PERC
WRITE "NUMBER OF YEARS = ?"
READ PARAMETER NUMYEAR
SET IO
.
LET NUMMONTH = 12*NUMYEAR
LET RATE = PERC/100
LET RATEMONTH = RATE/12
.
LET MONTH = SEQUENCE 1 1 NUMMONTH
LET FACTOR = 1+1/(((1+RATEMONTH)**NUMMONTH)-1)
.
LET MONTHPAY = PRIN*RATEMONTH*FACTOR
LET MPAY = MONTHPAY FOR I = 1 1 NUMMONTH
LET MPRIN = (MPAY-PRIN*RATEMONTH)*(1+RATEMONTH)**(MONTH-1)
LET MINT = MPAY-MPRIN
.
LET CUMPAY = MPAY*MONTH
LET CUMPRIN = CUMULATIVE SUM MPRIN
LET CUMINT = CUMULATIVE SUM MINT
.
SET WRITE FORMAT F4.0,3F8.2,2X,3F10.2
WRITE MORTGAGE.OUT. MONTH MPAY MPRIN MINT CUMPAY CUMPRIN CUMINT
.
WRITE " "
WRITE " "
WRITE "   MORTGAGE ANALYSIS"
WRITE " "
WRITE "PRINCIPLE        = ^PRIN"
WRITE "PERCENTAGE RATE  = ^PERC"
WRITE "NUMBER OF YEARS  = ^NUMYEAR"
WRITE "MONTHLY PAYMENT  = ^MONTHPAY"
WRITE " "
WRITE "OUTPUT FILE = MORTGAGE.OUT"
.
FEEDBACK ON
