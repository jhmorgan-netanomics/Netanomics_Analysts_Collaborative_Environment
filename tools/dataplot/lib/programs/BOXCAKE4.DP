. THIS IS DATAPLOT PROGRAM FILE    BOXCAKE4.DP
. SIGNAL-TO-NOISE CAKE TASTE
. BOX & JONES
. TAGUCHI PARAMETER DESIGN EXAMPLE
. 2**3 ANALYSIS OF S/N (OVER TIME AND TEMP)
. INPUT DATA FILE--BOXCAKE2.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. -----START POINT----------
.
DIMENSION 40 VARIABLES
DEVICE 2 POSTSCRIPT
.
. STEP 1--READ IN THE DATA
.
SKIP 50
READ BOXCAKE2.DAT YM YSD X1 X2 X3
LET Y = YSD
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 2
LABEL SIZE 4
Y1LABEL SD TASTE
X3LABEL AUTOMATIC
X1LABEL SETTINGS
PLOT Y
CHAR X; SPIKE ON; PLOT Y
SPIKE OFF; X1LABEL
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3
LINES SOLID ALL; LINES BLANK BLANK
Y1LABEL
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
DELETE YM YSD X1 X2 X3 Y FOR I = 1 1 1
.
ER
MULTIPLOT OFF
X3LABEL
LET NUMFAC = 3
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 0 3
LET STRING S0 = SD TASTE
LET STRING S1 = FLOUR % (X1)
LET STRING S2 = SHORTENING % (X2)
LET STRING S3 = EGGS % (X3)
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 2 2
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 2.5 ALL; LABEL SIZE 3; TIC LABEL SIZE 3
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3    X1
BLOCK PLOT Y X1 X3    X2
BLOCK PLOT Y X1 X2    X3
MULTIPLOT OFF
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
ER
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B1 = COEF(1)
LET RESSD = RESSD2(1)
LET PRED = MU + 0.5*(B1*X1)
LET RES = Y-PRED
ER
SET WRITE DECIMALS 2
WRITE X1 X2 X3 Y PRED RES
WRITE OUT2. X1 X2 X3 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
MULTIPLOT 2 2
X1LABEL PRED = ^MU + 0.5*(^B1*X1)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
LABEL SIZE 3
. PLOT RES RUNSEQ
PLOT RES
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 0.5 1.0 1.5 2.0 2.5 3.0
LET U1 = X1
LET U2 = X3
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL FLOUR (X1)
Y1LABEL EGGS (X3)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
ER
LET YSORT = SORTC Y X1 X2 X3
SET WRITE DECIMALS 2
WRITE X1 X2 X3 YSORT
WRITE OUT3. X1 X2 X3 YSORT
.
