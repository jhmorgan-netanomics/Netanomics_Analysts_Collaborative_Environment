. THIS IS THE DATAPLOT PROGRAM FILE     PONTIUS.DP
. PURPOSE--ANALYZE PAUL PONTIUS
.          LOAD CELL CALIBRATION DATA
. DATE--APRIL 1992
.
. -----START POINT----------
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = PONTIUS.DP
.
. --------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
READ PONTIUS.DAT Y X
.
. --------------------------
. STEP 2--SPECIFY PLOT ATTRIBUTES
.
X3LABEL AUTOMATIC
LABEL SIZE 4
CHAR X BLANK
LINES BLANK SOLID
FEEDBACK OFF
.
. --------------------------
. STEP 3--DO LINEAR FIT
.
MULTIPLOT 2 2
.
PLOT Y X
.
CAPTURE JUNK1.
LINEAR FIT Y X
END OF CAPTURE
.
PLOT Y PRED VERSUS X
PLOT RES X
NORMAL PROBABILITY PLOT RES
.
MULTIPLOT OFF; MOVE 50 92; JUST CENTER; HW 3 2
TEXT LINEAR FIT (PONTIUS CALIBRATION DATA)
CALL MARK.DP
.
. --------------------------
. STEP 4--DO QUADRATIC FIT
.
MULTIPLOT 2 2
.
PLOT Y X
.
CAPTURE JUNK2.
QUADRATIC FIT Y X
END OF CAPTURE
.
PLOT Y PRED VERSUS X
PLOT RES X
NORMAL PROBABILITY PLOT RES
.
MULTIPLOT OFF; MOVE 50 92; JUST CENTER; HW 3 2
TEXT QUADRATIC FIT (PONTIUS CALIBRATION DATA)
CALL MARK.DP
