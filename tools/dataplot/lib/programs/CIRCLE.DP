. THIS IS THE DATAPLOT PROGRAM FILE     CIRCLE.DP
. PURPOSE--DETERMINE THE EQUATION OF A CIRCLE
. ANALYSIS TECHNIQUE--MATRIX DEFINITION OF EQUATION OF A CIRCLE
. APPLICATION--
. PROBLEM--GIVEN 3 POINTS ON A PLANE
.          FIND THE EQUATION OF THE CIRCLE.
.          FIND THE RADIUS OF THE CIRCLE
.          FIND ITS CENTER
.          PLOT RAW DATA AND CIRCLE
. REFERENCE--EISENHART, LUTHER PFAHLER, COORDINATE GEOMETRY,
.         DOVER PUBLICATIONS, INC. 1939, PAGE 145.
. NOTE--THE SOLUTION IS (XC,YC) = (0,0) R = 1
. DATE--OCTOBER 1987
.
. -----START POINT-----------------------------------
.
ECHO
DIMENSION 40 VARIABLES
.
.      STEP 1--
.      DEFINE A DUMMY POINT
.      PLUS THE DESIRED 3 (X,Y) POINTS
.
READ X Y
-99 -99
1 0
0 1
-1 0
END OF DATA
.
.      STEP 2--
.      SET UP THE MATRIX
.      WHOSE DETERMINANT DEFINES THE
.      EQUATION OF A CIRCLE
.
LET C1 = X**2+Y**2
LET C2 = X
LET C3 = Y
LET C4 = 1 FOR I = 1 1 4
LET A = MATRIX DEFINITION C1 4 4
PRINT A
.
.      STEP 3--
.      SOLVE FOR THE COEFFICIENTS
.      IN THE GENERAL FORM
.      FOR THE EQUATION OF A CIRCLE
.
LET A11 = MATRIX COFACTOR A 1 1
LET A12 = MATRIX COFACTOR A 1 2
LET A13 = MATRIX COFACTOR A 1 3
LET A14 = MATRIX COFACTOR A 1 4
PRINT A11 A12 A13 A14
.
.      STEP 4--
.      SOLVE FOR THE COEFFICIENTS
.      IN THE CENTER-RADIUS FORM
.      FOR THE EQUATION OF A CIRCLE
.
LET XC = -0.5*A12/A11
LET YC = -0.5*A13/A11
LET R = SQRT(XC**2+YC**2-A14/A11)
PRINT XC YC R
.
.      STEP 5--
.      CALCULATE DUMMY POINTS ON THE CIRCLE
.
DEGREES
LET THETA = SEQUENCE 0 1 360
LET X2 = R*COS(THETA)+XC
LET Y2 = R*SIN(THETA)+YC
.
.      STEP 6--
.      PLOT OUT THE 3 ORIGINAL POIJNTS,
.      THE COMPUTED CENTER, AND
.      THE CIRCLE ITSELF.
.
PRE-SORT OFF
CHARACTERS X C BLANK
LINES BLANK BLANK SOLID
LET XC2(1)=XC
LET YC2(1)=YC
PLOT Y X FOR I = 2 1 4 AND
PLOT YC2 XC2 AND
PLOT Y2 X2
.
