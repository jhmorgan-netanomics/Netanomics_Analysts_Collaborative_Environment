. THIS IS THE DATAPLOT PROGRAM FILE     FFT3.DP
. PURPOSE--REMOVE HIGH FREQUENCY CONTAMINATION FROM A SIGNAL
. ANALYSIS TECHNIQUE--FFT, INVERSE FFT
. APPLIATION--SPECTROSCOPY, THERMOMETRY, ETC.
.
. -----START POINT-----------------------------------
.
ECHO
DIMENSION 20 VARIABLES
.
.      STEP 1--
.      DEFINE THE PURE SIGNAL
.      DEFINE THE CONTAIMINATION
.      COMBINE PURE + CONTAMINATION
.      TO YIELD THE OBSERVED RESPONSE
.      PLOT
.
LET X = SEQUENCE 0 .1 25.55
LET YS = SIN(X)
LET YN = NORMAL RANDOM NUMBERS FOR I = 1 1 256
LET YN = YN/10
LET Y = YS+YN
MULTIPLOT 2 2
YLIMITS -2 2
Y1LABEL AUTOMATIC
. FONT SIMPLEX
PLOT YS
PLOT YN
PLOT Y
YLIMITS
.
.      STEP 2--
.      FFT THE RESPONSE
.      PLOT THE REAL AND IMAGINARY PARTS
.
LET U V = FFT Y
MULTIPLOT 2 2
PLOT U
PLOT V
.
.      STEP 3--
.      IDENTIFY AND ZERO-OUT THE HIGHH FREQUENCY
.      IN THE REAL AND IMAGINARY PARTS
.
LET TAG = 0 FOR I = 1 1 256
LET TAG = 1 FOR I = 108 1 148
LET U = 0 SUBSET TAG = 0
LET V = 0 SUBSET TAG = 0
.
.      STEP 4--
.      TRANSFORM BACK
.      PLOT AND COMPARE
.
LET XPRIME YPRIME = INVERSE FFT U V
MULTIPLOT 2 2
YLIMITS -2 2
PLOT Y
PLOT YS
PLOT XPRIME
PLOT YPRIME
