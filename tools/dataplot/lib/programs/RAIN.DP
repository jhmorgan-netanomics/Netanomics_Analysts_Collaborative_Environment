. THIS IS THE DATAPLOT PROGRAM FILE     RAIN.DP
. PURPOSE--DETERMINE THE EQUATION OF MOTION OF A RAINDROP
.          WITH SEVERAL DIFFERENT AIR RESISTANCE VALUES.
. ANALYSIS TECHNIQUE--RUNGE-KUTTA SOLUTION TO DIFFERENTIAL EQUATION
. APPLICATION--
. NOTE--
.       Y = SPEED AFTER TIME X
.       X = TIME (IN SECONDS)
.       M = MASS
.       C = AIR RESISTANCE
.
. -----START POINT-----------------------------------
.
ECHO
.
.      STEP 1--
.      DEFINE THE RIGHT-HAND SIDE FUNCTION OF Y' = F
.      DEFINE THE DESIRED GRID OF X VALUES
.      DEFINE INITIAL CONDITION
.      DEFINE A TABLE OF VALUES FOR C
.
LET FUNCTION F = 32-(C/M)*Y**2
LET X = 0 .1 5
LET Y(1)=0
LET M = 1
LET CTAB = DATA 2 1 .5 .1 .01 0
.
.      STEP 2--
.      SOLVE THE DIFFERENTIAL EQUATION FOR EACH VALUE OF C
.
LOOP FOR K = 1 1 6
LET C = CTAB(K)
LET Y = RUNGE-KUTTA F X
LET Y^K = Y
END OF LOOP
.
.      STEP 3--
.      PLOT OUT THE 6 TRACES
.
Y1LABEL SPEED
XLABEL TIME (IN SECONDS)
X2LABEL STARTING SPEED = 0
X3LABEL AIR RESISTANCE = 2, 1, .5, .1, .01, 0
PLOT Y1 Y2 Y3 Y4 Y5 Y6 VS X
