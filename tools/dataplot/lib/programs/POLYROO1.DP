. THIS IS THE DATAPLOT PROGRAM FILE     POLYROOTS1.DP
. PURPOSE--DETERMINE THE COMPLEX ROOTS OF THE POLYNOMIAL
.          1.2 + 3.55*X + 12.25*X**2 + 7.33*X**3
.          + 14.2377*X**4 -10.752*X**5 + 5.42*X**6
. ANALYSIS TECHNIQUE--LAGUERRE'S METHOD FOR FINDING COMPLEX ROOTS
.                     OF COMPLEX POLYNOMIALS
.                     (ALTHOUGH IN THIS CASE, THE POLYNOMIAL IS REAL).
. APPLICATION--FILTER DESIGN, PROBABILITY THEORY, EIGENVALUES
. SOURCE (FOR ALGORITHM)--PRESS, WILLIAM H, FLANNERY, BRIAN, P.,
.                         TEUKOLSKY, SAUL A., AND VETTERLING, WILLIAM T.,
.                         NUMERICAL RECIPES--THE ART OF SCIENTIFIC COMPUTING,
.                         CAMBRIDGE UNIVERSITY PRESS, 1986, PAGES 263-266.
. NOTE--FOR TESTING PURPOSES, THE ROOTS ARE
.          -0.1977768    -0.6085631
.          -0.1977768     0.6085631
.          -0.1802901    -0.3198090
.          -0.1802901     0.3198091
.           1.369949      1.461156
.           1.369949     -1.461156
.
. -----START POINT-----------------------------------
.
ECHO
.
.      STEP 1--DEFINE THE COEFFICIENTS OF THE POLYNOMIAL
.
READ P
1.2
3.55
12.25
7.33
14.2377
-10.752
5.42
END OF DATA
.
.      STEP 2--
.      DEFINE A SEQUENCE OF POINTS.
.      EVALUATE THE POLYNOMIAL AT THOSE POINTS.
.      PLOT THE EVALUATED POLYNOMIAL.
.
LET X = SEQUENCE -1 .1 1
LET Y = POLYNOMIAL EVALUATION P X
PLOT Y X
.
.      STEP 3--
.      FIND THE COMPLEX ROOTS
.
LET X2 Y2 = COMPLEX ROOTS P
PRINT X2 Y2
.
.      STEP 4--
.      PLOT THE COMPLEX ROOTS
.
CHAR X
LINES
GRID ON
PLOT Y2 X2
