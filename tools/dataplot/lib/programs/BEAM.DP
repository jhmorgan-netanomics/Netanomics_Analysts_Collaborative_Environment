. THIS IS THE DATAPLOT PROGRAM FILE     BEAM.DP
. PURPOSE--DETERMINE DEFLECTION VS DISTANCE TRACE OF ELASTIC BEAM
.          ANCHORED AT ONE END AND WITH A LOAD AT THE OTHER END
.          (CANTILEVER).
. ANALYSIS TECHNIQUE--RUNGE-KUTTA SOLUTION OF 2ND ORDER DIFFERENTIAL EQUATION
. APPLICATION--STRUCTURAL ENGINEERING
. SOURCE--DORN, WILLIAM S. AND MCCRACKEN, DANIEL D.,
.         NUMERICAL METHODS WITH FORTRAN IV CASE STUDIES,
.         WILEY, P
.         JOHN WILEY AND SONS, INC., NEW YORK, 1972, PAGES 391-401.
. TO FIND--DEFLECTION VALUE FOR VARIOUS DISTANCES OUT ONTO THE BEAM
. NOTE--THE ELASTIC DEFLECTION SATISFIES THE DIFFERENTIAL EQUATION--
.       Y'' / (1 + (Y')**2 )**(3/2)   =   P*(L-X) / (E*I)
.       Y'' = P*(L-X) / (E*I)     *    (1 + (Y')**2 )**(3/2)
.       WITH Y(0) = 0 AND Y'(0) = 0
.       WHERE
.          Y = DEFLECTION FROM HORIZONTAL (IN INCHES)
.          X = DISTANCE OUT ONTO BEAM (X = 0, 2, 4, 6, ..., 100 INCHES)
.          P = LOAD (IN POUNDS) AT FREE END (HERE = 64)
.          L = LENGTH (IN INCHES) OF BEAM (HERE = 100)
.          E = YOUNG'S MODULUS (= 6 * 10**6)
.          I = MOMENT OF INERTIA OF CROSS SECTION (HERE = 0.128 INCHES**4)
. NOTE--FOR SOME APPLICATIONS, IF CAN ASSUME Y' SMALL (<< 1),
.       THEN (Y')**2 BECOMES NEGLIGIBLE, AND THE DIFFERENTIAL EQUATION
.       REDUCES TO THE BERNOULLI-EULER LAW (Y'' = P*(L-X) / (E*I)
.       WHICH HAS A KNOW SOLUTION OF Y = (P/(6*E*I)) * (X**2) * (3*L-X)
.       WE ARE   NOT   MAKING THIS ASSUMPTION, AND ARE THEREFOR
.       SOLVING THE MORE DIFFICULT PROBLEM.  IT IS OF INTEREST, HOWEVER,
.       TO COMPARE THE SOLUTION OF THE MORE DIFFICULT PROBLEM
.       WITH THE SOLUTION BASED ON THE BERNOULLI-EULER SIMPLER PROBLEM.
. NOTE--FOR TESTING PURPOSES, THE TRUE SOLUTION IS
.           Y(2)   =   .01000
.           Y(4)   =   .03950
.           Y(6)   =   .08822
.           Y(8)   =   .15576
.           Y(10)  =   .24174
.           Y(50)  =  5.25897
.           Y(100) = 17.03627
.
. -----START POINT-----------------------------------
.
ECHO
.
.      STEP 1--
.      DEFINE THE PHYSICAL PARAMETERS OF THE BEAM
.
LET P = 64
LET L = 100
LET E = 6*10**6
LET I = 0.128
.
.      STEP 2--
.      DEFINE THE RIGHT-SIDE FUNCTION OF Y''(X) = F
.
LET FUNCTION F1 = P*(L-X)/(E*I)
LET FUNCTION F2 = (1+YP**2)**(3/2)
LET FUNCTION F = F1*F2
.
.      STEP 3--
.      DEFINE INITIAL CONDITIONS.
.      DEFINE THE DESIRED SEQUENCE OF POINTS
.      AT WHICH TO COMPUTE THE SOLUTION CURVE.
.
LET Y(1) = 0
LET YP(1) = 0
LET X = SEQUENCE 0 2 100
.
.      STEP 4--
.      SOLVE THE DIFFERENTIAL EQUATION
.
LET Y YP = RUNGA-KUTTA F X
PLOT Y X
PRINT X Y YP
.
.      STEP 5--
.      PLOT OUT THE SOLUTION CURVE
.
LET YBE = (P/(6*E*I))*(X**2)*(3*L-X)
LINES SOLID DOTTED
PLOT Y YBE VS X
