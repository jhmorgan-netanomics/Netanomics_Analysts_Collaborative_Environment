. THIS IS THE DATAPLOT PROGRAM FILE     4PLOT.DP
. PURPOSE--GENERATE A 4-PLOT OF GEAR DIAMETER DATA
.
. -----START POINT-----------------------------------
.
LET COLOR = 1
LET WINDSIZE = 1
CALL SETCOLOR.DP
.
. ERASE
BOX 0 0 100 100
LET STRING MARK = 4PLOT.DP
.
Y1LABEL
X1LABEL
.
SKIP 25
READ GEAR.DAT Diameter Batch
.
LABEL SIZE 4
TIC LABEL SIZE 3
MULTIPLOT CORNER COORDINATES 5 10 95 95
MULTIPLOT 2 2
.
RUN SEQUENCE PLOT Diameter
CHARACTER SIZE 3
LINES BLANK; CHARACTERS X
LAG PLOT Diameter
LINES SOLID; CHARACTERS BLANK
HISTOGRAM Diameter
CHARACTERS X; LINES SOLID
NORMAL PROBABILITY PLOT Diameter
.
MULTIPLOT OFF
.
MOVE 50 93.5
JUSTIFICATION CENTER
HW 5 2.5
TEXT Gear Diameter Analysis
.
MOVE 50 5
HW 3 2
TEXT Testing Underlying Assumptions
TEXT 4-PLOT Diameter
.
CALL MARK2.DP
EXIT
