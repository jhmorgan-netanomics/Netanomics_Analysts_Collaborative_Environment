.  Perform a Fisher's dsicriminant analysis on Iris data.
.
.  READ DATA,  3 GROUPS (N1=N2=N3=3), 2 VARIABLES
. FEEDBACK OFF
Echo on
DIMENSION 200 COLUMNS
SKIP 25
READ IRIS.DAT SEPLENG SEPWIDTH PETLENG PETWIDTH TAG
SKIP 0
.
LET NTOT = SIZE SEPLENG
LET X = MATRIX DEFINITION SEPLENG NTOT 4
LET P = MATRIX NUMBER OF COLUMNS X
LET GROUPID = DISTINCT TAG
LET NG = SIZE GROUPID
LOOP FOR K = 1 1 NG
    LET N(K) = SIZE TAG SUBSET TAG = K
END OF LOOP
LOOP FOR K = 1 1 P
    LET XMGRAND(K) = MEAN X^K
END OF LOOP
.
.  CALCULATE B0 = SUM (I=1,NG) (XBARi - XBARALL)(XBARi-XBARALL)'
.
LET JUNK = 0 FOR I = 1 1 P
LET B0 = DIAGONAL MATRIX JUNK
.
LOOP FOR K = 1 1 G
   LOOP FOR L = 1 1 P
       LET XMEANI(L) = MEAN X^L  SUBSET TAG = K
   END OF LOOP
   LET TEMP = XMEANI - XMGRAND
   LET XM = MATRIX DEFINITION TEMP P 1
   LET XMT = MATRIX TRANSPOSE XM
   LET B0TEMP = MATRIX MULTIPLY XM XMT
   LET B0 = MATRIX ADDITION B0 B0TEMP
END OF LOOP
echo off
print g
PRINT " "; PRINT " "; PRINT "B0 MATRIX:"; PRINT B0
pause
.
.  CALCULATE Spooled = (N1-1)S1 + ... + (Ng-1)Sg)/(N1+...+ Ng - g)
.  W = Spooled WITHOUT THE DENOMINATOR TERM
LET JUNK2 = 0 FOR I = 1 1 G
LET W = DIAGONAL MATRIX JUNK2
LOOP FOR K = 1 1 G
    LET ROWLAST = SUM N FOR I = 1 1 K
    LET ATEMP = N(K)
    LET ROWFIRST = ROWLAST - ATEMP + 1
    LET XTEMP = MATRIX DEFINITION X ROWLAST P ROWFIRST
    LET SI = VARIANCE-COVARIANCE MATRIX XTEMP
    LET ATEMP = ATEMP - 1
    LET SI = MATRIX MULTIPLICATION SI ATEMP
    LET W = MATRIX ADDITION W SI
END OF LOOP
LET DENOM = 1/(NTOT - NG)
LET SPOOL = MATRIX MULTIPLY W DENOM
PRINT " "; PRINT " "; PRINT "POOLED COVARIANCE MATRIX:"; PRINT SPOOL
LET WINV = MATRIX INVERSE W
LET WINVB = MATRIX MULTIPLICATION WINV B0
.
.  COMPUTE EIGENVALUES AND SORT IN DECREASING ORDER
.  COMPUTE EIGENVECTORS, ONLY KEEP REAL COMPONENT, SORT
.
LET INDX = SEQUENCE 1 1 P
LET E = MATRIX EIGENVALUES WINVB
RETAIN E FOR I = 1 1 P
LET ESORT = SORTC E INDX
LET REVERSE = SEQUENCE P 1 1
LET REVERSE = SORTC REVERSE ESORT INDX
LET EV = MATRIX EIGENVECTORS WINVB
LET DIAG = 0 FOR I = 1 1 P
LET EVECT = DIAGONAL MATRIX DIAG
LOOP FOR K = 1 1 P
    LET LTAG = INDX(K)
    LET EVECT^LTAG = EV^K FOR I = 1 1 P
END OF LOOP
PRINT " "; PRINT " "; PRINT "EIGENVALUES:"; PRINT ESORT
.  NORMALIZE L'SpooledL =1
.  DIST = L'SpooledL, MULTIPLY EIGENVECTOR BY 1/SQRT(DIST)
LOOP FOR K = 1 1 P
   LET LTEMP = MATRIX DEFINITION EVECT^K P 1
   LET LTEMPT = MATRIX TRANSPOSE LTEMP
   LET MATTMP = MATRIX MULTIPLY LTEMPT SPOOL
   LET MATTMZ = MATRIX MULTIPLY MATTMP LTEMP
   LET DIST = MATTMZ1(1)
   LET FACT = 1/SQRT(DIST)
   LET EVECT^K = FACT*EVECT^K
END OF LOOP
PRINT " "; PRINT " "
PRINT "COLUMNS ARE THE DISCRIMINANT FUNCTIONS:"; PRINT EVECT
.  PLOT FIRST 2 DISCRIMINANTS
LET ZY = 0 FOR I = 1 1 NTOT
LOOP FOR K = 1 1 P
    LET FACT = EVECT1(K)
    LET ZY = ZY + FACT*X^K
END OF LOOP
LET ZX = 0 FOR I = 1 1 NTOT
LOOP FOR K = 1 1 P
    LET FACT = EVECT2(K)
    LET ZX = ZX + FACT*X^K
END OF LOOP
LOOP FOR K = 1 1 NG
   LET AY = MEAN ZY SUBSET TAG = K
   LET AX = MEAN ZX SUBSET TAG = K
   LET GMEANX(K) = AX
   LET GMEANY(K) = AY
END OF LOOP
PRINT " "; PRINT " "; PRINT "GROUP MEANS:"; PRINT GMEANX GMEANY
Y1LABEL FIRST DISCRIMINANT
X1LABEL SECOND DISCRIMINANT
CHARACTER CIRCLE SQUARE TRIANGLE
LINE BLANK ALL
LEGEND 1 CIRC() - SPECIES 1
LEGEND 2 SQUA() - SPECIES 2
LEGEND 3 TRIA() - SPECIES 3
LEGEND FONT DUPLEX
LEGEND SIZE 1.2
TITLE PLOT FIRST 2 DISCRIMINANT FUNCTIONS
PLOT ZY ZX TAG
. 
FEEDBACK OFF
