. THIS IS DATAPLOT PROGRAM FILE    DUEUER.DP
. GENERATE SPECIALIZED CHARACTER PLOT WITH TOP AND SIDE HISTOGRAMS
. MARCH 1993
.
. -----START POINT-----
.
LET STRING MARK = DUEUER.DP
.
. -----------------------
. STEP 1--DEFINE THE DATA
. -----------------------
.
LET Y1 = NORMAL RANDOM NUMBERS FOR I = 1 1 40
LET Y2 = NORMAL RANDOM NUMBERS FOR I = 1 1 40
LET TAG = 1 FOR I = 1 1 20
LET TAG = 2 FOR I = 21 1 40
.
. -----------------------------------
. STEP 2--GENERATE THE CHARACTER PLOT
. -----------------------------------
.
FEEDBACK OFF
XLIMITS -3 3; YLIMITS -3 3
TIC OFFSET UNITS SCREEN
TIC OFFSET 0 0
PRE-ERASE ON
WINDOW CORNER COORDINATES 0 0 60 60
FRAME CORNER COORDINATES 15 20 100 100
Y1LABEL SLOPE
X1LABEL INTERCEPT
CHAR 1 2; LINES BLANK ALL
PLOT Y1 Y2 TAG
.
. ---------------------------------------
. STEP 3--GENERATE THE TOP-SIDE HISTOGRAM
. ---------------------------------------
.
PRE-ERASE OFF
WINDOW CORNER COORDINATES 0 60 60 90
FRAME CORNER COORDINATES 15 0 100 100
CHAR BLANK ALL; LINES SOLID; BAR ON
Y1LABEL; X1LABEL
TICS OFF; TIC LABELS OFF
Y2TICS ON; Y2TIC LABELS ON
TIC LABEL SIZE 3
XLIMITS -3 3; YLIMITS; YMINIMUM 0
HISTOGRAM Y2
.
. -----------------------------------------
. STEP 4--GENERATE THE RIGHT-SIDE HISTOGRAM
. -----------------------------------------
LET K=0
LET M1 = MEAN Y1
LET SD1 = STANDARD DEVIATION Y1
LET MID1 = M1-5.7*SD1
LET DEL = .6*SD1
LET MID2 = M1+5.7*SD1
LOOP FOR MID = MID1 DEL MID2
LET K=K+1
LET MIDREV = (MID2+DEL)-K*DEL +DEL
LET LOWER = MID-DEL/2
LET UPPER = MID+DEL/2
. LET X(K) = MIDREV
LET X(K) = MID
LET Y(K) = NUMBER Y1 SUBSET Y1 > LOWER SUBSET Y1 <= UPPER
END LOOP
.
WINDOW CORNER COORDINATES 60 0 90 60
FRAME CORNER COORDINATES 0 20 100 100
TICS OFF; TIC LABELS OFF
X2TICS ON; X2TIC LABELS ON
TIC LABEL SIZE 1.5
YLIMITS -3 3; XLIMITS; XMINIMUM 0
LINES; BAR ON
BAR DIRECTION H; BAR WIDTH DEL; PRE-SORT OFF
PLOT X Y
.
. ------------------------------
. STEP 5--WRITE OUT A PAGE TITLE
. ------------------------------
.
WINDOW CORNER COORDINATES 0 0 100 100
MOVE 50 94
HW 4 3
JUSTIFICATION CENTER
TEXT WHAT ISN'T CHEMISTRY?
.
CALL MARK.DP
