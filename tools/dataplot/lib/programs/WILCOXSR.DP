.
.  Perform a non-parameteric Wilcoxon signed rank test for a paired
.  sample.  This test can be easily adapted to a one-sample hypothesis
.  test that the mean (or median) is equal to a given value.
.
.  You must pre-create the variables X1 and X2.  These are the data
.  for the paired sample.
.
.  SET D0 TO CONSTANT YOU WANT TO TEST AGAINST.
.  THAT IS D0 = 0 TESTS U1 = U2 (OR U1 - U2 = 0)
.  WHILE D0 = 5 TESTS U1 - U2 = 5.
.
LET D0 = 0
.
LET DIFF = X1 - X2 - D0
RETAIN DIFF SUBSET DIFF <> 0
LET N = SIZE DIFF
LET TAG = 1 FOR I = 1 1 N
LET TAG = 2 SUBSET DIFF < 0
LET ADIFF = ABS(DIFF)
LET R = RANK ADIFF
LET WPLUS = SUM R SUBSET TAG = 1
LET WMINUS = SUM R SUBSET TAG = 2
LET W = MIN(WPLUS,WMINUS)
.
FEEDBACK OFF
IF N > 30
  LET UU = N*(N+1)/4
  LET SIGMA = SQRT(N*(N+1)*(2*N+1)/24)
  LET Z = (W - UU)/SIGMA
  LET ALPHA = 0.05
  LET ALPHA2 = 1.0 - ALPHA/2
  LET CRITICAL = NORPPF(ALPHA2)
  PRINT " "
  PRINT "H0: U1 - U2 = ^D0"
  PRINT "HA: U1 - U2 <> ^D0"
  PRINT "WILCOXON RANKED SIGN STATISTIC = ^W"
  PRINT "NORMAL APPROXIMATION FOR WILCOXON RANKED SIGN STATISTIC = ^Z"
  PRINT "NORMAL CRITICAL VALUE = +/- ^CRITICAL"
  LET Z2 = ABS(Z)
  IF Z2 <= CRITICAL
    PRINT "ACCEPT NULL HYPOTHESIS AT THE ^ALPHA SIGNIFICANCE LEVEL"
  END OF IF
  IF Z2 > CRITICAL
    PRINT "REJECT NULL HYPOTHESIS AT THE ^ALPHA SIGNIFICANCE LEVEL"
  END OF IF
END OF IF
.
.  WCRIT CONTAINS THE CRITICAL VALUES FOR A TWO-SIDED TEST AND
.  ALPHA = 0.05.  CONSULT TABLES FOR DIFFERENT SIGNIFICANCE LEVEL.
.  N NEEDS TO NE AT LEAST 6 IN ORDER TO OBTAIN A 0.05 SIGNIFICANCE
.  LEVEL.
.
IF N <= 30
  SERIAL READ WCRIT
  0 0 0 0 0
  1 2 4 6 8 11 14 17 21 25 30 35 40 46 52 59 66 73 
  81 90 98 107 117 127 137
  END OF DATA
  LET CRITICAL = WCRIT(N)
  PRINT " "
  PRINT "H0: U1 - U2 = ^D0"
  PRINT "HA: U1 - U2 <> ^D0"
  PRINT "WILCOXON RANKED SIGN STATISTIC = ^W"
  PRINT "CRITICAL VALUE = ^CRITICAL"
  IF W > CRITICAL
    PRINT "ACCEPT NULL HYPOTHESIS AT THE 0.05 SIGNIFICANCE LEVEL"
  END OF IF
  IF W <= CRITICAL
    PRINT "REJECT NULL HYPOTHESIS AT THE 0.05 SIGNIFICANCE LEVEL"
  END OF IF
END OF IF
.
