. This is Dataplot program file    PB12LIN2.DP
. Analysis of half of Lin's 12-run Plackett-Burman design
. thus will be k = 10 and n = 6
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = PB12LIN2.DP
.
SKIP 25
READ PB12LIN.DAT X1 TO X11
. LET Y = 20 + 0.5*(10*X3+5*X5+2*X8+1*X10)
LET Y = 20 + 0.5*(10*X1)
RETAIN Y X1 X2 X3 X4 X5 SUBSET X11 -1
RETAIN X6 X7 X8 X9 X10 X11 SUBSET X11 -1
.
X3LABEL AUTOMATIC
DEX MEAN PLOT Y X1 TO X10
CALL MARK.DP
.
TIC LABEL SIZE 5
LABEL SIZE 5
MULTIPLOT 3 1
DEX MEAN PLOT Y X1 TO X10
DEX SD PLOT Y X1 TO X10
DEX SN+ PLOT Y X1 TO X10
MULTIPLOT OFF
CALL MARK.DP
.
X3LABEL
LET NUMFAC = 10
LET DP = 1
YLIMITS 15 25
CALL DEXSSCOR.DP
.
X3LABEL AUTOMATIC
DEX MEAN PLOT Y X1 TO X10
.
LET M = MEAN Y
LET M3 = M FOR I = 1 1 10
LET TAG3 = 1 1 10
LOOP FOR K = 1 1 10
   LET CC3(K) = CORRELATION X1 X^K
END LOOP
LET CC4 = ABS(CC3)
.
MULTIPLOT 2 1
YLIMITS
PLOT CC3
LINES
SPIKE ON
PLOT CC4
LINES SOLID
SPIKE OFF
MULTIPLOT OFF
CALL MARK.DP
.
LET NUMFAC = 10
LET KMAX = NUMFAC-1
LET LMAX = NUMFAC
FEEDBACK OFF
LET PASS = 0
LOOP FOR K = 1 1 KMAX
   LET KP1 = K+1
   LOOP FOR L = KP1 1 LMAX
      LET PASS = PASS+1
      LET C(PASS) = CORRELATION X^K X^L
      LET ROW(PASS)=K
      LET COL(PASS)=L
   END LOOP
END LOOP
LET C2 = ABS(C)
.
X3LABEL AUTOMATIC
CHAR X ALL
LINES BLANK ALL
MULTIPLOT 2 2
YLIMITS
PLOT C SUBSET ROW 1
PLOT C2 SUBSET ROW 1
PLOT C
PLOT C2
CHAR BLANK ALL
LINES SOLID ALL
MULTIPLOT OFF
CALL MARK.DP
.
PRE-ERASE OFF
YLIMITS 15 25
XLIMITS 0 15
CHAR BLANK
X3LABEL
ERASE
DEX MEAN PLOT Y X1 TO X10
CHAR CIRCLE
CHAR FILL ON
CHAR HW 1.5 1
PLOT M3 TAG3 SUBSET CC3 <> 0
.
