. THIS IS DATAPLOT PROGRAM FILE     NORRIS6.DP
. CALIBRATION OF OZONE DETECTORS (PHOTOMETERS)
. JIM NORRIS, CHEMISTRY
. GUEST = CANADA
. FEBRUARY 1994
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
LET STRING MARK = NORRIS6.DP
.
. ------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
READ NORRIS6.DAT Y X YEAR MACH REP TAG
DELETE X YEAR MACH REP TAG Y SUBSET Y < -900
LET DEL = Y-X
.
. ------------------------
. STEP 2--PLOT    GUEST-NIST    RAW DATA
.
MULTIPLOT 3 3
FEEDBACK OFF
CHAR 1 2 3; LINES BLANK ALL
Y1LABEL GUEST-NIST
X1LABEL NIST
X3LABEL
CHAR SIZE 4 ALL
LABEL SIZE 4
TIC LABEL SIZE 4
LEGEND SIZE 4
LET DUMMY = 0
LOOP FOR YK = 1991 1 1993
   LOOP FOR MK = 1 1 3
      LET DUMMY = DUMMY + 1
      IF MK = 1; LET M = 4063; END IF
      IF MK = 2; LET M = 1626; END IF
      IF MK = 3; LET M = 254; END IF
      LEGEND 1 ^M / ^YK
      PLOT DEL X REP SUBSET MACH ^M SUBSET YEAR ^YK
      IF DUMMY = 1; LABEL
   END LOOP
END LOOP
MULTIPLOT OFF
MOVE 50 92; JUST CENTER; HW 3 2
TEXT MULTI-MACHINE & MULTI-YEAR ANALYSIS (FLOATING LIMITS)
CALL MARK.DP
.
MULTIPLOT 3 3
YLIMITS -20 10
FRAME CORNER COORDINATES 0 0 100 100
TICS OFF
TIC LABELS OFF
LABELS OFF
LEGEND 1 COORDINATES 5 95
LET DUMMY = 1
LOOP FOR YK = 1991 1 1993
   LOOP FOR MK = 1 1 3
      LET DUMMY = DUMMY + 1
      IF MK = 1; LET M = 4063; END IF
      IF MK = 2; LET M = 1626; END IF
      IF MK = 3; LET M = 254; END IF
      LEGEND 1 ^M / ^YK
      PLOT DEL X REP SUBSET MACH ^M SUBSET YEAR ^YK
   END LOOP
END LOOP
MULTIPLOT OFF
MOVE 50 92; JUST CENTER; HW 3 2
TEXT MULTI-MACHINE & MULTI-YEAR ANALYSIS (COMMON LIMITS)
CALL MARK.DP
LEGEND 1  ; LABEL  ; YLIMITS
.
. ------------------------------------------------------------------
. STEP 3--GENERATE INTERECEPT & SLOPE PLOTS FOR ALL MACHINES & YEARS
.
X3LABEL AUTOMATIC
MULTIPLOT 2 2
CHAR X BLANK; LINES BLANK SOLID
LABEL SIZE 3; TIC LABELS SIZE 3; CHAR SIZE 3 ALL
LINEAR INTERCEPT PLOT Y X TAG
LINEAR SLOPE PLOT Y X TAG
LINEAR RESSD PLOT Y X TAG
LINEAR CORRELATION PLOT Y X TAG
MULTIPLOT OFF
MOVE 50 96; JUST CENTER; HW 3 2
TEXT INTERCEPT & SLOPE ANALYSIS
HW 2 1
TEXT MACHINES = ALL     YEARS = ALL
CALL MARK.DP
.
. ------------------------------------------------------------
. STEP 4--GENERATE INTERECEPT & SLOPE PLOTS FOR TARGET MACHINE
 
LET TARGET = 1626
.
MULTIPLOT 2 2
LINEAR INTERCEPT PLOT Y X TAG SUBSET MACH ^TARGET
LINEAR SLOPE PLOT Y X TAG SUBSET MACH ^TARGET
LINEAR RESSD PLOT Y X TAG SUBSET MACH ^TARGET
LINEAR CORRELATION PLOT Y X TAG SUBSET MACH ^TARGET
MULTIPLOT OFF
MOVE 50 96; JUST CENTER; HW 3 2
TEXT INTERCEPT AND SLOPE ANALYSIS
HW 2 1
TEXT MACHINE = ^TARGET     YEARS = ALL
CALL MARK.DP
.
. ------------------------------------------------------------
. STEP 5--GENERATE INTERECEPT & SLOPE PLOTS FOR TARGET MACHINE
.         AND FOR 1993 ONLY
.
LET TARGET = 1626
.
MULTIPLOT 2 2
LINEAR INTERCEPT PLOT Y X TAG SUBSET MACH ^TARGET SUBSET YEAR 1993
LINEAR SLOPE PLOT Y X TAG SUBSET MACH ^TARGET SUBSET YEAR 1993
LINEAR RESSD PLOT Y X TAG SUBSET MACH ^TARGET SUBSET YEAR 1993
LINEAR CORRELATION PLOT Y X TAG SUBSET MACH ^TARGET SUBSET YEAR 1993
MULTIPLOT OFF
MOVE 50 96; JUST CENTER; HW 3 2
TEXT INTERCEPT AND SLOPE ANALYSIS
HW 2 1
TEXT MACHINE = ^TARGET     YEAR = 1993
CALL MARK.DP
.
. -----------------------------------------------
. STEP 6--FIT ALL SETS & STORE SUMMARY STATISTICS
.
. FEEDBACK OFF
CAPTURE NORRIS6.OUT
LET N = NUMBER Y
LOOP FOR K = 1 1 24
   LET SEQ = SEQUENCE 1 1 N
   RETAIN SEQ SUBSET TAG K
   LET ROW = SEQ(1)
   .
   LET YE = YEAR(ROW)
   LET M = MACH(ROW)
   LET R = REP(ROW)
   LET T = TAG(ROW)
   .
   LET YEAR2(K) = YE
   LET MACH2(K) = M
   LET REP2(K) = R
   LET TAG2(K) = T
   .
   WRITE "-------------------------------------------"
   WRITE "TAG = ^T   MACHINE = ^M     YEAR = ^YE    REP = ^R"
   WRITE " "
   .
   FIT Y = A0+A1*X SUBSET TAG K
   LET INT2(K)=A0
   LET SLOPE2(K)=A1
   LET RSD2(K) = RESSD
   SKIP 0; READ DPST1F.DAT COEF SDCOEF
   LET SDINT2(K) = SDCOEF(1)
   LET SDSLOPE2(K) = SDCOEF(2)
.
END LOOP
END CAPTURE
.
. ------------------------
. STEP 7--PLOT XXX
.
MULTIPLOT 2 2
LABEL AUTOMATIC
X3LABEL AUTOMATIC
CHAR 1 2 3 4 5 6 7 8 9
LET DINT2 = 2*SDINT2
LET DSLOPE2 = 2*SDSLOPE2
LINES BLANK ALL
PLOT INT2 SLOPE2 TAG2 SUBSET MACH2 ^TARGET
PLOT INT2 SLOPE2 TAG2 SUBSET MACH2 ^TARGET SUBSET YEAR2 1993
LINES SOLID ALL; LINES BLANK
ERROR BAR PLOT INT2 DINT2 DINT2 SLOPE2 DSLOPE2 DSLOPE2 ...
          SUBSET MACH2 ^TARGET
ERROR BAR PLOT INT2 DINT2 DINT2 SLOPE2 DSLOPE2 DSLOPE2  ...
          SUBSET MACH2 ^TARGET
MULTIPLOT OFF
CALL MARK.DP
 
