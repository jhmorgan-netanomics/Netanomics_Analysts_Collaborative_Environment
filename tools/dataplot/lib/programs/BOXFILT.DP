. THIS IS THE DATAPLOT PROGRAM FILE     BOXFILT.DP
. FILTRATION TIME AT NEW INDUSTRIAL PLANT
. BOX, HUNTER, & HUNTER, P. 424-429
. COMPLETE ANALYSIS OF 2**(7-4) DESIGN
. INPUT DATA FILE--BOXFILT.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. -----START POINT----------
.
DIMENSION 40 VARIABLES
DEVICE 2 POSTSCRIPT
.
. STEP 1--READ IN THE DATA (N=8)
.
SKIP 25
READ BOXFILT.DAT Y X1 X2 X3 X4 X5 X6 X7
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5 X6 X7
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5 X6 X7
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
ER
MULTIPLOT OFF
X3LABEL
LET NUMFAC = 7
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 40 80
LET STRING S0 = FILTRATION TIME (MIN)
LET STRING S1 = WATER SUPPLY (X1)
LET STRING S2 = RAW MATERIAL (X2)
LET STRING S3 = TEMPERATURE (X3)
LET STRING S4 = RECYCLE (X4)
LET STRING S5 = CAUSTIC SODA (X5)
LET STRING S6 = FILTER CLOTH (X6)
LET STRING S7 = HOLDUP TIME (X7)
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 5 5
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 10 ALL; LABEL SIZE 10; TIC LABEL SIZE 10
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3 X1
BLOCK PLOT Y X4 X5 X1
BLOCK PLOT Y X6 X7 X1
BLOCK PLOT Y X1 X3 X2
BLOCK PLOT Y X4 X5 X2
BLOCK PLOT Y X6 X7 X2
BLOCK PLOT Y X1 X2 X3
BLOCK PLOT Y X4 X5 X3
BLOCK PLOT Y X6 X7 X3
BLOCK PLOT Y X1 X2 X4
BLOCK PLOT Y X3 X5 X4
BLOCK PLOT Y X6 X7 X4
BLOCK PLOT Y X1 X2 X5
BLOCK PLOT Y X3 X4 X5
BLOCK PLOT Y X6 X7 X5
BLOCK PLOT Y X2 X4 X5
BLOCK PLOT Y X1 X2 X6
BLOCK PLOT Y X3 X4 X6
BLOCK PLOT Y X5 X7 X6
BLOCK PLOT Y X1 X2 X7
BLOCK PLOT Y X3 X4 X7
BLOCK PLOT Y X5 X6 X7
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
ER
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B5 = COEF(1)
LET B3 = COEF(2)
LET B1 = COEF(3)
LET RESSD = RESSD2(3)
LET PRED = MU+0.5*(B5*X5+B3*X3+B1*X1)
LET RES = Y-PRED
ER
. SET WRITE DECIMALS 2
SET WRITE FORMAT 7F8.0,3F9.2
WRITE X1 X2 X3 X4 X5 X6 X7 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 X6 X7 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU + 0.5*(^B5*X5 + ^B3*X3 + ^B1*X1 )
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
LABEL SIZE 3
. PLOT RES RUNSEQ
PLOT
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 30 40 50 60 70 80 90
LET U1 = X5
LET U2 = X3
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL CAUSTIC SODA RATE (X5)
Y1LABEL TEMPERATURE (X3)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
ER
LET YSORT = SORTC Y X1 X2 X3 X4 X5 X6 X7
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 X5 X6 X7 YSORT
WRITE OUT3. X1 X2 X3 X4 X5 X6 X7 YSORT
.
. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
. STEP 11--READ IN THE  FULL (ORIGINAL + FOLDOVER) DATA (N = 16)
.
SKIP 25
READ BOXFILT2.DAT Y X1 X2 X3 X4 X5 X6 X7
.
. STEP 12--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5 X6 X7
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5 X6 X7
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 13--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
ER
MULTIPLOT OFF
X3LABEL
YLIMITS 50 90
CALL DEXSTAT2.DP
YLIMITS
.
