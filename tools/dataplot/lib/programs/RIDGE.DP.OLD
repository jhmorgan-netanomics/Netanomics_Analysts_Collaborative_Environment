.
.  COMPUTE A RIDGE REGRESSION.
.  USE BODY FAT DATA SET FROM NETER, WASSERMAN, AND KUNTER (SEE
.  THEIR RIDGE REGRESSION EXAMPLE ON PAGE 412-415).
.
DIMENSION 200 COLUMNS
LET P = 3
READ subject z1 z2 z3 y
1 19.5 43.1 29.1 11.9
2 24.7 49.8 28.2 22.8
3 30.7 51.9 37.0 18.7
4 29.8 54.3 31.1 20.1
5 19.1 42.2 30.9 12.9
6 25.6 53.9 23.7 21.7
7 31.4 58.5 27.6 27.1
8 27.9 52.1 30.6 25.4
9 22.1 49.9 23.2 21.3
10 25.5 53.5 24.8 19.3
11 31.1 56.6 30.0 25.4
12 30.4 56.7 28.3 27.2
13 18.7 46.5 23.0 11.7
14 19.7 44.2 28.6 17.8
15 14.6 42.7 21.3 12.8
16 29.5 54.4 30.1 23.9
17 27.7 55.3 25.7 22.6
18 30.2 58.6 24.6 25.4
19 22.7 48.2 27.1 14.8
20 25.2 51.0 27.5 21.1
FEEDBACK OFF
.
LET N = SIZE Y
.
.  Generate standardized regression model
.
LET FACT = 1/SQRT(N-1)
LOOP FOR K = 1 1 P
    LET XMEAN^K = MEAN Z^K
    LET XSD^K = STANDARD DEVIATION Z^K
    LET X^K = FACT*((Z^K-XMEAN^K)/XSD^K)
END OF LOOP
LET XMAT = MATRIX DEFINITION X1 N P
LET YMEAN = MEAN Y
LET YSD = STANDARD DEVIATION Y
LET YT = FACT*((Y-YMEAN)/YSD)
.
.  GENERATE VARIOUS CORRELATION MATRICES
.
LET RXX = CORRELATION MATRIX XMAT
LOOP FOR K = 1 1 P
    LET A = CORRELATION YT X^K
    LET RYX(K) = A
END OF LOOP
.
.  DEFINE VALUES OF C FOR RIDGE TRACE
.
SERIAL READ C
0 .001 .002 .003 .004 .005 .006 .007 .008 .009 
.01 .02 .03 .04 .05 .06 .07 .08 .09 
.1 .2 .3 .4 .5 .6 .7 .8 .9 1.0
END OF DATA 
LET NC = SIZE C
.
.  SOLVE VIA: 
.  1) B = (RXX + cI)**(-1)RYX
.  2) VIF = diagonal(RXX+cI)**(-1)*RXX*(RXX+cI)**(-1))
LOOP FOR K = 1 1 NC
    LET A = C(K)
    LET CDIAG = A FOR I = 1 1 P
    LET CMAT = DIAGONAL MATRIX CDIAG
    LET MATTMP = MATRIX ADDITION RXX CMAT
    LET MATINV = MATRIX INVERSE MATTMP
    LET BR = MATRIX MULTIPLY MATINV RYX
    PRINT BR
    LET MATZZZ = MATRIX MULTIPLY MATINV RXX
    LET MATZZZ = MATRIX MULTIPLY MATZZZ MATINV
    LET VIFALL = MATRIX DIAGONAL MATZZZ
    LOOP FOR L = 1 1 P
        LET B^L(K) = BR(L)
        LET VIF^L(K) = VIFALL(L)
    END OF LOOP
END OF LOOP
PRINT B1 B2 B3 VIF1 VIF2 VIF3
.
.  Plot ridge trace
.
TITLE RIDGE TRACE
X1LABEL C
Y1LABEL BLC()K
LINE SOLID DASH DOT
PLOT B1 B2 B3 VS C
.
. AFTER PICKING "BEST" VALUE OF C, CAN TRANSFORM BACK TO ORIGINAL UNITS:
.
.   Bk = (Sy/Sk)*B'k  (B'k are the standardized parameter estimates)
.   B0 = YMEAN - SUM(Bk*XMEANk)
.
