. THIS IS DATAPLOT MACRO     TESTPPCC.DP
. PURPOSE--ILLUSTRATE/TEST VARIOUS PROBABILITY PLOTS AND PPCC PLOTS
.
. -----START POINT
.
DEVICE 2 POSTSCRIPT
.
. -----------------------------------
. STEP 1--GENERATE THE SIMULATED DATA
. -----------------------------------
.
LET GAMMA = 1.8
LET Y = WEIBULL RANDOM NUMBERS FOR I = 1 1 100
LET Y2 = 10+20*Y
SET WRITE DECIMALS 3
WRITE JUNK. Y Y2
.
. -----------------------------------
. STEP 2--TEST UNDERLYING ASSUMPTIONS
. -----------------------------------
.
X3LABEL AUTOMATIC
LABEL SIZE 4
HW 4 3
4-PLOT Y2
.
. -----------------------------------------
. STEP 3--DISTRIBUTIONALLY ANALYZE THE DATA
. -----------------------------------------
.
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT Y;         MOVE 17 85; TEXT PPCC = ^PPCC
NORMAL PROBABILITY PLOT Y2;        MOVE 17 85; TEXT PPCC = ^PPCC
LOGISTIC PROBABILITY PLOT Y2;      MOVE 17 85; TEXT PPCC = ^PPCC
CAUCHY PROBABILITY PLOT Y2;        MOVE 17 85; TEXT PPCC = ^PPCC
.
MULTIPLOT 2 2
TUKEY PPCC PLOT Y2;                MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
PARETO PPCC PLOT Y2;               MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
EV2 PPCC PLOT Y2;                  MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
WEIBULL PPCC PLOT Y2;              MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
.
MULTIPLOT 2 2
WEIBULL PLOT Y2;                   MOVE 17 85; TEXT ETA = ^ETA
                                   MOVE 17 80; TEXT BETA = ^BETA
WEIBULL PPCC PLOT Y2;              MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
LET GAMMA1 = 1
LET GAMMA2 = 3
WEIBULL PPCC PLOT Y2;              MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
                                   MOVE 17 80; TEXT GAMMA = ^SHAPE
LET GAMMA = ^SHAPE
WEIBULL PROBABILITY PLOT Y2;       MOVE 17 85; TEXT MAXPPCC = ^MAXPPCC
ER
CAPTURE OUT.
LINEAR FIT YPLOT XPLOT
END CAPTURE
.
