. This is dataplot macro    testblockplot_boxreactor_5_32.dp  (new)    2/11/19
. Purpose: Do block plot (k = 1+4 = 5, n = 32) for Box Reactor
. Design: 2**5 Full Factorial Design
.         Primary Factor:    Weld  (2)
.         Robustness Factor: Plant (2)
.                            Speed (2)
.                            Shift (3)
. Date: September 2010
.
. -----start point-----
.
feedback off
.
let string header = Box, Hunter, & Hunter Chemical Reactor (k=5,n=32)
let string headhw1 = 3 2
let string headhw2 = 2.6 1.3
let string headhw3 = 2.6 1.3
let string currdate = 4/10/19
.
let pagecoun = 0
let string trailer = testblockplot_boxreactor_5_32.dp
let string trailer2 = Datafile = boxreactor_5_32.dat
.
. -----Step 1: Read in the data-----
.
let k = 5
let n = 32
.
skip 25
read boxreactor_5_32.dat y x1 x2 x3 x4 x5
stat v
.
. -----Step 2: Define strings-----
.
let string sty = $ Reacted
.
let string stx1 = Feed Rate
let string stx2 = Catalyst
let string stx3 = Agitation Rate
let string stx4 = Temperature
let string stx5 = Concentration
.
let string stxsh1 = FeedRate
let string stxsh2 = Cat
let string stxsh3 = AgRate
let string stxsh4 = Temp
let string stxsh5 = Conc
.
. -----Step 3: Define needed vectors-----
.
loop for j = 1 1 k
   let xc^j = code x^j
end loop
stat v
. dex mean plot y xc1 to xc^k
.
. -----Step 10: Do preliminary dex scatter plots and dex mean plots-----
.
let string header2 = Q. What is the Relative Importance of the Factors?
call dexsp.dp
call annotate.dp
.
call dexmp.dp
call annotate.dp
.
. -----Step 11: Generate 6 => 4block plots for each of the 5 primary factors-----
.
let numrob = k-1
loop for case = 1 1 k
   delete xprim; let xprim = x^case
   let string stprim = ^stx^case
   let string header2 = Q. Does X^case = ^stx^case  Have an Effect on ^sty?
   .
   let count = 0
   loop for j = 1 1 k
      if j <> case; let count = count + 1; let rid^count = ^j; end if
   end loop      
   .
   char bl all; char 1 2; char color blue red 
   lines solid all; lines bl bl
   let string stlegend = X^case: ^stx^case (2 levels: -1, +1)
   let string stevent = Level1 < Level2; let lower = -1; let upper = +1
   let string corncol = blue
   .
   delete tag cond
   loop for jrob = 1 1 numrob
      delete xrob^jrob; let xrob^jrob = x^rid^jrob
   end loop
   .
   let tag = 8*(xc^rid1-1) + 4*(xc^rid2-1) + 2*(xc^rid3-1) + xc^rid4
   . plot tag
   . pause
   .
   let tag = code tag
   let condp = tag
   delete cond; let cond = condp
   x2label Robustness Conditions (X^rid1,X^rid2,X^rid3,X^rid4): (^stx^rid1,^stx^rid2,^stx^rid3,^stx^rid4)
   .
   let dcondp = distinct condp
   let ndcondp = number dcondp
   .
   y1label ^sty
   x3label ^stbott
   xlim 1 ndcondp
   x1tics off
   x1tic labels off
   y1tic offset units screen; y1tic offset 5 20
   x1tic offset units screen; x1tic offset 5 5
   if ndcondp <= 5; x1tic offset units screen; x1tic offset 10 10; end if
   .
   . call blockplot_6plots.dp
   call blockplot_4plots.dp
   .
   x1tic offset units screen; x1tic offset 5 5
end loop

