. THIS IS THE DATAPLOT PROGRAM FILE     BALLSTICK.DP
. PURPOSE--GENERATE A (PARETO) BALL-AND-STICK PLOT
. DATE--NOVEMBER 1987
.
. -----START POINT-----------------------------------
.
DEVICE 2 TEKT 4014
.
SERIAL READ Y
35 33 32 24 16 12 9 7 4 4 2 0
END DATA
READ FUNCTION S1 TO S8
"IN-HOUSE" DATAPLOT "OTHER" LOTUS CMLIB OMNITAB IMSL NAG
READ FUNCTION S9 TO S13
STARPAC BMDP RS/1 MINITAB SAS
LET N = NUMBER Y
LET NP2 = N+2
.
LET XOFF=1
LET YOFF=2
HW 1.5 0.75
FONT SIMPLEX
DEGREES
ANGLE 45
.
TITLE NBS STATISTICAL COMPUTING SURVEY (1987)
Y1LABEL PERCENT UTILIZATION
XLABEL SOFTWARE SYSTEM
XTICS OFF
LINES
CHAR CIRCLE
CHARACTER FILL ON
SPIKE ON
.
LET X1D = 0
LET X2D = NP2
LET Y1D = 0
LET Y2D = 50
XLIMITS X1D X2D
YLIMITS Y1D Y2D
.
LET X1P = 20
LET X2P = 80
LET Y1P = 20
LET Y2P = 80
FRAME CORNER COORDINATES X1P Y1P X2P Y2P
.
FEEDBACK OFF
PLOT Y
.
LOOP FOR K = 1 1 N
LET XD = K
LET XRATIO = (XD-X1D)/(X2D-X1D)
LET XP = X1P + (X2P-X1P)*XRATIO
LET XP = XP+XOFF
LET YD = Y(K)
LET YRATIO = (YD-Y1D)/(Y2D-Y1D)
LET YP = Y1P + (Y2P-Y1P)*YRATIO
LET YP = YP+YOFF
MOVE XP YP
TEXT ^S^K
END LOOP
.
COPY
