. This is Dataplot program      NAKATAN1.DP
. Non-linear fit of Nakatani Data Set 1
. Date--January 1998
.
. -----start point-----
.
DEVICE 1 X11
DEVICE 2 POSTSCRIPT
.
. ----- STEP 1--READ IN THE DATA
.
SKIP 25
READ NAKATAN1.DAT Y X SDY
DELETE Y X SDY FOR I = 1 1 8
.
. ----- STEP 2--PLOT THE DATA
.
TITLE NAKATAN1.DAT
X3LABEL AUTOMATIC
CHARACTERS X BLANK
LINES BLANK SOLID
MULTIPLOT 2 2
PLOT Y X
.
. ----- STEP 3--SELECT THE MODEL, DEFINE STARTING VALUES, FIT THE DATA
.
LET FUNCTION DENOM = 1 + (X*A2)**2
LET FUNCTION F = (A1/DENOM) + A3
.
LET A1 = 80
LET A2 = 100
LET A3 = 3
.
FIT Y = F
.
SET WRITE DECIMALS 7
LET N = NUMBER Y
WRITE NAKATAN1.OUT N A1 A2 A3 RESSD
.
. ----- STEP 4--DO POST-FIT GRAPHICAL ANALYSIS
.
PLOT Y PRED VERSUS X
LET ZZERO = Y-Y
PLOT RES ZZERO VERSUS X
NORMAL PROBABILITY PLOT RES
MULTIPLOT OFF
.
. ----- STEP 5--WRITE OUT CONCLUSIONS
.
WRITE N A1 A2 A3 RESSD
