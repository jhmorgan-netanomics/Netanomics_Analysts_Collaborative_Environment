. THIS IS DATAPLOT MACRO   DDS.DP
. PURPOSE--CARRY OUT DDS ANALYSIS USING DATAPLOT'S DDS COMMAND
. NOTE--THE FOLLOWING MUST PRE-EXIST BEFORE CALLING THIS MACRO--
.    1. FILENAME = DATAPLOT STRING =    NAME OF INPUT DATA FILE
.    2. NUMSKIP  = DATAPLOT PARAMETER = NUMBER OF HEADER LINES TO SKIP
.    3. READFLAG = DATAPLOT PARAMETER = 1 (IF READ) OR 2 (IF SERIAL READ)
.    4. ORDER1   = DATAPLOT PARAMETER = AUTOREGRESSIVE ORDER
.    5. ORDER1   = DATAPLOT PARAMETER = MOVING AVERAGE ORDER
. NOTE--DATAPLOT    DDS   COMMAND (WITH 3 ARGUMENTS) FORMAT--
.    DDS <NAME OF VARIABLE> <AUTOREG. ORDER> <MOVING AVERAGE ORDER>
. CONTACT--
.    SUDHAKAR M. PANDIT
.    GHANASHYAM A. JOSHI
.    JAMES J. FILLIBEN
. DATE--AUGUST 1994
. EXAMPLE OF USAGE--
.    LET STRING FILENAME = SIN.DAT
.    LET NUMSKIP = 25
.    LET READFLAG = 1
.    LET ORDER1 = 2
.    LET ORDER2 = 1
.    CALL DDS.DP
.
. -----START POINT-----
.
DEVICE 2 POSTSCRIPT
DEFINE ER ESC FF
TIC OFFSET UNITS SCREEN; TIC OFFSET 5 5
LET FUNCTION MARK = DDS.DP
.
. ---------------------------------------------------------------
. STEP 1--READ IN RAW DATA
.
IF READFLAG = 1
   SKIP NUMSKIP
   READ ^FILENAME Y
END IF
IF READFLAG = 2
   SKIP NUMSKIP
   SERIAL READ ^FILENAME Y
END IF
.
. ---------------------------------------------------------------
. STEP 2-- CARRY OUT DDS ANALYSIS
 
DDS Y ORDER1 ORDER2
 
. ---------------------------------------------------------------
. STEP 3--READ IN THE DDS RESIDUALS, GREEN FUNCTION, AND SPECTRUM
.
SKIP 4
READ RESIDUAL.OUT RESIDUALS
SKIP 3
READ GREEN.OUT INDEX GREENTOT GREEN1 GREEN2 GREEN3 GREEN4
SKIP 3
READ SPECTRUM.OUT FREQ SPECTOT SPEC1 SPEC2 SPEC3 SPEC4
.
. ----------------------------------------------
. STEP 4--GENERATE A MULTIPLOT OF THE DDS OUTPUT
.
MULTIPLOT 2 2
FEEDBACK OFF
TIC LABEL SIZE 2
LABEL SIZE 3
TITLE DISPLACEMENT 3; TITLE SIZE 4
.
TITLE RAW DATA; Y1LABEL X(T); X1LABEL T
XLIMITS 0 150
LINES SOLID
PLOT Y
.
TITLE GREEN'S FUNCTION; Y1LABEL G(J); X1LABEL J
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
PLOT GREENTOT GREEN1 GREEN2 GREEN3 GREEN4 VERSUS INDEX
.
TITLE RESIDUALS; Y1LABEL A(T); X1LABEL T
XLIMITS 0 150
LINES
SPIKE ON
PLOT RESIDUALS
.
TITLE SPECTRUM; Y1LABEL S(F); X1LABEL F
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
SPIKE OFF
PLOT SPECTOT SPEC1 SPEC2 SPEC3 SPEC4 VERSUS FREQ
.
CALL MARK.DP
