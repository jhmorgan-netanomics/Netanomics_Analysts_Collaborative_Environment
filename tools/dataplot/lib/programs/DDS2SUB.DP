. THIS IS DATAPLOT PROGRAM FILE DDS2SUB.DP
. PURPOSE--SUBPROGRAM CALLED BY TESTDDS2.DP
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. FILENAME = DATAPLOT STRING    = NAME OF INPUT FILE
.       2. ORDER1   = DATAPLOT PARAMETER = AUTOREGRESSIVE ORDER
. NOTE--THE INPUT FILE IS ASSUMED TO HAVE 25 HEADER LINES
.       AND THE RESPONSE VARIABLE TO BE IN ONE LONG COLUMN.
.
. ---------------------------------------------------------------
. STEP 1--READ IN RAW DATA
.
SKIP 25
READ ^FILENAME Y
.
. ---------------------------------------------------------------
. STEP 2-- CARRY OUT DDS ANALYSIS
.
LET ORDER2 = ORDER1 - 1
DDS Y ORDER1 ORDER2
.
. ---------------------------------------------------------------
. STEP 3--READ IN THE DDS RESIDUALS, GREEN FUNCTION, AND SPECTRUM
.
SKIP 5
READ RESIDUAL.OUT RESIDUALS
SKIP 5
READ GREEN.OUT INDEX GREENTOT GREEN1
SKIP 5
READ SPECTRUM.OUT FREQ SPECTOT SPEC1
.
. ----------------------------------------------
. STEP 4--GENERATE A MULTIPLOT OF THE DDS OUTPUT
.
MULTIPLOT 2 2
FEEDBACK OFF
TIC LABEL SIZE 2
LABEL SIZE 3
TITLE DISPLACEMENT 3; TITLE SIZE 4
.
TITLE RAW DATA; Y1LABEL Y(T); X1LABEL T
LINES SOLID
PLOT Y
.
TITLE RESIDUALS; Y1LABEL A(T); X1LABEL T
LINES
SPIKE ON
PLOT RESIDUALS
SPIKE OFF
.
TITLE GREEN'S FUNCTION; Y1LABEL G(J); X1LABEL J
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
PLOT GREENTOT GREEN1 VERSUS INDEX
.
TITLE SPECTRUM; Y1LABEL S(F); X1LABEL F
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
PLOT SPECTOT SPEC1 VERSUS FREQ
.
MULTIPLOT OFF
MOVE 50 95
JUST CENTER
TEXT DDS Analysis   Data File = ^FILENAME   ARMA ^ORDER1 ^ORDER2   ...
Sres = ^RESSD
CALL MARK.DP
