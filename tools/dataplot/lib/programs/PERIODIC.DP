. THIS IS THE DATAPLOT PROGRAM FILE     PERIODIC.DP
. ANALYZE PERIODIC TABLE DATA
. DATE--APRIL 1992
.
. -----START POINT---------------
.
DIMENSION 20 VARIABLES
. DEVICE 2 POSTSCRIPT
. LET STRING MARK = PERIODIC.DP
.
. -------------------------------
. STEP 1--READ IN THE DATA
.
SKIP 25
COLUMN LIMITS 18 132
READ PERIODIC.DAT Y1 TO Y8
.
. -------------------------------
. STEP 2--DEFINE NAMES OF THE VARIABLES
.
LET STRING S1 = 1. ORDER NUM
LET STRING S2 = 2. ROW
LET STRING S3 = 3. COLUMN
LET STRING S4 = 4. ATOMIC WT
LET STRING S5 = 5. DENSITY
LET STRING S6 = 6. ATOM DIAM
LET STRING S7 = 7. TERR ABUND
LET STRING S8 = 8. COEF OF RES
.
LET N = NUMBER Y1
LET INDEX = SEQUENCE 1 1 N
.
. -------------------------------
. STEP 3--SET UP THE MULTIPLOT
MULTIPLOT CORNER COORDINATES 5 5 95 93
MULTIPLOT 8 8
FRAME CORNER COORINDATES 0 0 100 100
TICS OFF
TIC LABELS OFF
LINES
FEEDBACK OFF
.
. -------------------------------
. STEP 4--GENERATE NAMES ON THE DIAGONAL
.
LEGEND 1 COORDINATES 50 50
LEGEND 1 JUSTIFICATION CENTER
LEGEND 1 SIZE 12
CHAR
LOOP FOR K = 1 1 8
MULTIPLOT 8 8 K K
LEGEND 1 ^S^K
PLOT Y1
END LOOP
LEGEND 1
.
. -------------------------------
. STEP 5--GENERATE MATRIX OF SCATTER PLOTS
.
CHAR X ALL
CHAR SIZE 8 ALL
LOOP FOR K = 1 1 7
LET KP1=K+1
LOOP FOR L = KP1 1 8
MULTIPLOT 8 8 K L
PLOT Y^K Y^L SUBSET Y^K > 0 SUBSET Y^L > 0
END LOOP
END LOOP
.
. -------------------------------
. STEP 6--WRITE A TITLE FOR THE PAGE
.
MULTIPLOT OFF
HW 3 2
MOVE 50 95
JUSTIFICATION CENTER
TEXT ANALYSIS OF PERIODIC TABLE
.
. CALL MARK.DP
RESET PLOT CONTROL
