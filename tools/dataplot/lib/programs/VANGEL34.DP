. This is Dataplot program   VANGEL34.DP
. RECIPE Tolerance Limits Analysis of Graphite/Epoxy Composite Strength
. ANOVA Case
. Date--September 1997
.
. ----------start point----------
.
device 1 x11
device 2 postscript
.
. ------------------------
. Step 1--Read in the data
.
skip 25
read vangel34.dat y form panel batch
.
. -----------------------------------------
. Step 2--Test for randomness and normality
.
4-plot y
.
. ----------------------------------------------------------
. Step 3--Test for homoegneity (wrt location, wrt variation)
.
y1label Strength
x3label automatic
lines solid all
label size 3
tic label size 3
.
multiplot 2 3
   characters x all; lines solid all
   plot y form form
   plot y panel panel
   plot y batch batch
   characters box plot; lines box plot
   box plot y form
   box plot y panel
   box plot y batch
multiplot off
.
characters blank all
lines solid all
y1label
x3label
label size 2
tic label size 2
.
let y1 = y
let y2 = y
retain y1 subset form 1
retain y2 subset form 2
x3label automatic
bihistogram y1 y2
x3label
.
let x = form
call ftestloc.dp
call levene.dp
.
let x = panel
call ftestloc.dp
call levene.dp
.
let tag = 40*form + panel
call ftestloc.dp
call levene.dp
.
let x = form
capture vangel34.out1a
   call ftestloc.dp
   call levene.dp
end capture
.
let x = panel
capture vangel34.out1b
   call ftestloc.dp
   call levene.dp
end capture
.
. -------------------------
. Step 4--Test for outliers
.
let ysave = y
anova y form panel
let y = res
.
call grubb.dp
capture vangel34.out2
   call grubb.dp
end capture
.
. --------------------------------------
. Step 5--Test for best-fit distribution
.
multiplot 2 4
   justification center; hw 4 3
   normal probability plot y
      move 50 95; text Normal Prob Plot
      move 28 85; let ppcc = round(ppcc,4); text ^ppcc
      move 50 10; text loc = ^ppa0 scale = ^ppa1
   lognormal probability plot y
      move 50 95; text Lognormal Prob Plot
      move 28 85; let ppcc = round(ppcc,4); text ^ppcc
      move 50 10; text loc = ^ppa0 scale = ^ppa1
   tukey ppcc plot y
      move 50 95; text Tukey PPCC Plot
      move 50 85; text optimal tail length = ^shape
   let lambda = shape; tukey probability plot y
      move 50 95; text Tukey Prob Plot (^lambda)
      move 28 85; let ppcc = round(ppcc,4); text ^ppcc
      move 50 10; text loc = ^ppa0 scale = ^ppa1
let miny = minimum y
if miny > 0
   weibull plot y
      move 50 95; text 2-par. Weibull Plot
      move 50 10; text (eta = ^eta, beta = ^beta)
   let gamma = beta; weibull probability plot y
      move 50 95; text Weibull Prob Plot (^gamma)
      move 28 85; let ppcc = round(ppcc,4); text ^ppcc
      move 50 10; text loc = ^ppa0 scale = ^ppa1
end if
   weibull PPCC plot y
      move 50 95; text 3-par. Weibull PPCC Plot
      move 50 85; text optimal shape = ^shape
   let gamma = shape; weibull probability plot y
      move 50 95; text Weibull Prob Plot (^gamma)
      move 28 85; let ppcc = round(ppcc,4); text ^ppcc
      move 50 10; text loc = ^ppa0 scale = ^ppa1
multiplot off
.
pre-erase off
   erase
   title Weibull Density Functions

   let miny = min y
   let maxy = max y
   let plotxmin = 1
   if miny < 0
      let plotxmin = 100*int(2*miny/100)
      if miny < plotxmin
         let plotxmin = 10*int(2*miny/10)
      end if
      if miny < plotxmin
         let plotxmin = miny
      end if
   end if
   let plotxmax = 100*int(2*maxy/100)
   xlimits plotxmin plotxmax

   let plotymin = 0
   let plotymax = .04
   ylimits plotymin plotymax

   relative histogram y
   lines dotted; let loc = 0; let scale = ^eta; let shape = ^beta
   plot (1/scale)*weipdf((x-loc)/scale,shape) for x = plotxmin 1 plotxmax
   lines solid; let loc = ^ppa0; let scale = ^ppa1; let shape = ^gamma
   plot (1/scale)*weipdf((x-loc)/scale,shape) for x = plotxmin 1 plotxmax

   justification left; hw 2 1
   move 18 85; text 2-parameter
   lines dotted; draw 28 85 38 85
   move 18 80; text 3-parameter
   lines solid; draw 28 80 38 80

   limits
pre-erase on

. ------------------------------------------------
. Step 6--Compute (recipe/normal) tolerance limits
.
let y = ysave
.
recipe content 90
recipe confidence 95
recipe factors 1
.
recipe anova y form batch
capture vangel34.out3
   recipe anova y form batch
end capture
.
y1label Strength
x3label automatic
characters x circle box
character fill on on on
character hw  2 1 .75 1 .2 1
lines blank blank blank
label size 3
.
multiplot 2 2
   plot y pred tol versus form
   plot y pred tol versus panel
   plot y pred tol versus batch
multiplot off
.
recipe anova y form panel
capture vangel34.out4
   recipe anova y form panel
end capture
.
multiplot 2 2
   plot y pred tol versus form
   plot y pred tol versus panel
   plot y pred tol versus batch
multiplot off
.
. ------------
. Step 7--Done
.
.
