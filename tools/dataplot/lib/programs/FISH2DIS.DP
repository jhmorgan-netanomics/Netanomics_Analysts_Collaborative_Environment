.  Perform a Fisher's dsicriminant analysis
.  Case with 2 populations (has simplifications from general case).
.
.  Hemophillia data from "Applied Multivariate Statistical Analysis"
.  by Johnson and Wichern (p. 570).
.
.  AHFACT = LOG10(AHF activity)
.  AHFANT = LOG10(AHF antigen)
.
.  TAG = 1 - Noncarriers
.  TAG = 2 - Carriers
FEEDBACK OFF
DELETE X
DIMENSION 200 COLUMNS
SKIP AUTOMATIC
READ FISH2POP.DAT AHFACT AHFANT TAG
SKIP 0
.
LET NTOT= SIZE AHFACT
LET P = 2
LET X = MATRIX DEFINITION AHFACT NTOT P
LET GROUPID = DISTINCT TAG
LET NG = 2
LET K1 = GROUPID(1)
LET N1 = SIZE TAG SUBSET TAG = K1
LET K2 = GROUPID(2)
LET N2 = SIZE TAG SUBSET TAG = K2
LET N = DATA N1 N2
.
.  CALCULATE DIFFERENCE IN MEAN VECTORS FOR 2 GROUPS
.
LET XMEAN1 = MATRIX COLUMN MEAN X SUBSET TAG = K1
LET XMEAN2 = MATRIX COLUMN MEAN X SUBSET TAG = K2
LET XDIFF = XMEAN1 - XMEAN2
LET XM = MATRIX DEFINITION XDIFF P 1
LET XMT = MATRIX TRANSPOSE XM
PRINT " "; PRINT " "; PRINT "(XBAR1-XBAR2) MATRIX:"; PRINT XM
.
.  CALCULATE Spooled = (N1-1)S1 + .. + (Ng-1)Sg)/(N1+..+ Ng - g)
LET ATEMP = N1 - 1
LET SA = VARIANCE-COVARIANCE MATRIX X SUBSET TAG = K1
LET SA = MATRIX MULTIPLICATION SA ATEMP
LET ATEMP = N2 - 1
LET SB = VARIANCE-COVARIANCE MATRIX X SUBSET TAG = K2
LET SB = MATRIX MULTIPLICATION SB ATEMP
LET SPOOL = MATRIX ADDITION SA SB
LET DENOM = 1/(NTOT - NG)
LET SPOOL = MATRIX MULTIPLY SPOOL DENOM
LET SPINV = MATRIX INVERSE SPOOL
PRINT " "; PRINT " "
PRINT "INVERSE OF POOLED COVARIANCE MATRIX:"; PRINT SPINV
LET DISCRM = MATRIX MULTIPLY XMT SPINV
LET DTEMP = MATRIX MULTIPLY DISCRM XM
LET D2 = DTEMP1(1)
PRINT " "; PRINT " "; PRINT "MAXIMUM SEPARATION = ^D2"
PRINT " "; PRINT " "
PRINT "COLUMNS ARE THE DISCRIMINANT FUNCTIONS:"; PRINT DISCRM
.  PLOT FIRST 2 DISCRIMINANTS
LET Y = 0 FOR I = 1 1 NTOT
LOOP FOR K = 1 1 P
    LET FACT = DISCRM^K(1)
    LET Y = Y + FACT*X^K
END OF LOOP
LET MEAN1 = MEAN Y SUBSET TAG = K1
LET XPT1 = N1/2
LET MEAN2 = MEAN Y SUBSET TAG = K2
LET XPT2 = N1 + N2/2
LET MEANY = DATA MEAN1 MEAN2
LET XSEQ2 = DATA XPT1 XPT2
.
LET XSEQ = SEQUENCE 1 1 NTOT
Y1LABEL DISCRIMINANT
X1LABEL SEQUENCE
LINE BLANK ALL
CHARACTER CIRCLE SQUARE CIRCLE
CHARACTER FILL OFF OFF ON
CHARACTER SIZE 1.5 ALL
TITLE FISHER'S 2 POPULATION DISCRIMINANTION
LEGEND 1 CIRC() - NON-CARRIER
LEGEND 2 SQUA() - CARRIER
LEGEND 3 FILL SOLID
LEGEND 3 CIRC() - GROUP MEANS
LEGEND SIZE 1.2
LEGEND FONT DUPLEX
LEGEND 1 COORDINATE 70 87
LEGEND 2 COORDINATE 70 84
LEGEND 3 COORDINATE 70 81
.
PLOT Y XSEQ TAG AND
PLOT MEANY XSEQ2
SLEEP 3
.
.
.  Hypothesis test whether u1 = u2
.
LET C1 = (N1+N2-P-1)
LET C2 = (N1+N2-2)*P
LET C3 = (N1*N2)/(N1+N2)
LET FSTAT = (C1/C2)*C3*D2
LET ALPHA = 0.95
LET FCRIT = FPPF(ALPHA,P,C1)
PRINT " "; PRINT " ";PRINT "H0: U1 = U2"
PRINT "HA: U1 <> U2"
PRINT "VALUE OF F-STATISTIC = ^FSTAT"
PRINT "CRITICAL VALUE = ^FCRIT"
IF FSTAT <= FCRIT
  PRINT "ACCEPT NULL HYPOTHESIS"
END OF IF
IF FSTAT > FCRIT
  PRINT "REJECT NULL HYPOTHESIS"
END OF IF
.
.  Classify new observations
.
LET XPOS = XMEAN1 + XMEAN2
LET M = MATRIX MULTIPLY DISCRM XPOS
LET M = 0.5*M
LET ATEMP = M(1)
LET XNEW1 = DATA -.112 -.059 .064 -.043 -.050 -.094 -.123 -.011 -.210 -.126
LET NEW2 = DATA -.279 -.068 .012 -.052 -.098 -.113 -.143 -.037 -.090 -.019
LET NNEW = SIZE XNEW1
LET YNEW = 0 FOR I = 1 1 NNEW
LOOP FOR K = 1 1 P
    LET FACT = DISCRM^K(1)
    LET YNEW = YNEW + FACT*XNEW^K
END OF LOOP
LET TAG2 = 1 FOR I = 1 1 NEW
LET TAG2 = 2 SUBSET YNEW < ATEMP
LET XSEQ3 = SEQUENCE 1 1 NNEW
PRINT " "; PRINT " "; PRINT "1 = CARRIER, 2= NON-CARRIER"; 
PRINT XSEQ3 TAG2
LET XSEQ4 = DATA 1 NNEW
LET YCUT = DATA ATEMP ATEMP
CHARACTER BLANK CIRCLE CIRCLE
CHARACTER FILL OFF OFF ON
LINE DOTTED
LEGEND 1; LEGEND 2; LEGEND 3
TITLE CLASSIFY NEW POINTS
Y1LABEL VALUE OF DISCRIMINANT FUNCTION
XTIC OFFSET 0.5 0.5
PLOT YCUT XSEQ4 AND
PLOT YNEW XSEQ3 TAG2
.
FEEDBACK ON
