. THIS IS THE DATAPLOT PROGRAM FILE     CHEMMIX.DP
. PURPOSE--DETERMINE PROPER FEED RATE OF 4 INPUT COMPOSITIONS
.          TO YIELD A PRE-SPECIFIED OUTPUT COMPOSITION
.          AT A PRE-SPECIFIED OUTPUT RATE.
. ANALYSIS TECHNIQUE--SOLVING A SYSTEM OF 4 LINEAR EQUATIONS
.                     SOLVE A * X = B
.                     WHERE A IS A MATRIX AND B IS A VECTOR.
. APPLICATION--CHEMICAL MIXING
. SOURCE (PROBLEM)--FOGIEL, THE LINEAR ALGEBRA PROBLEM SOLVER,
.                   RESEARCH AND EDUCATION ASSOCIATION, 1980
.                   PAGE 792
. GIVEN--INPUT PROPORTIONS--
.                               IN1     IN2     IN3     IN4
.           SULFURIC ACID       80%      0%     30%     10%
.           NITRIC ACID          0%     80%     10%     10%
.           WATER               16%     20%     60%     72%
.           INERT                4%      0%      0%      8%
. GIVEN--DESIRED OUTPUT (2000 LB/HR) AT THE FOLLOWING PROPORTIONS--
.           SULFURIC ACID       40% (THEREFORE 800 LB PER HOUR)
.           NITRIC ACID         27% (THEREFORE 540 LB PER HOUR)
.           WATER               31% (THEREFORE 620 LB PER HOUR)
.           INERT                2% (THEREFORE  40 LB PER HOUR)
. TO FIND X1 = FLOW RATE OF INPUT STREAM 1
.         X2 = FLOW RATE OF INPUT STREAM 2
.         X3 = FLOW RATE OF INPUT STREAM 3
.         X4 = FLOW RATE OF INPUT STREAM 4
. NOTE--TO SET UP THE 4 EQUATIONS (ONE FOR EACH COMPOUND)
.       APPLY CONSERVATION OF MASS (MASS BALANCES)
. NOTE--FOR TESTING PURPOSES, THE SOLUTION IS
.       800  600  500  100   LB PER HOUR
. DATE--OCTOBER 1987
.
. -----START POINT-----------------------------------
.
.
ECHO
DIMENSION 100 VARIABLES
.
.      STEP 1--
.      DEFINE THE LEFT-HAND SIDE
.      OF THE SYSTEM OF LINEAR EQUATIONS.
.
READ MATRIX A
.80 .00 .30 .10
.00 .80 .10 .10
.16 .20 .60 .72
.04 .00 .00 .08
END OF DATA
.
.      STEP 2--
.      DEFINE THE RIGHT-HAND SIDE
.      OF THE SYSTEM OF LINEAR EQUATIONS.
.
READ B
800
540
620
40
END OF DATA
PRINT A B
.
.      STEP 3--
.      SOLVE THE LINEAR SYSTEM
.
LET X = MATRIX SOLUTION A B
PRINT X
