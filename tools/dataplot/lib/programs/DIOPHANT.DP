. THIS IS THE DATAPLOT PROGRAM FILE     DIOPHANTINE.DP
. PURPOSE--DETERMINE THE SOLUTIONS OF A DIOPHANTINE EQUATION--
.          FIND THE INTEGER SOLUTIONS (X AND Y) OF X**2 + 1 = 2*Y**4
. ANALYSIS TECHNIQUE--EVALUATION AND SET INTERSECTION WITH INTEGERS
. SOURCE--STEEN, LYNN ARTHUR, MATHEMATICS TODAY,
.         VINTAGE BOOKS, NEW YORK, 1980, PAGE 40.
. APPLICATION--
.
. -----START POINT-----------------------------------
.
ECHO
.
.      STEP 1--
.      DEFINE THE FUNCTION OF INTEREST
.      IN Y = F(X) FORM.
.      DEFINE A SEQUENCE OF X VALUES.
.      COMPUTE CORRESPONDING Y VALUES.
.      PLOT THE VALUES.
.
LET FUNCTION F = ((X**2+1)/2)**0.25
LET X = SEQUENCE 0 1 500
LET Y = F
PLOT Y X
.
.      STEP 2--
.      DETERMINE THE INTERSECTION OF THE Y VALUES
.      WITH THE (INTEGER) X VALUES.
.
LET Y2 = SET INTERSECTION X Y
PRINT Y2
.
.      STEP 3--
.      IN ANOTHER FASHION,
.      DETERMINE THE Y VALUES
.      WHICH ARE INTEGER
.      AND THE (NECESSARILY INTEGER) X VALUES.
.
LET Y3 = FRACT(Y)
PRINT X Y SUBSET Y3 0
