This is file mlefl.men--2-Parameter Fatigue Life MLE Estimates
 
--------------------
 1. Fatigue Life Maximum Likelihood Estimates
 2. FATIGUE LIFE MAXIMUM LIKELIHOOD Y
 3.
 4. Maximum likelihood point estimates for the shape
 5. parameter gamma and the scale parameter can be
 6. obtained for uncensored data.  You can optionally
 7. request to have bootstrap  confidence limits generated
 8. for the distribution parameters.
 9.
10. Response Variable:
10. @CE 1 0 42 50 ? gui-variable
11.
12. Number of bootstrap samples (0 to omit)?
12. @CE 2 0 42 50 *
13.
14. Subset/Except/For Specification:
15. @CE 3 0 1 50 *
16.
17.
18. Compute 2-Parameter Fatigue Life MLE's?
--------------------
GR FE
LET NUMBOOT = 0
@IF 2 0 LET NUMBOOT = _2
let numboot = min(1000,numboot)
if numboot > 0
   bootstrap samples numboot
   set maximum likelihood percentiles default
   device 1 font simplex
   multiplot 2 2
   multiplot corner coordinates 0 0 100 100
   multiplot scale factor 2
   label
   title case asis
   label case asis
   case asis
   title displacement 2
   x2label displacement 13
   x3label displacement 18
   y1label Parameter Estimate
   x1label
   x2label Bootstrap Sample
   title Bootstrap Plot
   line color blue red green
   limits
   bootstrap fatigue life mle plot _1 _3
   line color black all
   .
   delete agamma ascale
   skip 0
   read dpst1f.dat agamma ascale
   y1label
   x2label
   title Gamma Parameter
   let amed = median agamma
   let amean = mean agamma
   let asd = sd agamma
   x2label Median = ^amed, Mean = ^amean
   x3label SD = ^asd
   histogram agamma
   title Scale Parameter
   let amed = median ascale
   let amean = mean ascale
   let asd = sd ascale
   x2label Median = ^amed, Mean = ^amean
   x3label SD = ^asd
   histogram ascale
   x3label displacement
   end of multiplot
   .
   let alpha = 0.05
   let xqlow = alpha/2
   let xqupp = 1 - alpha/2
   .
   let xq = xqlow
   let gam95low = xq quantile agamma
   let xq = xqupp
   let gam95upp = xq quantile agamma
   let xq = xqlow
   let mu95low = xq quantile ascale
   let xq = xqupp
   let mu95upp = xq quantile ascale
   .
   device 1 font tektronix
   window corner coordinates 50 0 100 50
   justification left
   height 4
   move 5 70
   text Confidence Interval for Gamma:
   move 5 63
   text (^gam95low,^gam95upp)
   move 5 50
   text Confidence Interval for Scale:
   move 5 43
   text (^mu95low,^mu95upp)
   window corner coordinates 0 0 100 100
   height
   title
   label
end of if
FATIGUE LIFE MAXIMUM LIKELIHOOD _1 _3
--------------------
WEB HELP MAXIMUM LIKELIHOOD
--------------------
