----- <~fit\HAHN1.DP> Run HAHN1.DAT Example

. This is file    HAHN1.DP

. EXAMPLE -- TOM HAHN COPPER THERMAL EXPANSION STUDY
. MODEL   -- QUADRATIC/QUADRATIC AND CUBIC/CUBIC TECHNIQUE FOR
.            UPDATING AND IMPROVING RATIONAL FIT MODEL.  A SECOND FIT
.            IS RESTRAINED TO GO THROUGH THE ORGIN.
DELETE X Y
SKIP 25
READ HAHN1.DAT X Y
MULTIPLOT CORNER COORDINATES 2 2 98 98
MULTIPLOT 2 2
DEVICE 1 FONT SIMPLEX
CHARACTER X
CHARACTER SIZE 1.0
LINE BLANK
TITLE RAW DATA
Y1LABEL COEFFICIENT OF THERMAL EXPANSION (COPPER)
X1LABEL TEMPERATURE (DEGREES KELVIN)
PLOT Y X
.
LET X2 = DATA 10 50 120 200 800
LET Y2 = DATA 0 5 12 15 20
EXACT 2/2 FIT Y2 X2 Y X
.
TITLE EXACT QUADRATIC/QUADRATIC FIT
PLOT Y PRED VS X
.
LET X2 = DATA 40 30 FOR I = 6 1 7
LET Y2 = DATA 3 2 FOR I = 6 1 7
EXACT 3/3 RATIONAL FIT Y2 X2 Y X
.
TITLE EXACT CUBIC/CUBIC RATIONAL FIT
PLOT Y Y PRED VS X
END OF MULTIPLOT
SLEEP 5
MULTIPLOT 2 2 
FIT Y = (A0+A1*X+A2*X**2+A3*X**3)/(1+B1*X+B2*X**2+B3*X**3)
TITLE (A0+A1*X+A2*X**2+A3*X**3)/(1+B1*X+B2*X**2+B3*X**3)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
PLOT Y PRED VS X
TITLE
Y1LABEL RESIDUALS
PLOT RES X
X1LABEL
NORMAL PROBABILITY PLOT RES
END OF MULTIPLOT
SLEEP 5
MULTIPLOT 2 2 
FIT Y = (A0+A1*X+A2*X**2+A3*X**3)/(1+B1*X+B2*X**2+B3*X**3)
TITLE (A0+A1*X+A2*X**2+A3*X**3)/(1+B1*X+B2*X**2+B3*X**3)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
Y1LABEL COEFFICIENT OF THERMAL EXPANSION (COPPER)
X1LABEL TEMPERATURE (DEGREES KELVIN)
PLOT Y PRED VS X
TITLE
Y1LABEL RESIDUALS
PLOT RES X
X1LABEL
NORMAL PROBABILITY PLOT RES
END OF MULTIPLOT
.
DEVICE 1 FONT TEKTRONIX
