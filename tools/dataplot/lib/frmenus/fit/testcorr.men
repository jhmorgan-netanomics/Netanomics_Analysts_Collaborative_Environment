This is file testcorr.men--Hypothesis Test for Correlation
 
--------------------
 1. Hypothesis Test for Correlation Coefficient
 2. Macro (Reference: Snedecor & Cochran, page 185)
 3.
 4.
 5. Variables:
 6.    Response    Y:
 6. @CE 1 0 42 50 ? gui-variable
 7.    Independent X:
 7. @CE 2 0 42 50 ? gui-variable
 8.
 9. Confidence level (select one):
10. @CE 3 1 3 23 95% limits
11. @CE 3 2 3 23 99% limits
12.
13. Value to test against (defaults to 0):
13. @CE 4 0 42 50 ?
14.
15. Subset/Except/For Specification:
16. @CE 5 0 1 50 *
17.
18.
19. Compute the Hypothesis Test?
--------------------
GR FE
LET ZCORR = CORRELATION _1 _2 _5
 
LET ZN = NUMBER _1 _5
 
LET ZP = .95
@IF 3 1 LET ZP = .95
@IF 3 2 LET ZP = .99
LET ZP2 = ZP+(1-ZP)/2
LET ZPPF = NORPPF(ZP2)
LET ZPPF2 = -ZPPF

LET ZPERC = 100*ZP
LET ZPERC2 = 100 - ZPERC
 
LET ZHA = 0
LET ZHA = _4
LET ZTEMP1 = LN((1+ZCORR)/(1-ZCORR))
LET ZTEMP2 = LN((1+ZHA)/(1-ZHA))
LET ZSTAT = SQRT(ZN-3)*(0.5*(ZTEMP1 - ZTEMP2))
LET STRING ZZZZSTR = ACCEPTED
IF ZSTAT > ZPPF
  LET STRING ZZZZSTR = REJECTED
END OF IF
IF ZSTAT < ZPPF2
  LET STRING ZZZZSTR = REJECTED
END OF IF

CAPTURE ZZZZJUNK.DAT
WRITE "H0: r = ^ZHA "
WRITE "H1: r <> ^ZHA "
WRITE " "
WRITE "Number of Observations                 = ^ZN"
WRITE "Estimated Correlation                  = ^ZCORR"
WRITE "Significance Level                     = ^ZPERC2 %"
WRITE " "
WRITE "Fisher z-transformation test statistic = ^ZSTAT "
WRITE "Lower critical value                   = ^ZPPF2 "
WRITE "Upper critical value                   = ^ZPPF "
WRITE " "
WRITE "Hypotheis is ^ZZZZSTR "
END OF CAPTURE
 
LIST ZZZZJUNK.DAT
--------------------
WEB HELP CORRELATION
--------------------
