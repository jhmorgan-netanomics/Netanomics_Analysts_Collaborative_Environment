----- <~clas\eda\ELECT92.DP> Load & Execute ELECT92.DAT

. This is file    ELECT92.DP

DELETE Y X1 TO X4
SKIP 25
COLUMN LIMITS 11 132
READ ELECT92.DAT STATE BUSH CLINTON PEROT ECV
TITLE ELECT92.DAT

DEVICE 1 FONT SIMPLEX
LABEL CASE ASIS
LEGEND CASE ASIS
CHARACTER CASE ASIS
.
FEEDBACK OFF
LET K = 0
LOOP FOR W = 0 .005 1
    LET K = K+1
    LET PERCBUSH(K) = 100*W
    LET DEL = (BUSH+W*PEROT)-(CLINTON+(1-W)*PEROT)
    LET ECVBUSH(K) = SUM ECV SUBSET DEL > 0
END OF LOOP
.
TITLE 1992 ELECTION:  PEROT VOTE ANALYSIS
Y1LABEL ELECTORAL COLLEGE VOTES FOR BUSH
XLABEL PERCENTAGE OF THE PEROT VOTE TO BUSH
.
PLOT ECVBUSH PERCBUSH AND
PLOT 270 FOR X = 0 1 100
SLEEP 3
.
GRID ON
PLOT ECVBUSH PERCBUSH AND
PLOT 270 FOR X = 0 1 100
SLEEP 3
.
. --------------------------------------------------------------
. STEP 1--DEFINE WHICH ONE OF THE 4 BLOCK PLOT CASES IS TO BE DONE--
.         CASE 1--ORDERED ALPHABETICALLY
.         CASE 2--ORDERED BY ELECTORAL COLLEGE VOTES
.         CASE 3--ORDERED BY CLINTON PERCENTAGE
.         CASE 4--ORDERED BY DIFFERENCE OF CLINTON & BUSH PERCENTAGES
. --------------------------------------------------------------
.
LET CASE = 4
.
. ------------------------------------
. STEP 2--READ IN STATE ID'S AND VOTES
. ------------------------------------
.
SKIP 17; READ STRING ELECT92.DAT S1 TO S20
SKIP 18; READ STRING ELECT92.DAT S21 TO S40
SKIP 19; READ STRING ELECT92.DAT S41 TO S51
.
SKIP 25
COLUMN LIMITS 11 132
READ ELECT92.DAT ST Y1 Y2 Y3 EC
COLUMN LIMITS 1 132
LET SEQ = 1 1 51
.
. ---------------------------------------------------------
. STEP 3--COMPUTE PERCENTAGES AND THEN "COLUMNIZE" THE DATA
. ---------------------------------------------------------
.
LET YALL = Y1+Y2+Y3
LET PERC1 = 100*(Y1/YALL)
LET PERC2 = 100*(Y2/YALL)
LET PERC3 = 100*(Y3/YALL)
.
IF CASE = 1
   X2LABEL SORTED ALPHABETICALLY
END IF
IF CASE = 2
   X2LABEL SORTED BY ELECTORAL VOTES
   LET EC = SORTC EC PERC1 PERC2 PERC3 ST
END IF
IF CASE = 3
   X2LABEL SORTED BY CLINTON PERCENTAGE
   LET PERC2 = SORTC PERC2 PERC1 PERC3 EC ST
END IF
. THIS IS DATAPLOT MACRO    ELECT92B.DP
. (SUB-MACRO FOR ELECT92.DP)
.
. ----------------------------------------
. STEP 1B--GENERATE THE ORDERED BLOCK PLOT
. ----------------------------------------
.
TITLE 1992 PRESIDENTIAL ELECTION
Y1LABEL PERCENTAGE OF VOTES
XLABEL STATE
X3LABEL AUTOMATIC
LEGEND 1 B = BUSH    C = CLINTON    P = PEROT
LEGEND 1 COOR 16 87
CHAR B C P
LINES BL BL BL
CHAR HW 1.6 1 ALL
BAR EXPANSION 1 1.5
XLIMITS 0 50
BLOCK PLOT PERC STATE CANDIDATE
.
. -----------------------------------------------------------------
. STEP 2B--ANNOTATE PLOT WITH STATE AND ELECTORAL COLLEGE VOTE INFO
. -----------------------------------------------------------------
.
FEEDBACK OFF
JUSTIFICATION CENTER
HW 1 0.5
LOOP FOR K = 1 1 51
LET Y = MOD(K,2) - 2
MOVEDATA K Y
LET L = ST(K)
TEXT ^S^L
MOVEDATA K -5
LET A = EC(K)
TEXT ^A
END LOOP
FEEDBACK ON
.
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
