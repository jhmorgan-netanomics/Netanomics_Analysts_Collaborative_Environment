----- <~clas\eda\BOXCAKE2.DP> Load & Execute BOXCAKE2.DAT

. This is file    BOXCAKE2.DP

. MEAN CAKE TASTE
. BOX & JONES
. TAGUCHI PARAMETER DESIGN EXAMPLE
. 2**3 ANALYSIS OF MEANS
. INPUT DATA FILE--BOXCAKE2.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
DELETE Y X1 TO X5
.
. STEP 1--READ IN THE DATA
.

SKIP 50
READ BOXCAKE2.DAT YM YSD X1 X2 X3
LET Y = YM
TITLE BOXCAKE2.DAT
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
.
MULTIPLOT 2 2
LABEL SIZE 4
Y1LABEL MEAN TASTE
X3LABEL AUTOMATIC
X1LABEL SETTINGS
PLOT Y
CHAR X; SPIKE ON; PLOT Y
SPIKE OFF; X1LABEL
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3
LINES SOLID ALL; LINES BLANK BLANK
Y1LABEL
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
DELETE YM YSD X1 X2 X3 Y FOR I = 1 1 1
.
MULTIPLOT OFF
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 3
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 3 7
LET STRING S0 = MEAN TASTE
LET STRING S1 = FLOUR % (X1)
LET STRING S2 = SHORTENING % (X2)
LET STRING S3 = EGGS % (X3)
CALL DEXSTAT2.DP
YLIMITS
SLEEP 3
ERASE
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 2 2
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 2.5 ALL; LABEL SIZE 3; TIC LABEL SIZE 3
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3    X1
BLOCK PLOT Y X1 X3    X2
BLOCK PLOT Y X1 X2    X3
MULTIPLOT OFF
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
SLEEP 3
ERASE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B23 = COEF(1)
LET B3 = COEF(2)
LET RESSD = RESSD2(2)
LET PRED = MU + 0.5*(B23*X2*X3 + B3*X3)
LET RES = Y-PRED
SET WRITE DECIMALS 2
WRITE X1 X2 X3 Y PRED RES
WRITE OUT2. X1 X2 X3 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED = ^MU. + 0.5*(^B23*X2*X3 + ^B3*X3)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
X3LABEL
LABEL SIZE 3
PLOT RES
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
SLEEP 3
ERASE
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 1 2 3 4 5 6 7
LET U1 = X3
LET U2 = X2
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL EGGS (X3)
Y1LABEL SHORTENING (X2)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
LET YSORT = SORTC Y X1 X2 X3
SET WRITE DECIMALS 2
WRITE X1 X2 X3 YSORT
WRITE OUT3. X1 X2 X3 YSORT
.
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
CAPTURE ZZZZJUNK.DAT
WRITE "THE FILES: OUT1., OUT2., AND OUT3. CONTAIN THE"
WRITE "QUANTITIATIVE OUTPUT FROM THE YATES ANALYSIS."
WRITE "ENTER THE COMMANDS:"
WRITE "     LIST OUT1."
WRITE "     LIST OUT2."
WRITE "     LIST OUT3."
WRITE "TO VIEW THEM"
END OF CAPTURE
LIST ZZZZJUNK.DAT
