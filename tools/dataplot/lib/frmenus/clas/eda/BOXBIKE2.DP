----- <~clas\eda\BOXBIKE2.DP> Load & Execute BOXBIKE2.DAT

. This is file    BOXBIKE2.DP

. FACTORS AFFECTING BIKE SPEED
. BOX, HUNTER, & HUNTER, P. 390-397
. COMPLETE ANALYSIS OF 2**(7-4) DESIGN
. INPUT DATA FILE--BOXBIKE2.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
. STEP 1--READ THE DATA
.
DELETE Y X1 TO X7
SKIP 25
READ BOXBIKE2.DAT Y X1 TO X7
TITLE BOXBIKE2.DAT

.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5 X6 X7
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 7
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 50 90
LET STRING S0 = TIME (SEC)
LET STRING S1 = SEAT (X1)
LET STRING S2 = DYNAMO (X2)
LET STRING S3 = HANDLEBARS (X3)
LET STRING S4 = GEAR (X4)
LET STRING S5 = RAINCOAT (X5)
LET STRING S6 = BREAKFAST (X6)
LET STRING S7 = TIRES (X7)
. CALL DEXSTAT2.DP
YLIMITS
SLEEP 3
ERASE
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 5 5
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 7 ALL; LABEL SIZE 7; TIC LABEL SIZE 7
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3 X1
BLOCK PLOT Y X4 X5 X1
BLOCK PLOT Y X6 X7 X1
BLOCK PLOT Y X1 X3 X2
BLOCK PLOT Y X4 X5 X2
BLOCK PLOT Y X6 X7 X2
BLOCK PLOT Y X1 X2 X3
BLOCK PLOT Y X4 X5 X3
BLOCK PLOT Y X6 X7 X3
BLOCK PLOT Y X1 X2 X4
BLOCK PLOT Y X3 X5 X4
BLOCK PLOT Y X6 X7 X4
BLOCK PLOT Y X1 X2 X5
BLOCK PLOT Y X3 X4 X5
BLOCK PLOT Y X6 X7 X5
BLOCK PLOT Y X2 X4 X5
BLOCK PLOT Y X1 X2 X6
BLOCK PLOT Y X3 X4 X6
BLOCK PLOT Y X5 X7 X6
BLOCK PLOT Y X1 X2 X7
BLOCK PLOT Y X3 X4 X7
BLOCK PLOT Y X5 X6 X7
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
SLEEP 3
ERASE
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B4 = COEF(1)
LET B2 = COEF(2)
LET RESSD = RESSD2(2)
LET PRED = MU+0.5*(B4*X4+B2*X2)
LET RES = Y-PRED
ER
. SET WRITE DECIMALS 2
SET WRITE FORMAT 7F8.0,3F9.2
WRITE X1 X2 X3 X4 X5 X6 X7 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 X6 X7 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU + 0.5*(^B4*X4 + ^B2*X2 )
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
LABEL SIZE 3
. PLOT RES RUNSEQ
PLOT
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
SLEEP 3
ERASE
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 50 60 70 80 90
LET U1 = X4
LET U2 = X2
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL GEAR (X4) OR SEAT*DYNAMO (X1*X2)
Y1LABEL DYNAMO (X2)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X.. FACTORS
.
SLEEP 3
ERASE
LET YSORT = SORTC Y X1 X2 X3 X4 X5 X6 X7
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 X5 X6 X7 YSORT
WRITE OUT3. X1 X2 X3 X4 X5 X6 X7 YSORT
.
. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
. STEP 11--READ IN THE  FULL (ORIGINAL + FOLDOVER) DATA (N = 16)
.
SKIP 25
READ BOXBIKE3.DAT Y X1 X2 X3 X4 X5 X6 X7
.
. STEP 12--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5 X6 X7
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5 X6 X7
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 13--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
MULTIPLOT OFF
X3LABEL
YLIMITS 50 90
CALL DEXSTAT2.DP
YLIMITS
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
LIST OUT1.
LIST OUT2.
LIST OUT3.
.
