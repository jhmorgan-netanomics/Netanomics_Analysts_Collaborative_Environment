----- <~clas\eda\BOXCAKE.DP> Load & Execute BOXCAKE.DAT

. This is file    BOXCAKE.DP

. CAKE TASTE
. BOX & JONES
. TAGUCHI PARAMETER DESIGN EXAMPLE
. 2**5 ANALYSIS
. INPUT DATA FILE--BOXCAKE.DAT
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
.
. STEP 1--READ IN THE DATA
.
DELETE Y X1 TO X5
SKIP 25
READ BOXCAKE.DAT Y X1 TO X5
TITLE BOXCAKE.DAT

DELETE Y X1 X2 X3 X4 X5 FOR I = 1 1 5
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4 X5
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4 X5
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
MULTIPLOT OFF
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 5
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 3 7
LET STRING S0 = TASTE RATING
LET STRING S1 = FLOUR % (X1)
LET STRING S2 = SHORTENING % (X2)
LET STRING S3 = EGGS % (X3)
LET STRING S4 = TEMPERATURE (X4)
LET STRING S5 = TIME (X5)
CALL DEXSTAT2.DP
YLIMITS
SLEEP 3
ERASE
.
. STEP 4--GENERATE BLOCK PLOTS
.
MULTIPLOT 2 3
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 2.5 ALL; LABEL SIZE 3; TIC LABEL SIZE 3
X3LABEL AUTOMATIC
BAR EXPANSION FACTOR 1 1.5
BLOCK PLOT Y X2 X3 X4 X5    X1
BLOCK PLOT Y X1 X3 X4 X5    X2
BLOCK PLOT Y X1 X2 X4 X5    X3
BLOCK PLOT Y X1 X2 X3 X5    X4
BLOCK PLOT Y X1 X2 X3 X4    X5
BAR EXPANSION FACTOR 1 1
 
MULTIPLOT 2 2
LET FACTORY = PATTERN 1 2 3 4 5 6 7 8 FOR I = 1 1 32
LET CUSTOMER = SEQUENCE 1 8 1 4
CHAR 1 2 3 4 5 6 7 8; LINES BL BL BL BL BL BL BL BL
BLOCK PLOT Y X4 X5    FACTORY
CHAR BLANK ALL; CHAR 1 2 3 4; LINES SOLID ALL; LINES BL BL BL BL
BLOCK PLOT Y X1 X2 X3     CUSTOMER
MULTIPLOT OFF
SLEEP 3
ERASE
.
MULTIPLOT 2 2
CHAR BLANK ALL; CHAR 1 2; LINES SOLID ALL; LINES BL BL
CHAR SIZE 4 ALL
BAR EXPANSION FACTOR 2 1.5
MEAN BLOCK PLOT Y X4 X5    X1
MEAN BLOCK PLOT Y X4 X5    X2
MEAN BLOCK PLOT Y X4 X5    X3
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
SLEEP 3
ERASE
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B45 = COEF(1)
LET B145 = COEF(2)
LET B23 = COEF(3)
LET RESSD = RESSD2(3)
LET PRED = MU + 0.5*(B45*X4*X5 + B145*X1*X4*X5 + B23*X2*X3)
LET RES = Y-PRED
SET WRITE DECIMALS 2
WRITE X1 X2 X3 X4 X5 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 X5 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PRED = ^MU+0.5*(^B45*X4*X5+^B145*X1*X4*X5+^B23*X2*X3)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
X3LABEL
LABEL SIZE 3
PLOT RES
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
SLEEP 3
ERASE
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 1 2 3 4 5 6 7
LET U1 = X4
LET U2 = X5
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL TEMPERATURE (X4)
Y1LABEL TIME (X5)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
LET YSORT = SORTC Y X1 X2 X3 X4 X5
SET WRITE DECIMALS 2
WRITE X1 X2 X3 X4 X5 YSORT
WRITE OUT3. X1 X2 X3 X4 X5 YSORT
.
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
CAPTURE ZZZZJUNK.DAT
WRITE "THE FILES: OUT1., OUT2., AND OUT3. CONTAIN THE"
WRITE "QUANTITIATIVE OUTPUT FROM THE YATES ANALYSIS."
WRITE "ENTER THE COMMANDS:"
WRITE "     LIST OUT1."
WRITE "     LIST OUT2."
WRITE "     LIST OUT3."
WRITE "TO VIEW THEM"
END OF CAPTURE
LIST ZZZZJUNK.DAT
