----- <~clas\anov\QUINLAN.DP> Load & Execute QUINLAN.DAT

. This is file    QUINLAN.DP

. NOISE/SHRINKAGE OF SPEEDOMETER CASING
. JIM QUINLAN
. TAGUCHI ANALYSIS EXAMPLE
. 2**(15-11) ANALYSIS
. INPUT DATA FILE--QUINLAN.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2A. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT2B.
.               OUT3A. (SORTED Y AND CARRY-ALONG X'S)
.               OUTBB.
.
. STEP 1--READ IN THE DATA
.
SKIP 50
DELETE Y X1 TO X15
READ QUINLAN.DAT X1 TO X15 Y1 TO Y4
TITLE QUINLAN.DAT
.
LOOP FOR K = 1 1 15
LET X^K = -1 SUBSET X^K 1
LET X^K = +1 SUBSET X^K 2
END LOOP
LET Y = (Y1+Y2+Y3+Y4)/4
DELETE Y1 Y2 Y3 Y4
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 TO X15
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 TO X15
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
MULTIPLOT OFF
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 15
LET DP = 1
LET STRING STAT = MEAN
YLIMITS 0 .6
LET STRING S0 = SHRINKAGE
LET STRING S1 = X1
LET STRING S2 = X2
LET STRING S3 = X3
LET STRING S4 = X4
LET STRING S5 = X5
LET STRING S6 = X6
LET STRING S7 = X7
LET STRING S8 = X8
LET STRING S9 = X9
LET STRING S10 = X10
LET STRING S11 = X11
LET STRING S12 = X12
LET STRING S13 = X13
LET STRING S14 = X14
LET STRING S15 = X15
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
. NONE DONE HERE
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
SLEEP 3
ERASE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B5 = COEF(1)
LET B5 = -B5
LET B7 = COEF(2)
LET RESSD = RESSD2(2)
LET PRED = MU + 0.5*(B5*X5 + B7*X7)
LET RES = Y-PRED
ER
SET WRITE DECIMALS 3
WRITE X1 X2 X3 X4 X5 X6 X7 X8 X9 X1
WRITE X11 X12 X13 X14 X15 Y PRED RES
WRITE OUT2A. X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
WRITE OUT2B. X11 X12 X13 X14 X15 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PRED = ^MU + 0.5*(^B5*X5 + ^B7*X7)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
X3LABEL
LABEL SIZE 3
PLOT RES
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
SLEEP 3
ERASE
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
LET YCONT = DATA 0 .1 .2 .3 .4 .5 .6
LET U1 = X5
LET U2 = X7
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL WIRE BRAID TYPE (X5)
Y1LABEL WIRE DIAMETER (X7)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X... FACTORS
.
LET Y2 = SORTC Y X1 X2 X3 X4 X5 X6 X7 X8 X9 X10 X11 X12 X13 X14 X15
SET WRITE DECIMALS 3
WRITE X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
WRITE X11 X12 X13 X14 X15 Y2
WRITE OUT3A. X1 X2 X3 X4 X5 X6 X7 X8 X9 X10
WRITE OUT3B. X11 X12 X13 X14 X15 Y2
.
DEVICE 1 FONT SIMPLEX
RESET PLOT CONTROL
CAPTURE ZZZZJUNK.DAT
WRITE "THE FILES: OUT1., OUT2., OUT3A., AND OUT3B. . CONTAIN THE"
WRITE "QUANTITIATIVE OUTPUT FROM THE YATES ANALYSIS."
WRITE "ENTER THE COMMANDS:"
WRITE "     LIST OUT1."
WRITE "     LIST OUT2."
WRITE "     LIST OUT3A."
WRITE "     LIST OUT3B."
WRITE "TO VIEW THEM"
END OF CAPTURE
LIST ZZZZJUNK.DAT
