----- <~clas\eda\BOXCLEAN.DP> Load & Execute BOXCLEAN.DAT

. This is file    BOXCLEAN.DP

. STABILITY OF HOUSEHOLD CLEANSER
. BOX, HUNTER, & HUNTER, P. 422-424
. COMPLETE ANALYSIS OF 2**(4-1) DESIGN
. INPUT DATA FILE--BOXCLEAN.DAT
. OUTPUT FILES--DPPL1F.DAT (A POSTSCRIPT FILE CONTAINING ALL PLOT OUTPUT)
.               OUT1. (YATES ANALYSIS OUTPUT)
.               OUT2. (PREDICTED VALUES AND RESIDUALS FROM FINAL MODEL)
.               OUT3. (SORTED Y AND CARRY-ALONG X'S)
.
.
. STEP 1--READ IN THE DATA
.
DELETE Y X1 X2 X3 X4
SKIP 25
READ BOXCLEAN.DAT Y X1 TO X4
.
. STEP 2--GENERATE DEX SCATTER PLOT & DEX MEAN PLOT
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
DEVICE 1 FONT SIMPLEX
MULTIPLOT 2 1
LABEL SIZE 4
X3LABEL AUTOMATIC
CHAR X; LINES BLANK
DEX SCATTER PLOT Y X1 X2 X3 X4
CHAR BLANK; LINES SOLID
DEX MEAN PLOT Y X1 X2 X3 X4
LINES SOLID ALL; LINES BLANK BLANK
.
. STEP 3--GENERATE MATRIX OF MEAN PLOTS (USE DEXSTAT2.DP MACRO)
.
MULTIPLOT OFF
SLEEP 3
ERASE
X3LABEL
LET NUMFAC = 4
LET DP = 2
LET STRING STAT = MEAN
YLIMITS 10 20
LET STRING S0 = STABILITY
LET STRING S1 = ACID CONC (X1)
LET STRING S2 = CAT CONC (X2)
LET STRING S3 = TEMP (X3)
LET STRING S4 = MONOMER CONC (X4)
CALL DEXSTAT2.DP
YLIMITS
.
. STEP 4--GENERATE BLOCK PLOTS
.
SLEEP 3
ERASE
MULTIPLOT 2 2
CHAR 1 2; LINES BLANK BLANK
CHAR SIZE 3 ALL; LABEL SIZE 3; TIC LABEL SIZE 3
X3LABEL AUTOMATIC
BLOCK PLOT Y X2 X3 X1
BLOCK PLOT Y X1 X4 X2
BLOCK PLOT Y X1 X4 X3
BLOCK PLOT Y X2 X3 X4
MULTIPLOT OFF
CHAR SIZE 2 ALL; LABEL SIZE 2; TIC LABEL SIZE 2
.
. STEP 5--DO YATES ANALYSIS (DATA MUST BE IN YATES ORDER)
.         NOTE--COEFFICIENTS STORED OUT TO FILE  DPST1F.DAT
.
SLEEP 3
ERASE
YATES Y
CAPTURE OUT1.
YATES Y
END CAPTURE
.
. STEP 6--GENERATE NORMAL AND HALFNORMAL PROBABILITY PLOTS ON THE EFFECTS
.         NOTE--MUST READ COEFFICIENTS IN FROM FILE  DPST1F.DAT
.
SKIP 0
READ DPST1F.DAT TAG COEF T RESSD1 RESSD2
WRITE TAG COEF T RESSD1 RESSD2
CHAR X; LINES BLANK
LABEL SIZE 4
X3LABEL AUTOMATIC
MULTIPLOT 2 2
NORMAL PROBABILITY PLOT COEF
LET ABSCOEF = ABS(COEF)
HALFNORMAL PROBABILITY PLOT ABSCOEF
.
. STEP 7--GENERATE PREDICTED VALUES AND RESIDUALS FOR PARTIAL MODEL
.
LET MU = MEAN Y
LET B1 = COEF(1)
LET B2 = COEF(2)
LET RESSD = RESSD2(2)
LET PRED = MU+0.5*(B1*X1+B2*X2)
LET RES = Y-PRED
SET WRITE DECIMALS 2
WRITE X1 X2 X3 X4 Y PRED RES
WRITE OUT2. X1 X2 X3 X4 Y PRED RES
.
. STEP 8--GENERATE NORMAL PROBABILITY PLOT OF RESIDUALS
.
X1LABEL PREDICTED Y = ^MU + 0.5 * (^B1 * X1 + ^B2 * X2)
X2LABEL RESIDUAL STANDARD DEVIATION = ^RESSD
X3LABEL
LABEL SIZE 3
PLOT
NORMAL PROBABILITY PLOT RES
X1LABEL
X2LABEL
LABEL SIZE 2
MULTIPLOT OFF
.
. STEP 9--GENERATE CONTOUR PLOT FOR THE 2 MOST IMPORTANT FACTORS
.          (USE MACRO DEXCONT.DP)
.
SLEEP 3
ERASE
LET YCONT = DATA 5 10 15 20 25
LET U1 = X1
LET U2 = X2
CHAR BLANK ALL; LINES SOLID ALL
X1LABEL ACID CONCENTRATION (X1)
Y1LABEL CATALYST CONCENTRATION (X2)
X3LABEL
CALL DEXCONT.DP
X1LABEL
Y1LABEL
.
. STEP 10--SORT Y AND CARRY ALONG X.. FACTORS
.
LET YSORT = SORTC Y X1 X2 X3 X4
SET WRITE DECIMALS 0
WRITE X1 X2 X3 X4 YSORT
WRITE OUT3. X1 X2 X3 X4 YSORT
.
CAPTURE ZZZJUNK.DAT
WRITE "THE FILES: OUT1., OUT2., AND OUT3. CONTAIN THE"
WRITE "QUANTITIATIVE OUTPUT FROM THE YATES ANALYSIS."
WRITE "ENTER THE COMMANDS:"
WRITE "     LIST OUT1."
WRITE "     LIST OUT2."
WRITE "     LIST OUT3."
WRITE "TO VIEW THEM"
END OF CAPTURE
LIST ZZZZJUNK.DAT
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
