This is file bootsub.men--Bootstrap Confidence Intervals for Error (Subbotin)
 
--------------------
 1. Error (Subbotin) Bootstrap Confidence Intervals
 2. BOOTSTRAP ERROR PLOT Y
 3.
 4. The Mcleish PPCC/probability plot generates
 5. point estimates for the location, scale, and shape
 6. parameters.  This menu uses the bootstrap to generate
 7. confidence intervals for these parameters.  Both
 8. uncensored and censored data are supported.  Grouped
 9. data is not.  Censoring is only supported for the PPCC
10. plot.
11.
12. If a censoring variable is specified, it should contain
13. a value of 1 to signify a failure time and a value of 0
14. to signify a censoring time.
15.
16. Response Variable:
16. @CE 1 0 42 50 ? gui-variable
17. Optional Censoring Variable:
17. @CE 2 0 42 50 ? gui-variable
18.
19. Number of bootstrap samples:
19. @CE 3 0 42 50 *
20. Select goodness of fit critierion:
21. @CE 4 1 1 30 PPCC
22. @CE 4 2 1 30 Kolmogorov-Smirnov
23. Minimum Value for alpha (omit for default):
23. @CE 5 0 42 50 *
24. Maximum Value for alpha (omit for default):
24. @CE 6 0 42 50 *
25.
26. Significance Level (default=0.05):
27. @CE 7 0 42 50 ? *
28.
29. Subset/Except/For Specification:
30. @CE 8 0 1 50 *
31.
32.
33. Compute Bootstrap Confidence Intervals?
--------------------
GR FE
LET NUMBOOT = 100
@IF 3 0 LET NUMBOOT = _3
let numboot = min(1000,numboot)
let numboot = max(100,numboot)
let icensor = 1
@IF 2 0 let icensor = 3
@IF 4 2 let icensor = icensor + 1
let alpha = 0.05
let alpha = _7
if alpha <= 0
   let alpha = 0.05
end of if
if alpha >= 1
   let alpha = 0.05
end of if
if alpha > 0.5
   let alpha = 1 - alpha
end of if
let xqlow = alpha/2
let xqupp = 1 - alpha/2
delete alpha1 alpha2
let alpha1 = _5
let alpha2 = _6
.
bootstrap samples numboot
set maximum likelihood percentiles default
device 1 font simplex
multiplot 2 3
multiplot corner coordinates 0 0 100 100
multiplot scale factor 2
label
title case asis
label case asis
case asis
title displacement 2
x2label displacement 13
x3label displacement 18
y1label Parameter Estimate
x1label
x2label Bootstrap Sample
title Bootstrap Plot
line color blue red green black
limits
if icensor = 1
   bootstrap error plot _1 _8
end of if
if icensor = 2
   bootstrap error ks plot _1 _8
end of if
if icensor = 3
   bootstrap error censored plot _1 _2 _8
end of if
if icensor = 4
   bootstrap error censored plot _1 _2 _8
   . bootstrap error ks censored plot _1 _2 _8
end of if
line color black all
.
delete aloc ascale aalpha
skip 0
read dpst1f.dat aloc ascale aalpha
y1label
x2label
title Location Parameter
let amed = median aloc
let amean = mean aloc
let asd = sd aloc
x2label Median = ^amed, Mean = ^amean
x3label SD = ^asd
histogram aloc
title Scale Parameter
let amed = median ascale
let amean = mean ascale
let asd = sd ascale
x2label Median = ^amed, Mean = ^amean
x3label SD = ^asd
histogram ascale
title Alpha Parameter
let amed = median aalpha
let amean = mean aalpha
let asd = sd aalpha
x2label Median = ^amed, Mean = ^amean
x3label SD = ^asd
histogram aalpha
x3label displacement
end of multiplot
.
let xq = xqlow
let loc95low = xq quantile aloc
let xq = xqupp
let loc95upp = xq quantile aloc
let xq = xqlow
let sca95low = xq quantile ascale
let xq = xqupp
let sca95upp = xq quantile ascale
let xq = xqlow
let alp95low = xq quantile aalpha
let xq = xqupp
let alp95upp = xq quantile aalpha
.
device 1 font tektronix
window corner coordinates 50 0 100 50
justification left
height 4
move 5 70
text Confidence Interval for Location:
move 5 63
text (^loc95low,^loc95upp)
move 5 50
text Confidence Interval for Scale:
move 5 43
text (^sca95low,^sca95upp)
move 5 30
text Confidence Interval for Alpha:
move 5 23
text (^alp95low,^alp95upp)
window corner coordinates 0 0 100 100
height
title
label
--------------------
WEB HELP DISTRIBUTIONAL BOOTSTRAP
--------------------
