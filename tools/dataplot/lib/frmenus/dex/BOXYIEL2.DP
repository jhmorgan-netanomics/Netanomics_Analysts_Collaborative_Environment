----- <~dex\BOXYIEL2.DP> Load & Execute BOXYIEL2.DAT

. This is file    BOXYIEL2.DP

. PURPOSE--CARRY OUT GRAPHICAL ANALYSIS
.          OF "OPTIMIZING" DESIGNS
.          (RESPONSE SURFACE DESIGNS)
. THIS PROGRAM WILL--
.   1) READ IN DATA
.   2) GENERATE MAIN EFFECTS SCATTER AND MEAN PLOTS
.   3) GENERATE POST-FIT CONTOUR PLOT
.
. -----------------------------------------------------
. STEP 1--READ IN THE DATA
DELETE Y X1 TO X2
SKIP 25
READ BOXYIEL2.DAT Y X1 X2
.
. ----------------------------------------------------
. STEP 2--GENERATE THE MAIN EFFECTS SCATTER PLOT
.         AND MAIN EFFECTS MEAN PLOT
.
X3LABEL AUTOMATIC
MULTIPLOT CORNER COORDINATES 5  5 95 95
MULTIPLOT 2 2
DEVICE 1 FONT SIMPLEX
LINES BLANK ALL
CHAR X BLANK
DEX SCATTER PLOT Y X1 TO X2
LINES SOLID ALL
CHAR BLANK ALL
DEX MEAN PLOT Y X1 TO X2
END OF MULTIPLOT
SLEEP 3
ERASE
X3LABEL
.
. ------------------------------------------------
. STEP 3--GENERATE A CONTOUR PLOT
.
LET U1 = X1
LET U2 = X2
LET Y2 = SEQUENCE 70 5 100
XLABEL X1
Y1LABEL X2
CALL DEXCONTQ.DP
.
DEVICE 1 FONT TEKTRONIX
RESET PLOT CONTROL
