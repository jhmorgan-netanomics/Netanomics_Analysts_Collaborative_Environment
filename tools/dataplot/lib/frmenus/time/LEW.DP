----- <~time\LEW.DP> Load LEW.DATA & generate 4-PLOT

. This is file    LEW.DP

DELETE Y X
TITLE
.
SKIP 25
READ LEW.DAT Y
.
MULTIPLOT CORNER COORDINATES 5 5 95 95
MULTIPLOT 2 2
X3LABEL AUTOMATIC
PLOT Y
CHARACTER X
LINE BLANK
LAG PLOT Y
CHARACTER BLANK ALL
LINE SOLID SOLID SOLID SOLID
YLIMITS -1 1
YTIC OFFSET 0.1 0.1
XLIMITS 0 50
XTIC OFFSET 0.5 0.5
AUTOCORRELATION PLOT Y
LINE SOLID ALL
XLIMITS
YLIMITS
XTIC OFFSET 0 0
YTIC OFFSET 0 0
SPECTRAL PLOT Y
END OF MULTIPLOT
RESET PLOT CONTROL
.
SLEEP 3
.
DEMODULATION FREQUENCY 0.3
MULTIPLOT 4 4
MULTIPLOT CORNER COORDINATES 2 2 98 98
CHAR x
LINE BLANK
LOOP FOR K = 0.28 0.0025 0.3175
    DEMODULATION FREQUENCY K
    COMPLEX DEMODULATION PHASE PLOT Y
END OF LOOP
END OF MULTIPLOT
SLEEP 3
LET FREQ = 0.3025
.
LINE SOLID
CHARACTER BLANK 
COMPLEX DEMODULATION AMPLITUDE PLOT Y
LET AMPL = 390
SLEEP 3
.
LET C = MEAN Y
LET N = SIZE Y
LET X = SEQUENCE 1 1 N
FIT Y = C + AMPL*SIN(2*3.14159*FREQ*X + PHASE)
.
4-PLOT RES

