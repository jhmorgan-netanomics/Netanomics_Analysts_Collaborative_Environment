----- <~time\DDSSUNS2.DP> Run DDS analysis on SUNSPOT2.DAT

. This is file    DDSSUNS2.DP

DELETE Y
DIMENSION 25 VARIABLES
SKIP 25
READ SUNSPOT2.DAT Y
.
. ==============================================================
. STEP 2-- CARRY OUT DDS ANALYSIS
 
LET ORDER1 = 8
LET ORDER2 = ORDER1 - 1
DDS Y ORDER1 ORDER2
. 
. ===============================================================
. STEP 3--READ IN THE DDS RESIDUALS, GREEN FUNCTION, AND SPECTRUM
.
SKIP 5
READ RESIDUAL.OUT RESIDUALS
SKIP 5
READ GREEN.OUT INDEX GREENTOT GREEN1
SKIP 5
READ SPECTRUM.OUT FREQ SPECTOT SPEC1
.
. ==============================================
. STEP 4--GENERATE A MULTIPLOT OF THE DDS OUTPUT
.
FEEDBACK OFF
MULTIPLOT CORNER COORDINATES 5 5 95 95
MULTIPLOT 2 2
TIC LABEL SIZE 2
LABEL SIZE 3
TITLE DISPLACEMENT 3
TITLE SIZE 4
.
TITLE RAW DATA
Y1LABEL Y(T)
X1LABEL T
LINES SOLID
PLOT Y
.
TITLE GREEN'S FUNCTION
Y1LABEL G(J)
X1LABEL J
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
PLOT GREENTOT GREEN1 VERSUS INDEX
.
TITLE RESIDUALS
Y1LABEL A(T)
X1LABEL T
LINES
SPIKE ON
PLOT RESIDUALS
.
TITLE SPECTRUM
Y1LABEL S(F)
X1LABEL F
XLIMITS
LINES SOLID DOT DASH1 DASH2 DASH3 DASH4
SPIKE OFF
PLOT SPECTOT SPEC1 VERSUS FREQ
.
END OF MULTIPLOT
TITLE
Y1LABEL
X1LABEL
TIC LABEL SIZE
LABEL SIZE
TITLE DISPLACEMENT
TITLE SIZE
FEEDBACK ON

