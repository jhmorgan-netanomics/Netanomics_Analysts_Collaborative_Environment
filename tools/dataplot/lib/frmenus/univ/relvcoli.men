This is file relvcoli.men--Conf. Limits for 100*sigma/mu
 
--------------------
 1. Confidence Limits for 100*sigma/mu
 2. LET S = RELATIVE SD Y       (Vangel-McKay Method)
 3.
 4. Response Variable:
 4. @CE 1 0 33 50 ?
 5.
 6. Confidence level (select one):
 7. @CE 2 1 3 23 95% limits
 8. @CE 2 2 3 23 99% limits
 9.
10. Number of output decimal places:
10. @CE 3 0 37 50 ?
11.
12. Subset/Except/For Specification:
13. @CE 4 0 1 50 *
14.
15. Compute Confidence Limits?
--------------------
GR FE
SUMMARY _1 _4
 
@IF 2 1 LET ZP = .95
@IF 2 2 LET ZP = .99
LET ZP2 = ZP+(1-ZP)/2
LET ZPERC = 100*ZP
 
LET ZN = NUMBER _1 _4
LET ZDF = ZN-1
LET ZPPFU = CHSPPF(ZP2,ZDF)
LET ZPPFL = CHSPPF(1-ZP2,ZDF)
 
LET ZSTAT = RELATIVE SD _1 _4
LET ZSTAT0 = ZSTAT/100
LET ZTERM1 = (ZPPFU+2)/(ZDF+1)-1
LET ZTERM2 = (ZPPFL+2)/(ZDF+1)-1
LET ZARG1 = ZTERM1*(ZSTAT0**2) + ZPPFU/ZDF
LET ZARG2 = ZTERM2*(ZSTAT0**2) + ZPPFL/ZDF
LET ZLOWER = 100*ZSTAT0 / SQRT(ZARG1)
LET ZUPPER = 100*ZSTAT0 / SQRT(ZARG2)
 
IF ZLOWER < 0
   LET ZLOWER = 0
END IF
 
LET ZINC = (ZUPPER-ZLOWER)/200
LET ZWIDTH4 = (ZUPPER-ZLOWER)/4
LET ZLOWER2 = ZLOWER-ZWIDTH4
LET ZUPPER2 = ZUPPER+ZWIDTH4
 
IF ZLOWER2 < 0
   LET ZLOWER2 = .1
END IF
 
. With thanks to Mark Vangel:
LET FUNCTION ZNUMF = ((1/((ZZ/100)**2))+((ZDF-1)/(ZDF+1)))*ZDF*ZSTAT0*ZSTAT0
LET FUNCTION ZDENF = 1 + ZSTAT0*ZSTAT0/(ZDF+1)
LET FUNCTION ZF = (1/100)*CHSPDF((ZNUMF/ZDENF),ZDF)
LET FUNCTION ZF2 = (2*ZDF*(ZSTAT0)**2)/(((ZZ/100)**3)*ZDENF)
PLOT ZF*ZF2 FOR ZZ = ZLOWER2 ZINC ZUPPER2 AND
PLOT 0 FOR ZZ = ZLOWER2 ZINC ZUPPER2
 
JUSTIFICATION CENTER
LET ZZ = ZSTAT; LET ZPDFSTAT = ZF*ZF2
LET ZZ = ZLOWER; LET ZPDFLOWE = ZF*ZF2
LET ZZ = ZUPPER; LET ZPDFUPPE = ZF*ZF2
 
DRAWDDDS ZSTAT ZPDFSTAT ZSTAT 10
DRAWDDDS ZLOWER ZPDFLOWE ZLOWER 5
DRAWDDDS ZUPPER ZPDFUPPE ZUPPER 5
 
MOVEDS ZSTAT 6; TEXT ^ZSTAT
MOVEDS ZLOWER 1; TEXT ^ZLOWER
MOVEDS ZUPPER 1; TEXT ^ZUPPER
 
@IF 3 0 SET WRITE DECIMALS _3
WRITE "^ZPERC 2-Sided Confidence Limits for 100*sigma/mu"
WRITE " "
WRITE "Number of Observations  = ^ZN"
WRITE "Estimated Stan. Dev.    = ^ZSTAT"
WRITE " "
WRITE "Estimated Lower Limit   = ^ZLOWER"
WRITE "Estimated Upper Limit   = ^ZUPPER"
WRITE ZPERC ZN ZSTAT ZLOWER ZUPPER
--------------------
