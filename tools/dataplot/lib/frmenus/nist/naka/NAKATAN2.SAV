. This is Dataplot program      NAKATAN2.DP
. Non-linear fit of Nakatani Data Set 2
. Date--January 1998
.
. -----start point-----
.
. DEVICE 1 X11
DEVICE 2 POSTSCRIPT
.
. ----- STEP 1--READ IN THE DATA
.
SKIP 25
READ NAKATAN2.DAT Y X SDY
DELETE Y X SDY FOR I = 1 1 10
.
. ----- STEP 2--PLOT THE DATA
.
TITLE NAKATAN2.DAT
X3LABEL AUTOMATIC
CHARACTERS X BLANK
LINES BLANK SOLID
MULTIPLOT 2 2
PLOT Y X
.
. ----- STEP 3--SELECT THE MODEL, DEFINE STARTING VALUES, FIT THE DATA
.
LET FUNCTION F1 = 3*(SIN(A3*X) - X*A3*COS(A3*X)) / (A3*X)**3
LET FUNCTION F2 = 0.0058*F1**2
LET FUNCTION F3 = (X*A3)**A1 * (1  + 1/(X*A2)**2)**((A1-1)/2)
LET FUNCTION F4 = 1 + A1*GAMMA(A1-1) * SIN((A1-1)*ATAN(X*A2))/F3
LET FUNCTION F = 0.0476 * F2 * F4
.
LET A1 = 3
LET A2 = 60
LET A3 = 2
. LET A4 = 0.0058
. LET A5 = 10
. LET FUNCTION PHI = .0476
.
FIT Y = F
.
SET WRITE DECIMALS 7
LET N = NUMBER Y
WRITE NAKATAN2.OUT N A1 A2 A3 RESSD
.
. ----- STEP 4--DO POST-FIT GRAPHICAL ANALYSIS
.
PLOT Y PRED VERSUS X
PLOT RES X
NORMAL PROBABILITY PLOT RES
MULTIPLOT OFF
.
. ----- STEP 5--WRITE OUT CONCLUSIONS
.
WRITE N A1 A2 A3 RESSD
