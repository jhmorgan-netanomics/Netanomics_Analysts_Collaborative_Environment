.
.  Perform a Kruskal-Wallis non-parametric 1-way ANOVA
.
.  You must pre-define P to be the number of variables and
.  create the variables X1, X2, ..., XP.
.
FEEDBACK OFF
LOOP FOR K = 1 1 P
   LET N^K = SIZE X^K
   LET TAG^K = K FOR I = 1 1 N^K
END OF LOOP
LOOP FOR L = 2 1 P
   LET TEMP = X^L
   EXTEND X1 TEMP
   LET TEMP = TAG^L
   EXTEND TAG1 TEMP
   DELETE TEMP
END OF LOOP
.
LET N = SIZE X1
LET R = RANK X1
LET SUM1 = 0
LOOP FOR K = 1 1 P
   LET R^K = SUM R SUBSET TAG1 = K
   LET SUM1 = SUM1 + (R^K)**2/N^K
END OF LOOP
.
LET H = SUM1*(12/(N*(N+1))) - 3*(N+1)
LET ALPHA = 0.95
LET DF = P - 1
LET CRITICAL = CHSPPF(ALPHA,DF)
.
PRINT " "
PRINT "H0: ^P INDEPENDENT SAMPLES ARE FROM IDENTICAL POPULATIONS"
PRINT "HA: ^P INDEPENDNET SAMPLES ARE FROM DIFFERENT POPULATIONS"
PRINT "KRUSKAL-WALLIS H STATISTIC = ^H"
PRINT "CHI-SQUARE CRITICAL VALUE = ^CRITICAL"
IF H <= CRITICAL
  PRINT "ACCEPT NULL HYPOTHESIS"
END OF IF
IF H > CRITICAL
  PRINT "REJECT NULL HYPOTHESIS"
END OF IF
.
FEEDBACK ON
