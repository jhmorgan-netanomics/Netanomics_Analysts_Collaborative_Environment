. THIS IS THE DATAPLOT MACRO FILE     DEXCONTQ.DP
. PURPOSE--GENERATE A DEX CONTOUR PLOT FOR 2-LEVEL DESIGNS
.          FOR A QUADRATIC MODEL IN X1 AND C2
.          THIS MACRO GENERATES A CONTOUR PLOT
.          OF Y VERSUS U1 AND U2
.          AT ISOLINES AS GIVEN IN YCONT.
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y  = THE RESPONSE VARIABLE
.       2. U1 = DESIRED HORIZONTAL AXIS FACTOR (CODED)
.       3. U2 = DESIRED VERTICAL AXIS FACTOR (CODED)
.       4. YCONT = DESIRED CONTOUR LINE VALUES
. NOTE--THE CONTOUR PLOT WILL RANGE FOR -2 TO 2
.       IN BOTH U1 AND U2 DIRECTIONS.
. NOTE--SINCE THE BI-VARIATE QUADRATIC MODEL:
.          Y = C0 + C1*U1 + C2*U2 + C12*U1*U2 +
.              C11*U1**2 + C22*U2**2
.       WILL BE FIT AND SINCE THIS MODEL
.       CONTAINS 6 COEFFICIENTS,
.       THEN THE INPUT VECTORS U1 AND U2 MUST
.       CONSIST OF AT LEAST 6 DISTINCT POINTS
.       TYPICALLY CODED AROUND AROUND -1 AND +1.
. UPDATED--JUNE 1995
. NOTE--THIS MACRO CREATES SEVERAL INTERMEDIATE VARIABLES.
.       A COMMON CAUSE FOR THIS MACRO TO FAIL IS THAT THE USER
.       HAS AN INTERNAL DATAPLOT WORKSHEET THAT IS DIMENSIONED
.       (MAX NUMBER OF VARIABLES)
.       TOO SMALL (FOR EXAMPLE, THE DEFAULT DIMENSION
.       ON SOME COMPUTERS MAY ALLOW A MAX OF 10 VARIABLES ONLY).
.       TO ADJUST FOR THIS, THE ANALYST MAY "MANUALLY"
.       INCREASE THE MAX NUMBER OF VARIABLES
.       VIA THE DIMENSION STATEMENT, AS IN:
.          DIMENSION 50 VARIABLES
. EXAMPLE--
.       DIMENSION 50 VARIABLES
.       SKIP 25
.       READ BOXYIEL3.DAT Y X1 X2
.       LET U1 = X1
.       LET U2 = X2
.       LET YCONT = SEQUENCE 70 2 100
.       CALL DEXCONTQ.DP
.
. -----START POINT-------------------
.
capture junk.
. FEEDBACK OFF
.
MULTIPLOT FREEZE
.
LIMITS -2 2
TIC OFFSET UNITS SCREEN
TIC OFFSET 0 0
.
. STEP 1--COPY OVER THE INPUT DATA
.         (AND EXTRACT THE SUBSET IF A SUBSET/EXCEPT/FOR QUALIFIER EXISTS)
.
LET YPRIME = Y
LET U1PRIME = U1
LET U2PRIME = U2
RETAIN YPRIME U1PRIME U2PRIME ^QUAL
.
LET YPRIM2 = YPRIME
LET U1PRIM2 = U1PRIME
LET U2PRIM2 = U2PRIME
LET V1 = U1PRIM2
LET V2 = U2PRIM2
.
. STEP 2--COMPUTE COEFFICIENTS FROM THE CORNER POINTS.
.         COMPUTE PREDICTED VALUES AND RESIDUALS EVERWHERE.
.
. LET FUNCTION Q1 = C0 + C1*U1PRIM2 + C2*U2PRIM2 + C12*U1PRIM2*U2PRIM2
. LET FUNCTION Q2 = C11*U1PRIM2*U1PRIM2 + C22*U2PRIM2*U2PRIM2
LET FUNCTION Q1 = C0 + C1*V1 + C2*V2 + C12*V1*V2
LET FUNCTION Q2 = C11*V1*V1 + C22*V2*V2
LET FUNCTION Q12 = Q1+Q2
FIT YPRIM2 = Q12
.
LET FUNCTION Q3 = C0 + C1*U1 + C2*U2 + C12*U1*U2
LET FUNCTION Q4 = C11*U1*U1 + C22*U2*U2
LET FUNCTION Q34 = Q3+Q4
LET PRED = Q34 ^QUAL
LET RES = Y-PRED ^QUAL
.
. STEP 3--GENERATE THE CONTOUR PLOT
.
LET V1 = SEQUENCE -2 .4 2 FOR I = 1 1 121
LET V2 = SEQUENCE -2 11 .4 2
LET YGRID = Q12
CONTOUR PLOT YGRID V1 V2 YCONT
.
. STEP 4--DRAW THE INNER SAMPLING SQUARES
.         (AND A CENTER POINT, IF EXISTENT)
.
PRE-ERASE OFF
LINES DOTTED
CHARACTER CIRCLE
CHARACTER HW 1.5 1.2
CHARACTER FILL ON
PRE-SORT OFF
LET W1 = DATA -1 1 1 -1 -1
LET W2 = DATA -1 -1 1 1 -1
PLOT W2 W1
DELETE W1 W2
.
LET NCENTER = NUMBER YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
IF NCENTER > 0
   LET W3 = DATA 0 0
   PLOT W3 W3
   DELETE W3
END IF
.
LINES BLANK
PLOT U2PRIME U1PRIME
.
. STEP 5--WRITE THE AVERAGES NEAR THE VERTICES
.         AND THE AVERAGE NEAR THE CENTER POINT, IF EXISTENT)
.
LET YMM = MEAN YPRIM2 SUBSET U1PRIM2 -1 SUBSET U2PRIM2 -1
LET YPM = MEAN YPRIM2 SUBSET U1PRIM2 1 SUBSET U2PRIM2 -1
LET YMP = MEAN YPRIM2 SUBSET U1PRIM2 -1 SUBSET U2PRIM2 1
LET YPP = MEAN YPRIM2 SUBSET U1PRIM2 1 SUBSET U2PRIM2 1
JUST RIGHT; MOVEDATA -1.1 -1.2; TEXT ^YMM
JUST LEFT; MOVEDATA +1.1 -1.2; TEXT ^YPM
JUST RIGHT; MOVEDATA -1.1 +1.1; TEXT ^YMP
JUST LEFT; MOVEDATA +1.1 +1.1; TEXT ^YPP
.
LET YWEST = MEAN YPRIM2 SUBSET U1PRIM2 -1.414 SUBSET U2PRIM2 0
LET YNORTH = MEAN YPRIM2 SUBSET U1PRIM2 0 SUBSET U2PRIM2 +1.414
LET YEAST = MEAN YPRIM2 SUBSET U1PRIM2 +1.414 SUBSET U2PRIM2 0
LET YSOUTH = MEAN YPRIM2 SUBSET U1PRIM2 0 SUBSET U2PRIM2 -1.414
JUST RICE; MOVEDATA -1.5 -0; TEXT ^YWEST
JUST CENTER; MOVEDATA 0 +1.5; TEXT ^YNORTH
JUST LECE; MOVEDATA +1.5 0; TEXT ^YEAST
JUST CENTER; MOVEDATA 0 -1.6; TEXT ^YSOUTH
.
IF NCENTER > 0
   LET YCP = MEAN YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   JUST LEFT; MOVEDATA +0.1 +0.1; TEXT ^YCP
END IF
.
. STEP 6--CHECK FOR REPLICATED CENTER POINTS.
.         IF HAVE REPLICATED CENTER POINTS,
.         THEN DO CURVATURE CHECK VIA T TEST
.
IF NCENTER > 1.5
   LET MCENTER = MEAN YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   LET SDCENTER = SD YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   LET NEDGE = NUMBER YPRIME EXCEPT U1PRIME 0 EXCEPT U2PRIME 0
   LET MEDGE = MEAN YPRIME EXCEPT U1PRIME 0 EXCEPT U2PRIME 0
   LET STATNUM = MEDGE-MCENTER
   LET STATDEN = SDCENTER*SQRT((1/NEDGE)+(1/NCENTER))
   LET TESTSTAT = STATNUM/STATDEN
   LET NDF = NCENTER-1
   LET CUTOFF2 = TPPF(.975,NDF)
   LET CUTOFF1 = -CUTOFF2
.
   LET STRING CONCLUSION = NO CURVATURE
   IF TESTSTAT < CUTOFF1
      LET STRING CONCLUSION = CURVATURE
   END IF
   IF TESTSTAT > CUTOFF2
      LET STRING CONCLUSION = CURVATURE
   END IF
END IF
.
IF NCENTER > 1.5
   JUSTIFICATION CENTER
   MOVE 50 11
   TEXT Center: Num. Obs. = ^NCENTER  Average = ^MCENTER  SD = ^SDCENTER
   MOVE 50 8
   TEXT Edge: Num. Obs. = ^NEDGE   Average = ^MEDGE
   MOVE 50 5
   TEXT Curvature Check:  t Test Stat Value = ^TESTSTAT
   MOVE 50 2
   TEXT t^NDF Lower Cutoff = ^CUTOFF1   t^NDF Upper Cutoff = ^CUTOFF2
END IF
.
. STEP 7--CLEAN UP
.
MULTIPLOT UNFREEZE
.
LIMITS
TIC OFFSET UNITS SCREEN
TIC OFFSET UNITS 5 5
PRE-ERASE ON
LINES SOLID ALL
CHARACTERS BLANK ALL
CHARACTER FILL OFF
PRE-SORT ON
.
FEEDBACK ON
end capture
