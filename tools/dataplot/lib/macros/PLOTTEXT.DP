. THIS IS THE DATAPLOT MACRO FILE     PLOTTEXT.DP
. PURPOSE--SUPERIMPOSE A SET OF TEXT STRINGS
.          ON A PRE-EXISTING PLOT
. THE FOLLOWING 10 DATAPLOT PARAMETERS MUST
. BE PREDEFINED UPON ENTRANCE TO THIS MACRO--
.    1) XAXISMIN = XAXIS TIC LABEL MINIMUM
.    2) XAXISMAX = XAXIS TIC LABEL MAXIMUM
.    3) YAXISMIN = YAXIS TIC LABEL MINIMUM
.    4) YAXISMAX = YAXIS TIC LABEL MAXIMUM
.    5) X1COOR   = XAXIS FRAME MINIMUM (0 TO 100)
.    6) X2COOR   = XAXIS FRAME MAXIMUM (0 TO 100)
.    7) Y1COOR   = YAXIS FRAME MINIMUM (0 TO 100)
.    8) Y2COOR   = YAXIS FRAME MAXIMUM (0 TO 100)
.    9) XDEL     = HORIZONTAL SHIFT FOR TEXT LOCATION (0 TO 100)
.   10) YDEL     = VERTICAL   SHIFT FOR TEXT LOCATION (0 TO 100)
. ALSO--(X,Y) COORDINATES (IN DATA UNITS OF THE LAST PLOT)
.       ARE ASSUMED TO BE IN THE DATAPLOT
.       VARIABLES X AND Y RESPECTIVELY, AND
.       THE TEXT STRINGS TO BE PLOTTED ARE ASSUMED
.       TO BE IN THE STRINGS S1, S2, S3, ETC.
. DATE--JUNE 1989
.
. -----START POINT-----------------------------------
.
LET N = NUMBER Y
.
. LET PX1 = (XK-XAXISMIN)/(XAXISMAX-XAXISMIN)
. LET PY1 = (YK-YAXISMIN)/(YAXISMAX-YAXISMIN)
. LET PX2 = X1COOR+PX1*(X2COOR-X1COOR)
. LET PY2 = Y1COOR+PY1*(Y2COOR-Y1COOR)
. LET PX3 = PX2+XDEL
. LET PY3 = PY2+YDEL
LET XSLOPE = (X2COOR-X1COOR)/(XAXISMAX-XAXISMIN)
LET XINT = X1COOR-XAXISMIN*XSLOPE
LET XINT = XINT+XDEL
LET YSLOPE = (Y2COOR-Y1COOR)/(YAXISMAX-YAXISMIN)
LET YINT = Y1COOR-YAXISMIN*YSLOPE
LET YINT = YINT+YDEL
.
LOOP FOR K = 1 1 N
.
LET XK = X(K)
LET YK = Y(K)
LET PX3 = XINT+XSLOPE*XK
LET PY3 = YINT+YSLOPE*YK
.
MOVE PX3 PY3
TEXT ^S^K
.
END OF LOOP
