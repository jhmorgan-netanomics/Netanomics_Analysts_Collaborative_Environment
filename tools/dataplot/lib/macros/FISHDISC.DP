.
.  Macro to perform a Fisher's discriminant analysis
.
.  To run this macro:
.
.  1) Define the data matrix       ZZZZZZZX
.  2) Define the group id variable ZTAG
.  3) Define desired character settings for the plot
.  4) Enter:  CALL FISHDISC.DP
.
.  This macro does the following:
.
.  1) Calculates the p discriminant functions
.  2) Plots the first 2 discriminant functions
.
FEEDBACK OFF
DELETE GROUPID XMGRAND DIAG B0 XMEANI SPOOL WINVB E EV EVECT
DELETE ZY ZX GMEANX GMEANY
.
LET NTOT = MATRIX NUMBER OF ROWS ZZZZX
LET P = MATRIX NUMBER OF COLUMNS ZZZZX
.
LET GROUPID = DISTINCT ZTAG
LET NG = SIZE GROUPID
LET XMGRAND = MATRIX COLUMN MEANS ZZZZX
.
.  CALCULATE B0 = SUM (I=1,NG) (XBARi - XBARALL)(XBARi-XBARALL)'
.
LET DIAG = 0 FOR I = 1 1 P
LET B0 = DIAGONAL MATRIX DIAG
.
LOOP FOR K = 1 1 NG
   LET N^K = SIZE ZTAG SUBSET ZTAG = K
   LET XMEANI = MATRIX COLUMN MEANS ZZZZX SUBSET ZTAG = K
   LET XMEANI= XMEANI - XMGRAND
   LET B0TEMP = VECTOR TIMES TRANSPOSE XMEANI
   LET B0 = MATRIX ADDITION B0 B0TEMP
END OF LOOP
.
.  CALCULATE Spooled = (N1-1)S1 + .. + (Ng-1)Sg)/(N1+ .. + Ng - g)
LET SPOOL = POOLED VARIANCE-COVARIANCE MATRIX ZZZZX ZTAG
LET DENOM = NTOT - NG
LET WINVB = MATRIX MULTIPLICATION SPOOL DENOM
LET WINVB = MATRIX INVERSE WINVB
LET WINVB = MATRIX MULTIPLICATION WINVB B0
.
.  COMPUTE EIGENVALUES AND SORT IN DECREASING ORDER
.  COMPUTE EIGENVECTORS, ONLY KEEP REAL COMPONENT, SORT
.
LET E = MATRIX EIGENVALUES WINVB
LET EV = MATRIX EIGENVECTORS WINVB
LET INDX = SEQUENCE 1 1 P
RETAIN E FOR I = 1 1 P
LET ESORT = SORTC E INDX
LET REVERSE = SEQUENCE P 1 1
LET REVERSE = SORTC REVERSE ESORT INDX
LET EVECT = DIAGONAL MATRIX DIAG
.  NORMALIZE L'SpooledL =1
.  DIST = L'SpooledL, MULTIPLY EIGENVECTOR BY 1/SQRT(DIST)
LOOP FOR K = 1 1 P
    LET LZTAG = INDX(K)
    RETAIN EV^K  FOR I = 1 1 P
    LET EVECT^LZTAG = EV^K
    LET DIST = QUADRATIC FORM SPOOL EVECT^LZTAG
    LET EVECT^LZTAG = (1/SQRT(DIST))*EVECT^LZTAG
END OF LOOP
.  PLOT FIRST 2 DISCRIMINANTS
LET ZY = LINEAR COMBINATION ZZZZX EVECT1
LET ZX = LINEAR COMBINATION ZZZZX EVECT2
DEVICE 1 OFF
MEAN PLOT ZY ZTAG
LET GMEANY = YPLOT
MEAN PLOT ZX ZTAG
LET GMEANX = YPLOT
RETAIN GMEANX GMEANY SUBSET TAGPLOT = 1
DEVICE 1 ON
Y1LABEL FIRST DISCRIMINANT
X1LABEL SECOND DISCRIMINANT
LINE BLANK ALL
PLOT ZY ZX ZTAG
CAPTURE ZZZZJUNK.DAT
PRINT "FISHER's DISCRIMINANT ANALYSIS"
PRINT " "
PRINT " "
PRINT "B0 MATRIX (= between group sums of cross-products):"
PRINT B0
PRINT " "
PRINT " "
PRINT "POOLED VARIANCE-COVARIANCE MATRIX:"
PRINT SPOOL
PRINT " "
PRINT " "
PRINT "EIGENVALUES:"
PRINT ESORT
PRINT " "
PRINT " "
PRINT "COLUMNS ARE THE DISCRIMINANT FUNCTIONS:"
PRINT EVECT
PRINT " "
PRINT " "
PRINT "GROUP MEANS:"
PRINT GMEANX GMEANY
END OF CAPTURE
FEEDBACK ON
LIST ZZZZJUNK.DAT
