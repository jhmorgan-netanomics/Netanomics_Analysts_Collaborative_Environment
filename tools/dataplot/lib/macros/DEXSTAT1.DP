. THIS IS THE DATAPLOT MACRO FILE     DEXSTAT1.DP
. PURPOSE--GENERATE A MULTIPLOT OF STATISTIC PLOTS
.          FOR A USER-DEFINED STATISTIC (MEAN, MEDIAN,
.          SD, ANOP, ETC.)
.          THIS MACRO GENERATES PLOTS
.          FOR MAIN EFFECTS ONLY.
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y = THE RESPONSE VARIABLE
.       2. X1 X2 X3 X4 ETC. = FACTORS
.       3. NUMFAC = THE NUMBER OF FACTORS
.       4. STAT = THE STRING WITH THE DESIRED STATISTIC
.              EXAMPLE--LET STRING STAT = MEAN
.              EXAMPLE--LET STRING STAT = SD
.              EXAMPLE--LET STRING STAT = ANOP
.       5. DP = THE NUMBER OF DECIMAL PLACES THE
.              COMPUTED SLOPE SHOULD BE ROUNDED TO
.              EXAMPLE--LET DP = 1
.       6. ANOP LIMITS (ONLY NEEDED IF STAT = ANOP)
.              EXAMPLE--ANOP LIMITS 0 65
.              EXAMPLE--ANOP LIMITS 80 100
. NOTE--THE USER SHOULD PRE-SET YLIMITS
.       FOR MEAN PLOT, YLIMITS SHOULD BE ABOUT
.       DATA YMIN TO YMAX, OR ROUGHLY 1/2 THE RANGE
.       FOR ANOP PLOT, YLIMITS SHOULD = 0 100
. EXAMPLE--SKIP 25
.          READ BOXBIKE2.DAT Y X1 TO X7
.          LET NUMFAC = 7
.          LET STRING STAT = MEAN
.          LET DP = 1
.          YLIMITS 50 100
.          CALL DEXSTAT1.DP
.
. -----START POINT--------------------
.
FEEDBACK OFF
.
. ------------------------------------
. STEP 1--
. SET UP SPECIAL PLOT FORMAT
.
XLIMITS -2 2
CHAR X BLANK
LINES SOLID DOTTED
TICS OFF
TIC LABELS OFF
FRAME CORNER COORDINATES 0 0 100 100
.
. ------------------------------------
. STEP 2--
. GENERATE THE PLOTS
.
LET NC = INT(SQRT(NUMFAC-1))+1
LET NR = 1
LET NR = 2 IF NUMFAC >= 3
LET NR = 3 IF NUMFAC >= 7
LET NR = INT(NUMFAC/NC)+1 IF NUMFAC >= 11
MULTIPLOT NR NC
XLIM -2 2
LOOP FOR K = 1 1 NUMFAC
LET JUNK1 = X^K
LET JUNK1 = CODE JUNK1
LET JUNK1 = 2*JUNK1-3
LET XAXIS = JUNK1
LET XAXIS2 = Y*XAXIS
LET SLOPE = MEAN XAXIS2
LET SLOPE = 2*SLOPE
LET SLOPE = ROUND(SLOPE,DP)
LEGEND 1 ^K  ^SLOPE
^STAT PLOT Y XAXIS
END OF LOOP
.
. ------------------------------------
. STEP 3--
. REVERT BACK TO THE ORIGINAL PLOT FORMAT
.
XLIMITS
CHAR BLANK ALL
LINES SOLID ALL
Y1TICS ON
X1TICS ON
Y1TIC LABELS ON
X1TIC LABELS ON
FRAME CORNER COORDINATES
LEGEND 1
MULTIPLOT OFF
