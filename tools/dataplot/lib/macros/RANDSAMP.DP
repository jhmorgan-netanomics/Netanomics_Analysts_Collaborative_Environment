. THIS IS THE DATAPLOT PROGRAM FILE     RANDOMSAMPLE.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN
.          WILL GENERATE A SIMPLE OR STRATIFIED
.          RANDOM SAMPLE (WITH REPLACEMENT).
. DATE--JUNE 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
SET IO TERMINAL
.
ERASE
.
WRITE " "
WRITE "               ****************************"
WRITE "               **     DATAPLOT MENU      **"
WRITE "               **     RANDOM SAMPLE      **"
WRITE "               ****************************"
WRITE " "
.
. STEP 1A--
. READ IN USER SPECIFICATIONS
.
WRITE " "
WRITE "TOTAL NUMBER OF ITEMS IN THE POPULATION = ?"
READ PARAMETER NPOP
WRITE "DESIRED NUMBER OF ITEMS IN THE SAMPLE = ?"
READ PARAMETER N
WRITE "DESIRED NUMBER OF SUB-BLOCKS = ?"
WRITE "   ENTER 1 IF WANT A SIMPLE RANDOM SAMPLE"
WRITE "   ENTER 2 IF WANT 2 RANDOMIZED BLOCKS"
WRITE "   ENTER 3 IF WANT 3 RANDOMIZED BLOCKS, ETC."
READ PARAMETER NUMBLOCK
WRITE "OUTPUT FILE = ?"
READ STRING NFILE
.
LET NIPOP = NPOP/NUMBLOCK
LET NI = N/NUMBLOCK
.
. STEP 1B--
. WRITE OUT A SUMMARY OF USER SPECS
.
WRITE " "
WRITE " "
WRITE "POPULATION   SIZE                    = ^NPOP "
WRITE "TOTAL SAMPLE SIZE                    = ^N "
WRITE "NUMBER OF BLOCKS                     = ^NUMBLOCK "
WRITE "NUMBER OF POPULATION ITEMS PER BLOCK = ^NIPOP "
WRITE "NUMBER OF SAMPLE     ITEMS PER BLOCK = ^NI "
WRITE "OUTPUT FILE                          = ^NFILE "
WRITE " "
WRITE "THE SAMPLING PLAN IS NOW BEING FORMED ..."
.
. STEP 2A--
. GENERATE THE RANDOM SAMPLE
.
LET NIP1 = NI+1
LET Y2(1) = -999
LOOP FOR K = 1 1 NUMBLOCK
LET FACTOR = (K-1)*NIPOP
. LET Y = RANDOM PERMUTATION FOR I = 1 1 NIPOP
LET Y = UNIFORM RANDOM NUMBERS FOR I = 1 1 NIPOP
DELETE Y FOR I = NIP1 1 NIPOP
LET Y = NIPOP*Y
LET Y = INT(Y+0.5)
LET Y = Y+FACTOR
APPEND Y Y2
END OF LOOP
DELETE Y2(1)
LET Y3 = SORT Y2
.
. STEP 2B--
. PRINT OUT THE RANDOM SAMPLE
.
ERASE
WRITE " "
WRITE "        ITEM"
WRITE "     IDENTIFIER"
WRITE " "
LOOP FOR K = 1 1 N
LET Y3K = Y3(K)
WRITE "^K        ^Y3K "
LET KMOD = MOD(K,NI)
IF KMOD = 0
WRITE " "
END OF IF
END OF LOOP
.
. STEP 3A--
. RANDOMIZE THE RUN (TIME) SEQUENCE IN
. WHICH THE POPULATION ITEMS ARE TO
. BE EXPERIMENTED UPON.
.
. LET TIME = RANDOM PERMUTATION 1 1 N
LET TIME = UNIFORM RANDOM NUMBERS FOR I = 1 1 N
LET Y4 = Y3
LET TIME4 = SORTC TIME Y4
.
. STEP 3B--
. WRITE OUT THE FINAL SAMPLING PLAN
. (ORDERED BY RUN SEQUENCE)
.
WRITE " "
WRITE " "
WRITE "  RUN        ITEM"
WRITE "SEQUENCE  IDENTIFIER"
WRITE " "
LOOP FOR K = 1 1 N
LET Y4K = Y4(K)
WRITE ^NFILE "    ^K        ^Y4K "
WRITE "    ^K        ^Y4K "
END OF LOOP
.
LET DUMMY = SEQUENCE 1 1 N
SET WRITE FORMAT 0
WRITE ^NFILE DUMMY Y3 DUMMY Y4
WRITE " "
WRITE " "
WRITE "THE OUTPUT IS IN ^NFILE "
CALL LOGO3.DP
.
SET IO
SET IRD 5
