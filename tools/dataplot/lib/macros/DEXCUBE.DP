. THIS IS THE DATAPLOT MENU MACRO FILE     DEXCUBE.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN
.          WILL GENERATE A CUBE REPRESENTATION
.          FOR A 2**3 FACTORIAL DESIGN.
. DATE--JUNE 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
SET IO TERMINAL
.
ER
.
WRITE " "
WRITE "               ****************************"
WRITE "               **    DATAPLOT MENU--     **"
WRITE "               **    CUBE DIAGRAM FOR    **"
WRITE "               **  2**3 FACTORIAL DESIGN **"
WRITE "               ****************************"
WRITE " "
.
WRITE " "
WRITE "----------------------------------------"
WRITE " "
WRITE "DO YOU WISH A    DEVICE 2 POSTSCRIPT    COMMAND EXECUTED NOW?"
WRITE "ENTER 0 IF NO     ENTER 1 IF YES"
READ PARAMETER POSTYN
IF POSTYN = 1
DEVICE 2 POSTSCRIPT
END OF IF
WRITE " "
.
. ---------------------------------
. STEP 1--
. READ IN USER SPECIFICATIONS
.
WRITE " "
WRITE "NAME FOR VARIABLE 1 (FRONT, BOTTOM) = ?"
READ STRING A1
WRITE "NAME FOR VARIABLE 2 (LEFT, UP)      = ?"
READ STRING A2
WRITE "NAME FOR VARIABLE 3 (RIGHT, BOTTOM) = ?"
READ STRING A3
.
WRITE " "
WRITE "DATA POINT 1 (BOTTOM, FRONT, LEFT ) = ?"
READ STRING D1
WRITE "DATA POINT 2 (BOTTOM, FRONT, RIGHT) = ?"
READ STRING D2
WRITE "DATA POINT 3 (TOP   , FRONT, LEFT ) = ?"
READ STRING D3
WRITE "DATA POINT 4 (TOP   , FRONT, RIGHT) = ?"
READ STRING D4
WRITE "DATA POINT 5 (BOTTOM, BACK , LEFT ) = ?"
READ STRING D5
WRITE "DATA POINT 6 (BOTTOM, BACK , RIGHT) = ?"
READ STRING D6
WRITE "DATA POINT 7 (TOP   , BACK , LEFT ) = ?"
READ STRING D7
WRITE "DATA POINT 8 (TOP   , BACK , RIGHT) = ?"
READ STRING D8
.
WRITE " "
WRITE "TITLE          (POSITIONED AT TOP OF PAGE) = ?"
READ STRING T
WRITE "SUB-TITLE      (POSITIONED UNDER THE TITLE) = ?"
READ STRING ST
WRITE "BASE TITLE     (POSITIONED 3/4 DOWN PAGE) = ?"
READ STRING BT
WRITE "SUB-BASE TITLE (POSITIONED UNDER BASE TITLE) = ?"
READ STRING SBT
.
WRITE " "
WRITE "CHARACTER SIZE FOR THE DATA = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HD
WRITE "CHARACTER SIZE FOR TITLE = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HT
WRITE "CHARACTER SIZE FOR SUB-TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HST
WRITE "CHARACTER SIZE FOR BASE TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HBT
WRITE "CHARACTER SIZE FOR SUB-BASE TITLE = ?"
WRITE "(1 IS A RECOMMENDED CHOICE)"
READ PARAMETER HSBT
.
WRITE " "
WRITE "FONT = ?"
READ STRING F
.
WRITE " "
WRITE "THE CUBE IS NOW BEING FORMED--PLEASE WAIT . . ."
.
. ---------------------------------
. STEP 2--
. DEFINE INTERNAL SETTINGS
.
LET X1=29
LET Y1=35
.
LET XDEL=30
LET YDEL=30
LET X2DEL=12
LET Y2DEL=6
LET CIRCDEL=.4
.
.
. ---------------------------------
. STEP 3--
. DRAW THE CUBE
.
LET X2=X1+XDEL
LET Y2=Y1+YDEL
LET X3=X1+X2DEL
LET Y3=Y1+Y2DEL
LET X4=X2+X2DEL
LET Y4=Y2+Y2DEL
.
LET X1M=X1-CIRCDEL
LET X2M=X2-CIRCDEL
LET X3M=X3-CIRCDEL
LET X4M=X4-CIRCDEL
LET Y1M=Y1-CIRCDEL
LET Y2M=Y2-CIRCDEL
LET Y3M=Y3-CIRCDEL
LET Y4M=Y4-CIRCDEL
.
LET X1P=X1+CIRCDEL
LET X2P=X2+CIRCDEL
LET X3P=X3+CIRCDEL
LET X4P=X4+CIRCDEL
LET Y1P=Y1+CIRCDEL
LET Y2P=Y2+CIRCDEL
LET Y3P=Y3+CIRCDEL
LET Y4P=Y4+CIRCDEL
.
DEFINE ER ESC FF
ER
.
BOX X1 Y1 X2 Y2
BOX X3 Y3 X4 Y4
DRAW X1 Y1 X3 Y3
DRAW X1 Y2 X3 Y4
DRAW X2 Y1 X4 Y3
DRAW X2 Y2 X4 Y4
.
. ---------------------------------
. STEP 4--
. FILL IN THE CIRCLES AT THE 8 VERTICES
.
REGION FILL ON
CIRCLE X1M Y1M X1P Y1P
CIRCLE X2M Y1M X2P Y1P
CIRCLE X1M Y2M X1P Y2P
CIRCLE X2M Y2M X2P Y2P
CIRCLE X3M Y3M X3P Y3P
CIRCLE X4M Y3M X4P Y3P
CIRCLE X3M Y4M X3P Y4P
CIRCLE X4M Y4M X4P Y4P
REGION FILL OFF
.
. ---------------------------------
. STEP 5--
. DRAW IN THE 3 AXIS ARROWS
.
HW .5 2.5
LET X11 = (2*X1+X2)/3
LET X12 = (X1+2*X2)/3
LET Y11 = Y1 - 3
LET Y12 = Y1 - 3
ARROW X11 Y11 X12 Y12
.
LET X31 = X1 - 3
LET X32 = X1 - 3
LET Y31 = (2*Y1+Y2)/3
LET Y32 = (Y1+2*Y2)/3
ARROW X31 Y31 X32 Y32
.
LET X21 = X2
LET X21 = X21 + 5
LET X22 = (X2+2*X4)/3
LET X22 = X22 + 5
LET Y21 = Y1
LET Y22 = (Y1+2*Y3)/3
ARROW X21 Y21 X22 Y22
.
. ---------------------------------
. STEP 6--
. WRITE IN THE 3 AXIS IDENTIFIERS
.
FONT ^F
JUSTIFICATION CENTER
HW 1.5 1
ANGLE UNITS DEGREES
.
LET X5 = (X1+X2)/2
LET Y5 = Y1-10
MOVE X5 Y5
TEXT ^A1
.
ANGLE 90
LET X7 = X1-8
LET Y7 = (Y1+Y2)/2
MOVE X7 Y7
TEXT ^A2
.
ANGLE 35
LET X6 = (X2+X4)/2
LET X6 = X6 + 12
LET Y6 = (Y1+Y3)/2
LET Y6 = Y6 - 5
MOVE X6 Y6
TEXT ^A3
.
ANGLE 0
.
. ---------------------------------
. STEP 7--
. WRITE IN THE 8 DATA POINTS
. (1 DATA POINT PER VERTEX)
.
. HW 1.25 .8
LET WD = HD/2
HEIGHT HD
WIDTH WD
HW HD WD
.
JUSTIFICATION RIGHT
.
LET DELX1 = -1.5
LET DELY1 = -2.5
LET DELX2 = +1.5
LET DELY2 = -2.5
LET DELX3 = -1.5
LET DELY3 = +2.0
LET DELX4 = +1.5
LET DELY4 = -1.5
LET DELX5 = -1.5
LET DELY5 = +1.0
LET DELX6 = +1.5
LET DELY6 = +1.0
LET DELX7 = -1.5
LET DELY7 = +2.0
LET DELX8 = +1.5
LET DELY8 = +2.0
.
LET XNEW = X1 + DELX1
LET YNEW = Y1 + DELY1
MOVE XNEW YNEW
TEXT ^D1
JUSTIFICATION LEFT
LET XNEW = X2 + DELX2
LET YNEW = Y1 + DELY2
MOVE XNEW YNEW
TEXT ^D2
JUSTIFICATION RIGHT
LET XNEW = X1 + DELX3
LET YNEW = Y2 + DELY3
MOVE XNEW YNEW
TEXT ^D3
JUSTIFICATION LEFT
LET XNEW = X2 + DELX4
LET YNEW = Y2 + DELY4
MOVE XNEW YNEW
TEXT ^D4
JUSTIFICATION RIGHT
LET XNEW = X3 + DELX5
LET YNEW = Y3 + DELY5
MOVE XNEW YNEW
TEXT ^D5
JUSTIFICATION LEFT
LET XNEW = X4 + DELX6
LET YNEW = Y3 + DELY6
MOVE XNEW YNEW
TEXT ^D6
JUSTIFICATION RIGHT
LET XNEW = X3 + DELX7
LET YNEW = Y4 + DELY7
MOVE XNEW YNEW
TEXT ^D7
JUSTIFICATION LEFT
LET XNEW = X4 + DELX8
LET YNEW = Y4 + DELY8
MOVE XNEW YNEW
TEXT ^D8
.
. ---------------------------------
. STEP 8--
. WRITE OUT THE VARIOUS TITLES
.
JUSTIFICATION CENTER
.
MOVE 50 92
LET WT = HT/2
HEIGHT HT
WIDTH WT
TEXT ^T
.
MOVE 50 86
LET WST = HST/2
HEIGHT HST
WIDTH WST
TEXT ^ST
.
MOVE 50 10
LET WBT = HBT/2
HEIGHT HBT
WIDTH WBT
TEXT ^BT
.
MOVE 50 5
LET WSBT = HSBT/2
HEIGHT HSBT
WIDTH WSBT
TEXT ^SBT
.
. CALL LOGO3.DP
.
. ---------------------------------
. STEP 9--
. REVERT TO ORIGINAL SETTINGS
.
SET IO
SET IRD 5
FEEDBACK ON
