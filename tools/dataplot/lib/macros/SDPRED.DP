. THIS IS THE DATAPLOT MACRO FILE     SDPRED.DP
. PURPOSE--CARRY OUT A LEAST SQUARES LINEAR FIT,
.          COMPUTE UPPER AND LOWER 95% SINGLE-X CONFIDENCE LIMITS
.          FOR THE PREDICTED VALUE, AND
.          GENERATE THE PLOT.
.
. INPUT --RESPONSE VARIABLE IN Y
.         INDEPENDENT VARIABLE IN X
. OUTPUT--UPPER LIMIT VALUES IN UPPER
.         LOWER LIMIT VALUES IN LOWER
.
. FEBRUARY 1986.
.
. -----START POINT-----------------------------------
.
FIT Y = A+B*X
.
LET N = NUMBER X
LET TERM1 = 1/N
.
LET XBAR = MEAN X
LET DEL = X-XBAR
LET DEL2 = DEL*DEL
LET SSQ2 = SUM DEL2
LET TERM2 = DEL2/SSQ2
.
LET SDPRED = RESSD*SQRT(TERM1+TERM2)
.
LET NM2 = N-2
LET UPPER = PRED+TPPF(.975,NM2)*SDPRED
LET LOWER = PRED-TPPF(.975,NM2)*SDPRED
.
CHAR X BLANK BLANK BLANK
LINES BLANK SOLID DOT DOT
PLOT Y PRED UPPER LOWER VS X
.
