. THIS IS THE DATAPLOT PROGRAM FILE     PARETO.DP
. PURPOSE--MENU TO GENERATE A PARETO PLOT
. DATE--MAY 18, 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
ERASE
.
SET IO TERMINAL
.
WRITE " "
WRITE "               ***********************"
WRITE "               **   DATAPLOT MENU   **"
WRITE "               **    PARETO PLOT    **"
WRITE "               ***********************"
WRITE " "
.
. STEP 1--
. READ IN THE RESPONSE VARIABLE INFO (UNORDERED OR ORDERED) INFORMATION
.
WRITE " "
WRITE "STEP 1--DEFINE THE RESPONSE VALUES (ORDERED OR UNORDERED)."
WRITE "        NUMBER OF VALUES (N) = ?"
READ PARAMETER N
.
LOOP FOR K = 1 1 N
WRITE "        VALUE ^K = ?"
READ PARAMETER YTEMP^K
LET Y(K) = YTEMP^K
END OF LOOP
SET WRITE FORMAT 6
PRINT Y
.
WRITE " "
WRITE "STEP 2--DEFINE THE IDENTIFIER STRINGS FOR EACH RESPONSE VALUE"
WRITE " "
LOOP FOR K = 1 1 N
WRITE "IDENTIFIER STRING FOR VALUE ^K (Y^K ) = ?"
READ STRING S^K
END OF LOOP
.
. STEP 3--
. SPECIFY PLOT LIMITS
.
WRITE " "
WRITE "STEP 3--SPECIFY PLOT LIMITS."
WRITE "        VERTICAL   AXIS PLOT MINIMUM = ?"
READ PARAMETER YMIN
WRITE "        VERTICAL   AXIS PLOT MAXIMUM = ?"
READ PARAMETER YMAX
WRITE "        HORIZONTAL AXIS PLOT MINIMUM (USUALLY 0) = ?"
READ PARAMETER XMIN
WRITE "        HORIZONTAL AXIS PLOT MAXIMUM (USUALLY N+3) = ?"
READ PARAMETER XMAX
.
. STEP 4--
. DEFINE THE TITLE
.
WRITE " "
WRITE "STEP 4--DEFINE THE TITLE AND LABELS."
WRITE "TITLE (POSITIONED AT TOP OF PAGE) = ?"
READ STRING T
WRITE "LEFT   VERTICAL   AXIS LABEL      = ?"
READ STRING Y1L
WRITE "FIRST  HORIZONTAL AXIS LABEL      = ?"
READ STRING X1L
WRITE "SECOND HORIZONTAL AXIS LABEL      = ?"
READ STRING X2L
WRITE "THIRD  HORIZONTAL AXIS LABEL      = ?"
READ STRING X3L
WRITE "LEGEND 1 (WITHIN THE PLOT) = ?"
READ STRING L1
WRITE "LEGEND 2 (WITHIN THE PLOT) = ?"
READ STRING L2
WRITE "LEGEND 3 (WITHIN THE PLOT) = ?"
READ STRING L3
WRITE "LEGEND 4 (WITHIN THE PLOT) = ?"
READ STRING L4
WRITE "LEGEND 5 (WITHIN THE PLOT) = ?"
READ STRING L5
.
WRITE " "
WRITE "CHARACTER SIZE FOR THE IDENTIFIER STRINGS = ?"
WRITE "(A REASONABLE CHOICE IS 1.5)"
READ PARAMETER HIS
WRITE "CHARACTER SIZE OF LEGENDS = ?"
WRITE "(A REASONABLE CHOICE IS 1.0)"
READ STRING HL
WRITE "CHARACTER SIZE FOR BOTTOM 2 HORIZONTAL LABELS = ?"
WRITE "(A REASONABLE CHOICE IS 1.0)
READ STRING HX23
.
WRITE " "
WRITE "FONT = ?"
READ STRING F
.
. STEP 5--
. GENERATE A MESSAGE AND A PAUSE
.
WRITE "NOTE--AFTER THE FIRST PARETO PLOT HAS BEEN GENERATED,"
WRITE "      SUBSEQUENT PARETO PLOTS MAY BE REGENERATED"
WRITE "      BY HITTING / (THAT IS, A FORWARD SLASH)."
WRITE "NOTE--TO GENERATE THE FIRST PARETO PLOT,"
WRITE "      HIT CARRIAGE RETURN."
PAUSE
WRITE " "
WRITE "THE PARETO PLOT IS NOW BEING FORMED. . ."
.
. STEP 6--
. GENERATE THE PLOT
.
LET X = 1 1 N
LET X2 = X
LET Y2 = -Y
LET Y2 = SORTC Y2 X2
LET Y2 = -Y2
.
LINES
CHARACTERS
BAR ON
BAR WIDTH .5
BAR DIMENSION 3
BAR FILL ONTS
X1TICS OFF
X1TIC LABELS OFF
.
LET YOFFSET = 2
LET XOFFSET = 1
LET ANGLE = 45
.
FONT ^F
TITLE ^T
Y1LABEL ^Y1L
X1LABEL ^X1L
X2LABEL ^X2L
X3LABEL ^X3L
X2LABEL SIZE ^HX23
X3LABEL SIZE ^HX23
LEGEND 1 ^L1
LEGEND 2 ^L2
LEGEND 3 ^L3
LEGEND 4 ^L4
LEGEND 5 ^L5
LEGEND 1 COORDINATES 60 86
LEGEND 2 COORDINATES 60 81
LEGEND 3 COORDINATES 60 76
LEGEND 4 COORDINATES 60 71
LEGEND 5 COORDINATES 60 66
LEGEND SIZE ^HL
.
XLIMITS XMIN XMAX
YLIMITS YMIN YMAX
DEGREES
ANGLE ^ANGLE
PLOT Y2 X
LET WIS = HIS/2
HEIGHT HIS
WIDTH WIS
LOOP FOR K = 1 1 N
LET YK = Y2(K)
LET XK = X(K)
LET PY = 100*(YK-YMIN)/(YMAX-YMIN)
LET PX = 100*(XK-XMIN)/(XMAX-XMIN)
LET PY2 = 20+((90-20)/(100-0))*PY
LET PX2 = 15+((85-15)/(100-0))*PX
LET PY3 = PY2+YOFFSET
LET PX3 = PX2+XOFFSET
MOVE PX3 PY3
LET K2 = X2(K)
TEXT ^S^K2
END OF LOOP
RADIANS
ANGLE 0
SAVE JUNK.DPM 20 TO 1
.
CALL [FILLIB.DATA]LOGO3.DP
.
SET IRD 5
SET IO
