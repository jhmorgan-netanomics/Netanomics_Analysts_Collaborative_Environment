. THIS IS THE DATAPLOT MACRO FILE     DEXSCAT1.DP
. PURPOSE--GENERATE A MULTIPLOT OF SCATTER PLOTS
.          THIS MACRO GENERATES SCATTER PLOTS
.          FOR MAIN EFFECTS ONLY.
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y = THE RESPONSE VARIABLE
.       2. X1 X2 X3 X4 ETC. = FACTORS
.       3. NUMFAC = THE NUMBER OF FACTORS
. NOTE--THE USER SHOULD PRE-SET YLIMITS
.       FOR MEAN PLOT, YLIMITS SHOULD BE A BIT MORE
.       THAN DATA YMIN TO YMAX
. EXAMPLE--SKIP 25
.          READ BOXBIKE2.DAT Y X1 TO X7
.          LET NUMFAC = 7
.          YLIMITS 50 100
.          CALL DEXSCAT1.DP
.
. -----START POINT--------------------
.
FEEDBACK OFF
.
. ------------------------------------
. STEP 1--
. SET UP SPECIAL PLOT FORMAT
.
XLIMITS -2 2
CHAR X BLANK
LINES BLANK DOTTED
TICS OFF
TIC LABELS OFF
FRAME CORNER COORDINATES 0 0 100 100
.
. ------------------------------------
. STEP 2--
. GENERATE THE PLOTS
.
LET NC = INT(SQRT(NUMFAC-1))+1
LET NR = 1
LET NR = 2 IF NUMFAC >= 3
LET NR = 3 IF NUMFAC >= 7
LET NR = INT(NUMFAC/NC)+1 IF NUMFAC >= 11
MULTIPLOT NR NC
.
LOOP FOR K = 1 1 NUMFAC
LET PROD = X^K
PLOT Y PROD
END OF LOOP
.
. ------------------------------------
. STEP 3--
. REVERT BACK TO THE ORIGINAL PLOT FORMAT
.
XLIMITS
CHAR BLANK ALL
LINES SOLID ALL
Y1TICS ON
X1TICS ON
Y1TIC LABELS ON
X1TIC LABELS ON
FRAME CORNER COORDINATES
LEGEND 1
MULTIPLOT OFF
