. This is Dataplot macro   OC_NP.DP
. Purpose--Generate an Operating Characteristic curve for a
.          C (proportions) Control Chart.
.          The OC is defined as:
.            POICDF(int(ucl),c) - POICDF(int(lcl),c) versus c
.          where 
.            cbar   = total number of defectives/number of groups
.            ucl    = upper control limit
.            lcl    = lower control limit
.            c      = value of Poisson parameter
.
.          It is essentiallty a plot of the type II (or Beta error)
.          (probability of incorrectly accepting the hypothesis of
.          statistical control).  It measures the sensitivity of the
.          control chart.
. Input --Y        = vector containing number of defectives for each
.                    group
.       --CSTART   = start value for c
.       --CINC     = increment value for c
.       --CSTOP    = stop value for c
. Output--graph
.       --N        = Total sample size
.       --CBAR     = average number of defectives per group
.       --UCL      = CBAR + 3*SQRT(cbar)
.       --LCL      = CBAR - 3*SQRT(cbar)
.
delete zzzzc zzzzy
.
let numdef = sum y
let ngroups = size y
let cbar = numdef/ngroups
let ucl = cbar + 3*sqrt(cbar)
let lcl = cbar - 3*sqrt(cbar)
let lcl = max(0,lcl)
.
let cstartt = 0.1
let cinct = 0.1
let cstopt = 50
if cstart exists
  let cstartt = cstart
end of if
if cinc exists
  let cinct = cinc
end of if
if cstop exists
  let cstopt = cstop
end of if
.
let zzzzc = sequence cstartt cinct cstopt
let zzzza1 = int(ucl)
let zzzza2 = int(lcl)
.
let zzzzy = poicdf(zzzza1,zzzzc) - poicdf(zzzza2,zzzzc)
.
y1label Beta
x1label Poisson Parameter Value
title OC Curve for C Control Chart
.
plot zzzzy vs zzzzc
.
