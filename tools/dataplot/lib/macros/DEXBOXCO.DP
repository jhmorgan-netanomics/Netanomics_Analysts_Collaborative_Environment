. THIS IS DATAPLOT MACRO DEXBOXCO.DP
. PURPOSE--GENERATE YATES ANALYSIS EFFECTS PLOT
.          FOR VARIOUS MEMBERS OF THE BOX-COX TRANSFORMATIONAL
.          FAMILY FOR Y
. ORIGINAL VERSION--DECEMBER 1992
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y = THE RESPONSE VARIABLE (IN YATES ORDER)
. EXAMPLE--SKIP 25
.          READ OHM.DAT Y
.          CALL DEXBOXCO.DP
.
. -----START POINT--------------------
.
FEEDBACK OFF
.
. ------------------------------------
.
. ASSUMPTION--RUNNING A 2-LEVEL FULL FACTORIAL DESIGN
.
LET N = NUMBER Y
LET NM1 = N-1
.
IF N = 4
   LET NUMFAC = 2
END IF
IF N = 8
   LET NUMFAC = 3
END IF
.
IF NUMFAC = 2
   LET XZ1 = DATA -1 1 -1 1
   LET XZ2 = DATA -1 -1 1 1
   LET XZ3 = XZ1*XZ2
   LINES SO SO DO
END IF
IF NUMFAC = 3
   LET XZ1 = DATA -1 1 -1 1 -1 1 -1 1
   LET XZ2 = DATA -1 -1 1 1 -1 -1 1 1
   LET XZ3 = DATA -1 -1 -1 -1 1 1 1 1
   LET XZ4 = XZ1*XZ2
   LET XZ5 = XZ1*XZ3
   LET XZ6 = XZ2*XZ3
   LET XZ7 = XZ1*XZ2*XZ3
   LINES SO SO SO DO DO DO DA2
END IF
.
LET K = 0
LOOP FOR LAMBDA = -2 .2 2.05
.
LOOP FOR C = 1 1 NM1
IF LAMBDA = 0
   LET Y2Z = LOG(Y)
END IF
IF LAMBDA <> 0
   LET Y2Z = ((Y**LAMBDA)-1)/LAMBDA
END IF
LET PRODZ = Y2Z*XZ^C
LET K = K+1
LET COEFZ(C) = MEAN PRODZ
LET VERTZ(K) = MEAN PRODZ
LET TAGZ(K) = C
LET HORZ(K) = LAMBDA
END LOOP
.
LET COEF2Z = ABS(COEFZ)
LET MAXCOEF2 = MAXIMUM COEF2Z
LET IMIN = K-(NM1)+1
LET IMAX = K
LET VERTZ = ABS(VERTZ/MAXCOEF2) FOR I = IMIN 1 IMAX
.
END LOOP
.
PLOT VERTZ HORZ TAGZ
.
DELETE Y2Z COEFZ COEF2Z VERTZ HORZ TAGZ
DELETE PRODZ
DELETE XZ1 XZ2 XZ3
IF NUMFAC = 3
   DELETE XZ4 XZ5 XZ6 XZ7
END IF
.
FEEDBACK ON
