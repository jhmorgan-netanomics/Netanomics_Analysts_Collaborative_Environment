.
. Compute Bartlett's test
. for the data in the variables Y and X.
. Y and X must be created prior to running this macro.
.
.  CALCULATE SIZE, NUMBER OF GROUPS
FEEDBACK OFF
LET N = SIZE Y
LET XDIST = DISTINCT X
LET NGROUP = SIZE XDIST
.  CALCULATE INDIVIDUAL VARIANCES
LOOP FOR J = 1 1 NGROUP
    LET TEMP = XDIST(J)
    LET NI(J) = SIZE X SUBSET X = TEMP
    LET V(J) = VARIANCE Y SUBSET X = TEMP
END OF LOOP
. CALCULATE POOLED VARIANCE
LET TEMP2 = (NI-1)*V
LET VP = SUM TEMP2
LET VP = VP/(N-NGROUP)
. CALCULATE BARLETT'S STATISTIC
LET TEMP3 = (NI-1)*LOG10(V)
LET TEMP4 = SUM TEMP3
LET Q = (N-NGROUP)*LOG10(VP) - TEMP4
LET TEMP3 = (1/NI)
LET TEMP4 = SUM TEMP3
LET H = 1 + (TEMP4 - (1/(N-NGROUP)))/(3*(NGROUP-1))
LET B = 2.3026*Q/H
LET ALPHA = 0.95 
LET TEMP4 = NGROUP-1
LET CRITICAL = CHSPPF(ALPHA,TEMP4)
PRINT " "
PRINT "BARTLET's STATISTIC = ^B"
PRINT "CHI-SQUARE CRITICAL VALUE = ^CRITICAL"
.
IF B < CRITICAL
  PRINT "DO NOT REJECT THE NULL HYPOTHESIS (I.E., VARIANCES ARE EQUAL)"
END IF
IF B > CRITICAL
  PRINT "REJECT THE NULL HYPOTHESIS (I.E., VARIANCES ARE NOT EQUAL)"
END IF
