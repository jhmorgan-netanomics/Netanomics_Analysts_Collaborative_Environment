. THIS IS DATAPLOT MACRO    CONFLIMD.DP
. PURPOSE--COMPUTE 2-SIDED CONFIDENCE LIMITS FOR DIFFERENCE OF MEANS
. INPUT--DATA RESIDES IN VARIABLE Y1 AND Y2
. OUTPUT--2-SIDED 95% CONFIDENCE LIMITS FOR DIFFERENCE DEL
.         2-SIDED 99% CONFIDENCE LIMITS FOR DIFFERENCE DEL
.
LET N1 = NUMBER Y1
LET N2 = NUMBER Y2
LET NPOOL = (N1-1) + (N2-1)
.
LET YBAR1 = MEAN Y1
LET YBAR2 = MEAN Y2
.
LET S1 = STANDARD DEVIATION Y1
LET S2 = STANDARD DEVIATION Y2
LET SPOOL = SQRT(( (N1-1)*S1**2 + (N2-1)*S2**2 ) / NPOOL )
.
LET DEL = YBAR1-YBAR2
LET SDDEL = SPOOL * SQRT( (1/N1) + (1/N2) )
LET LOWER95 = DEL - TPPF(.975,NPOOL) * SDDEL
LET UPPER95 = DEL + TPPF(.975,NPOOL) * SDDEL
.
LET LOWER99 = DEL - TPPF(.995,NPOOL) * SDDEL
LET UPPER99 = DEL + TPPF(.995,NPOOL) * SDDEL
.
WRITE " "
WRITE "2-SIDED CONFIDENCE LIMITS FOR MU1-MU2"
WRITE LOWER95 UPPER95 LOWER99 UPPER99
