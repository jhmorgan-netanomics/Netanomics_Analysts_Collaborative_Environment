. This is Dataplot macro   E691BPK1.DP
. Purpose--Generate some prelimanry graphs for an interlab analysis
.          based on the ASTM E691 standard.
.
.          The following plot is generatred:
.          1) Box plot of k-consistency statistic: Laboratories
.             within Materials
.
. Input --Y        = response variable
.       --LABID    = lab ID variable
.       --MATID    = material ID variable
.       --E691TIT  = string containing title for analysis
.
.  Step 1: Determine number of materials
.
feedback save
feedback off
let nmat = unique matid
let nlab = unique labid
let ntot = nmat*nlab
let matcode = code matid
let labcode = code labid
let ky klab kmat = k consistency statistic y labcode matcode
.
.  Step 2: Set plot control features
.
label case asis
title case asis
label
y1label k - Consistency Statistic
x1label Material
title ^e691tit: k - Laboratories within Materials
character box plot
line box plot
fences on
let plot character hw 21 = 1.5 1.125
let plot character hw 22 = 1.0 0.75
let plot character hw 23 = 1.0 0.75
let plot character hw 24 = 1.5 1.125
let plot character fill 21 = on
let plot character fill 22 = on
let plot character fill 23 = on
let plot character fill 24 = on
.
xlimtis 1 nmat
major xtic mark number nmat
minor xtic mark number 0
tic offset units screen
tic offset 5 5
title offset 2
.
.  Step 3: Generate the plot
.
box plot ky kmat
.
reset plot control
delete ky klab kmat
feedback restore
.
