. THIS IS THE DATAPLOT PROGRAM FILE  COMP2EXP.DP
. PURPOSE - COMPARE 2 EXPONENTIAL DISTRIBUTIONS (WITH CENSORING)
. DATE JUNE 1998.
.
. ------------------ START POINT  -------------------
FEEDBACK OFF
SET IO TERMINAL
WRITE " "
WRITE "NAME OF FIRST VARIABLE OF FAILURE TIMES:"
READ STRING FAIL1
WRITE "NAME OF SECOND VARIABLE OF FAILURE TIMES:"
READ STRING FAIL2
WRITE " "
WRITE " "
WRITE "FOR CENSORED DATA, YOU CAN ALSO ENTER THE NAME OF"
WRITE "OF A TAG VARIABLE WHERE 1 = FAILURE TIME, 0 = CENSOR TIME"
WRITE " "
LET ICENSOR = 0
WRITE "DO YOU HAVE TAG VARIABLES (0 FOR NO, 1 FOR YES):"
READ PARAMETER ICENSOR
IF ICENSOR = 1
  WRITE "NAME OF FIRST TAG VARIABLE:"
  READ STRING FTAG1
  WRITE "NAME OF SECOND TAG VARIABLE (ENTER 0 TO OMIT):"
  READ STRING FTAG2
END OF IF
WRITE " "
WRITE "INPUT YOUR ALPHA "
READ PARAMETER ALPHA
IF ALPHA > 1
  LET ALPHA = ALPHA/100
END OF IF
IF ALPHA <= 0
  LET ALPHA = .05
END OF IF
IF ALPHA > 1
   LET ALPHA = .05
END OF IF
.
SET IO
.
.  FULL SAMPLE CASE
.
IF ICENSOR = 0
  LET R1 = SIZE ^FAIL1
  LET R2 = SIZE ^FAIL2
  LET A1 = MEAN ^FAIL1
  LET A2 = MEAN ^FAIL2
END OF IF
.
.  CENSORED CASE
.
IF ICENSOR = 1
  LET N1 = SIZE ^FAIL1
  LET R1 = SIZE ^FAIL1 SUBSET ^FTAG1 = 1
  LET N2 = SIZE ^FAIL2
  LET R2 = SIZE ^FAIL2 SUBSET ^FTAG2 = 1
  LET A1 = SUM ^FAIL1 SUBSET ^FTAG1 = 1
  LET A3 = MAXIMUM ^FAIL1 SUBSET ^FTAG1 = 1
  LET A1 = (A1 + (N1-R1)*A3)/R1
  LET A2 = SUM ^FAIL2 SUBSET ^FTAG2 = 1
  LET A4 = MAXIMUM ^FAIL2 SUBSET ^FTAG2 = 1
  LET A2 = (A2 + (N2-R2)*A4)/R2
END OF IF
.
LET FRATIO = A1/A2
LET V1 = 2*R1
LET V2 = 2*R1
LET PVAL = FCDF(FRATIO,V1,V2)
LET ALPLOW=ALPHA/2
LET ALPHIGH=1 - ALPLOW
LET PLOW = FPPF(ALPLOW,V1,V2)
LET PHIGH = FPPF(ALPHIGH,V1,V2)
LET PLOW = PLOW/FRATIO
LET PHIGH = PHIGH/FRATIO
.
WRITE " "
WRITE " "
WRITE "H0: EQUAL MEANS (AT THE ^ALPHA % LEVEL)"
WRITE "    THETA 1   = ^A1"
WRITE "    THETA 2   = ^A2"
WRITE "    F RATIO   = ^FRATIO"
WRITE "    P VALUE   = ^PVAL"
LET IACCEPT = 0
IF PVAL < ALPLOW
  LET IACCEPT = 1
END OF IF
IF PVAL > ALPHIGH
  LET IACCEPT = 1
END OF IF
IF IACCEPT = 0
    WRITE "    ACCEPT AT THE ^ALPHA % LEVEL"
END OF IF
IF IACCEPT = 1
    WRITE "    REJECT AT THE ^ALPHA % LEVEL"
END OF IF
WRITE " "
WRITE "    TWO-SIDED ^ALPHA% CONFIDENCE INTERVAL: (^PLOW,^PHIGH)"
WRITE " "
.
FEEDBACK ON
