. THIS IS THE DATAPLOT MACRO FILE     DEXCONT2.DP
. PURPOSE--GENERATE A DEX CONTOUR PLOT FOR 2-LEVEL DESIGNS
.          THIS MACRO GENERATES A CONTOUR PLOT
.          OF Y VERSUS U1 AND U2
.          AT ISOLINES AS GIVEN IN YCONT.
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y  = THE RESPONSE VARIABLE
.       2. U1 = DESIRED HORIZONTAL AXIS FACTOR (AS -1 AND +1)
.       3. U2 = DESIRED VERTICAL AXIS FACTOR (AS -1 AND +1)
.       4. YCONT = DESIRED CONTOUR LINE VALUES
.       5. QUAL = DESIRED SUBSET/EXCEPT/FOR QUALIFICATION (OPTIONAL)
.                 (IN PRACTICE, QUAL IS USUALLY LEFT UNDEFINED)
. NOTE--THE CONTOUR PLOT WILL RANGE FOR -2 TO 2
.       IN BOTH U1 AND U2 DIRECTIONS.
. NOTE--THE INPUT U1 AND U2 VECTORS USUALLY CONSIST
.       OF ONLY -1 AND +1 VALUES, BUT MAY IN FACT
.       HAVE OTHER VALUES OTHER THAN -1 AND +1
.       (E.G., CENTER POINT VALUES OF (0,0)).
.       NOTE HOWEVER THAT ANY U1 AND U2 VALUES OTHER
.       THANT -1 AND +1 WILL    NOT    BE USED IN THE COMPUTATION
.       OF THE MODEL COEFFICIENTS
.       AND THE SUBSEQUENT CONTOUR LINES.
. UPDATED--JUNE 1995
. NOTE--THIS MACRO CREATES SEVERAL INTERMEDIATE VARIABLES.
.       A COMMON CAUSE FOR THIS MACRO TO FAIL IS THAT THE USER
.       HAS AN INTERNAL DATAPLOT WORKSHEET THAT IS DIMENSIONED
.       (MAX NUMBER OF VARIABLES)
.       TOO SMALL (FOR EXAMPLE, THE DEFAULT DIMENSION
.       ON SOME COMPUTERS MAY ALLOW A MAX OF 10 VARIABLES ONLY).
.       TO ADJUST FOR THIS, THE ANALYST MAY "MANUALLY"
.       INCREASE THE MAX NUMBER OF VARIABLES
.       VIA THE DIMENSION STATEMENT, AS IN:
.          DIMENSION 50 VARIABLES
. EXAMPLE 1--WITH NO DATA SUBSETTING/QUALIFICATION:
.          DIMENSION 50 VARIABLES
.          SKIP 25
.          READ BOXYIELD.DAT Y X1 X2
.          LET U1 = X1
.          LET U2 = X2
.          LET YCONT = SEQUENCE 50 2 70
.          CALL DEXCONT2.DP
. EXAMPLE 2--WITH    DATA SUBSETTING/QUALIFICATION:
.          DIMENSION 50 VARIABLES
.          SKIP 25
.          READ BOXYIELD.DAT Y X1 X2
.          LET U1 = X1
.          LET U2 = X2
.          LET YCONT = SEQUENCE 50 2 70
.          LET STRING QUAL = FOR I = 1 1 4
.          CALL DEXCONT2.DP
.
. -----START POINT-------------------
.
FEEDBACK OFF
.
MULTIPLOT FREEZE
.
LIMITS -2 2
TIC OFFSET UNITS SCREEN
TIC OFFSET 0 0
.
. STEP 1--EXTRACT ONLY THE CORNER POINTS OF THE DESIGN
.         (IGNORE CENTER POINTS, FOR EXAMPLE)
.
LET YPRIME = Y
LET U1PRIME = U1
LET U2PRIME = U2
RETAIN YPRIME U1PRIME U2PRIME ^QUAL
.
LET YPRIM2 = YPRIME
LET U1PRIM2 = U1PRIME
LET U2PRIM2 = U2PRIME
RETAIN YPRIM2 U1PRIM2 U2PRIM2 SUBSET U1 -1 +1 SUBSET U2 -1 +1
.
. STEP 2--COMPUTE COEFFICIENTS FROM THE CORNER POINTS
.
LET MU = MEAN YPRIM2
LET JUNKVAR = YPRIM2*U1PRIM2
LET JUNKSCAL = MEAN JUNKVAR
LET B1 = 2*JUNKSCAL
LET JUNKVAR = YPRIM2*U2PRIM2
LET JUNKSCAL = MEAN JUNKVAR
LET B2 = 2*JUNKSCAL
LET JUNKVAR = YPRIM2*U1PRIM2*U2PRIM2
LET JUNKSCAL = MEAN JUNKVAR
LET B12 = 2*JUNKSCAL
.
. STEP 3--GENERATE THE CONTOUR PLOT
.
. LET V1 = SEQUENCE -2 .4 2 FOR I = 1 1 121
. LET V2 = SEQUENCE -2 11 .4 2
. LET YGRID = MU+0.5*(B1*V1+B2*V2+B12*V1*V2)
. CONTOUR PLOT YGRID V1 V2 YCONT
.
LET U1JUNK = SEQUENCE -2 .05 2
LET FUNCTION FJUNK = (2*(Y0-MU)-B1*U1JUNK)/(B2+B12*U1JUNK)
LET NYCONT = NUMBER YCONT
LOOP FOR K = 1 1 NYCONT
   LET Y0 = YCONT(K)
   LET Y1JUNK = FJUNK
   PLOT Y1JUNK U1JUNK
   PRE-ERASE OFF
END OF LOOP
. DELETE U1JUNK Y1JUNK
.
. STEP 4--DRAW THE INNER SAMPLING SQUARE
.         (AND A CENTER POINT, IF EXISTENT)
.
PRE-ERASE OFF
LINES DOTTED
CHARACTER CIRCLE
CHARACTER HW 1.5 1.2
CHARACTER FILL ON
PRE-SORT OFF
LET W1 = DATA -1 1 1 -1 -1
LET W2 = DATA -1 -1 1 1 -1
PLOT W2 W1
DELETE W1 W2
.
LET NCENTER = NUMBER YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
IF NCENTER > 0
   LET W3 = DATA 0 0
   PLOT W3 W3
   DELETE W3
END IF
.
. STEP 5--WRITE THE AVERAGES NEAR THE VERTICES
.         AND THE AVERAGE NEAR THE CENTER POINT, IF EXISTENT)
.
LET YMM = MEAN YPRIM2 SUBSET U1PRIM2 -1 SUBSET U2PRIM2 -1
LET YPM = MEAN YPRIM2 SUBSET U1PRIM2 1 SUBSET U2PRIM2 -1
LET YMP = MEAN YPRIM2 SUBSET U1PRIM2 -1 SUBSET U2PRIM2 1
LET YPP = MEAN YPRIM2 SUBSET U1PRIM2 1 SUBSET U2PRIM2 1
JUST RIGHT; MOVEDATA -1.1 -1.2; TEXT ^YMM
JUST LEFT; MOVEDATA +1.1 -1.2; TEXT ^YPM
JUST RIGHT; MOVEDATA -1.1 +1.1; TEXT ^YMP
JUST LEFT; MOVEDATA +1.1 +1.1; TEXT ^YPP
.
IF NCENTER > 0
   LET YCP = MEAN YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   JUST LEFT; MOVEDATA +0.1 +0.1; TEXT ^YCP
END IF
.
. STEP 6--CHECK FOR REPLICATED CENTER POINTS.
.         IF HAVE REPLICATED CENTER POINTS,
.         THEN DO CURVATURE CHECK VIA T TEST
.
IF NCENTER > 1.5
   LET MCENTER = MEAN YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   LET SDCENTER = SD YPRIME SUBSET U1PRIME 0 SUBSET U2PRIME 0
   LET NEDGE = NUMBER YPRIME SUBSET U1PRIME -1 +1 SUBSET U2PRIME -1 +1
   LET MEDGE = MEAN YPRIME SUBSET U1PRIME -1 +1 SUBSET U2PRIME -1 +1
   LET STATNUM = MEDGE-MCENTER
   LET STATDEN = SDCENTER*SQRT((1/NEDGE)+(1/NCENTER))
   LET TESTSTAT = STATNUM/STATDEN
   LET NDF = NCENTER-1
   LET CUTOFF2 = TPPF(.975,NDF)
   LET CUTOFF1 = -CUTOFF2
.
   LET STRING CONCLUSION = NO CURVATURE
   IF TESTSTAT < CUTOFF1
      LET STRING CONCLUSION = CURVATURE
   END IF
   IF TESTSTAT > CUTOFF2
      LET STRING CONCLUSION = CURVATURE
   END IF
END IF
.
IF NCENTER > 1.5
   JUSTIFICATION CENTER
   MOVE 50 11
   TEXT Center: Num. Obs. = ^NCENTER  Average = ^MCENTER  SD = ^SDCENTER
   MOVE 50 8
   TEXT Edge: Num. Obs. = ^NEDGE   Average = ^MEDGE
   MOVE 50 5
   TEXT Curvature Check:  t Test Stat Value = ^TESTSTAT
   MOVE 50 2
   TEXT t^NDF Lower Cutoff = ^CUTOFF1   t^NDF Upper Cutoff = ^CUTOFF2
END IF
.
. STEP 7--CLEAN UP
.
MULTIPLOT UNFREEZE
.
LIMITS
TIC OFFSET UNITS SCREEN
TIC OFFSET UNITS 5 5
PRE-ERASE ON
LINES SOLID ALL
CHARACTERS BLANK ALL
CHARACTER FILL OFF
PRE-SORT ON
.
FEEDBACK ON
