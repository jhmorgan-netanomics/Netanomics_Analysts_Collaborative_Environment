. THIS IS THE DATAPLOT PROGRAM FILE     HOTELLING.DP
. PURPOSE--CARRY OUT A LEAST SQUARES LINEAR FIT,
.          COMPUTE UPPER AND LOWER 95% (SIMULTANEOUS)
.          HOTELLING CONFIDENCE LIMITS
.          FOR THE PREDICTED VALUES, AND
.          GENERATE THE PLOT.
.
. INPUT --RESPONSE VARIABLE IN Y
.         INDEPENDENT VARIABLE IN X
. OUTPUT--UPPER LIMIT VALUES IN UPPER
.         LOWER LIMIT VALUES IN LOWER
.
. MARCH 1988
.
. -----START POINT-----------------------------------
.
LINEAR FIT Y X
.
LET N = NUMBER X
LET TERM1 = 1/N
.
LET XBAR = MEAN X
LET DEL = X-XBAR
LET DEL2 = DEL*DEL
LET SSQ2 = SUM DEL2
LET TERM2 = DEL2/SSQ2
.
LET SDPRED = RESSD*SQRT(TERM1+TERM2)
.
LET NM2 = N-2
LET K = SQRT(2*FCDF(.95,2,NM2))
LET UPPER = PRED+K*SDPRED
LET LOWER = PRED-K*SDPRED
.
CHAR X BLANK BLANK BLANK
LINES BLANK SOLID DOT DOT
PLOT Y PRED UPPER LOWER VS X
.
