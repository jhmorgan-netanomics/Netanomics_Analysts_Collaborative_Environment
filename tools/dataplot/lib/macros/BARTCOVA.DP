FEEDBACK OFF
.
.  Macro to perform a Bartlett's test for equal covariance matrices
.
.  To run this macro:
.
.  1) Define the data matrix       ZX
.  2) Define the group id variable ZTAG
.  3) Enter:  CALL BARTCOVA.DP
.
DELETE SI SITEMP GROUPID
LET NTOT = MATRIX NUMBER OF ROWS ZX
LET P = MATRIX NUMBER OF COLUMNS ZX
DIMENSION MATRIX ROWS NTOT
.
LET DIAG = 0 FOR I = 1 1 P
LET SI = DIAGONAL MATRIX DIAG
LET GROUPID = DISTINCT ZTAG
LET NGROUP = SIZE GROUPID
LET ZZZZSUM = 0
.
LOOP FOR K = 1 1 NGROUP
   LET ICURR = GROUPID(K)
   LET SITEMP = VARIANCE COVARIANCE MATRIX ZX SUBSET ZTAG = ICURR
   LET N^K = SIZE ZTAG SUBSET ZTAG = ICURR
   LET ZZZZJUNK = MATRIX DETERMINANT SITEMP
   LET ZZZZSUM = ZZZZSUM + LN(ZZZZJUNK)
   LET SITEMP = MATRIX MULTIPLICATION SITEMP N^K
   LET SI = MATRIX ADDITION SI SITEMP
END OF LOOP
LET NTEMP = 1/NTOT
LET SI = MATRIX MULTIPLICATION SI NTEMP
LET ZZZZJUNK = MATRIX DETERMINANT SI
LET ZZZZBART = NTOT*ZZZZJUNK - ZZZZSUM
LET ZZZZJUN1 = 2*P*P + 3*P - 1
LET ZZZZJUN2 = 6*(P+1)*(NGROUP-1)
LET ZZZZSUM = 0
LOOP FOR K = 1 1 NGROUP
    LET ZZZZSUM = ZZZZSUM + (1/N^K)
END OF LOOP
LET C = 1 - (ZZZZJUN1/ZZZZJUN2)*(ZZZZSUM - 1/NTOT)
LET ALPHA = 0.95
LET ZZZZDF = 0.5*(NGROUP-1)*P*(P+1)
LET ZZZZCRIT = CHSPPF(ALPHA,ZZZZDF)
LET STRING ZZZZSTR = ACCEPTED
IF ZZZZBART > ZZZZCRIT
  LET STRING ZZZZSTR = REJECTED
END OF IF
.
CAPTURE ZZZZJUNK.DAT
PRINT " "
PRINT "BARTLETT's TEST FOR EQUAL COVARIANCE MATRICES"
PRINT " "
PRINT "VALUE OF BARTLETT TEST STATISTIC            = ^ZZZZBART"
PRINT " "
PRINT "ALPHA                                       = 0.05"
PRINT "CHI-SQUARE DEGREES OF FREEDOM               = ^ZZZZDF"
PRINT "CHI-SQUARE CRITICAL VALUE (ALPHA = 0.05)    = ^ZZZZCRIT"
PRINT " "
PRINT "HYPOTHESIS OF EQUAL COVARIANCE MATRICES IS  = ^ZZZZSTR"
END OF CAPTURE
FEEDBACK ON
LIST ZZZZJUNK.DAT
