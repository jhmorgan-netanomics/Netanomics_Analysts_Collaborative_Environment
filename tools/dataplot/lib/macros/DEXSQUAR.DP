. THIS IS THE DATAPLOT MENU MACRO FILE     DEXSQUAR.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN
.          WILL GENERATE A SQUARE REPRESENTATION
.          FOR A 2**2 FACTORIAL DESIGN.
.          (THIS IS A COMMONLY-USED SUMMARY
.          PLOT FOR THE PROJECTION
.          FROM K-SPACE TO 2-SPACE
.          OF THE 2 MOST IMPORTANT FACTORS
.          OUT OF K FACTORS).
. DATE--JULY 1989
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
SET IO TERMINAL
.
DEFINE ER ESC FF
ER
.
WRITE " "
WRITE "               ****************************"
WRITE "               **    DATAPLOT MENU--     **"
WRITE "               **    SQUARE DIAGRAM FOR  **"
WRITE "               **  2**2 FACTORIAL DESIGN **"
WRITE "               ****************************"
WRITE " "
.
WRITE " "
WRITE "----------------------------------------"
WRITE " "
WRITE "DO YOU WISH A    DEVICE 2 POSTSCRIPT    COMMAND EXECUTED NOW?"
WRITE "ENTER 0 IF NO     ENTER 1 IF YES"
READ PARAMETER POSTYN
IF POSTYN = 1
DEVICE 2 POSTSCRIPT
END OF IF
WRITE " "
.
. ---------------------------------
. STEP 1--
. READ IN USER SPECIFICATIONS
.
WRITE " "
WRITE "NAME FOR VARIABLE 1 (FRONT, BOTTOM) = ?"
READ STRING A1
WRITE "NAME FOR VARIABLE 2 (LEFT, UP)      = ?"
READ STRING A2
.
WRITE " "
WRITE "DATA POINT 1 (BOTTOM, FRONT, LEFT ) = ?"
READ STRING D1
WRITE "DATA POINT 2 (BOTTOM, FRONT, RIGHT) = ?"
READ STRING D2
WRITE "DATA POINT 3 (TOP   , FRONT, LEFT ) = ?"
READ STRING D3
WRITE "DATA POINT 4 (TOP   , FRONT, RIGHT) = ?"
READ STRING D4
.
WRITE " "
WRITE "TITLE          (POSITIONED AT TOP OF PAGE) = ?"
READ STRING T
WRITE "SUB-TITLE      (POSITIONED UNDER THE TITLE) = ?"
READ STRING ST
WRITE "BASE TITLE     (POSITIONED 3/4 DOWN PAGE) = ?"
READ STRING BT
WRITE "SUB-BASE TITLE (POSITIONED UNDER BASE TITLE) = ?"
READ STRING SBT
.
WRITE " "
WRITE "CHARACTER SIZE FOR THE DATA = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HD
WRITE "CHARACTER SIZE FOR TITLE = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HT
WRITE "CHARACTER SIZE FOR SUB-TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HST
WRITE "CHARACTER SIZE FOR BASE TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HBT
WRITE "CHARACTER SIZE FOR SUB-BASE TITLE = ?"
WRITE "(1 IS A RECOMMENDED CHOICE)"
READ PARAMETER HSBT
.
WRITE " "
WRITE "FONT = ?"
READ STRING F
.
WRITE " "
WRITE "THE SQUARE IS NOW BEING FORMED--PLEASE WAIT . . ."
.
. ---------------------------------
. STEP 2--
. DEFINE INTERNAL SETTINGS
.
. LET X1=29
LET X1=30
LET Y1=35
.
. LET XDEL=30
LET XDEL=40
. LET YDEL=30
LET YDEL=40
LET X2DEL=12
LET Y2DEL=6
LET CIRCDEL=.4
.
. ---------------------------------
. STEP 3--
. DRAW THE SQUARE
.
LET X2=X1+XDEL
LET Y2=Y1+YDEL
LET X3=X1+X2DEL
LET Y3=Y1+Y2DEL
LET X4=X2+X2DEL
LET Y4=Y2+Y2DEL
.
LET X1M=X1-CIRCDEL
LET X2M=X2-CIRCDEL
LET X3M=X3-CIRCDEL
LET X4M=X4-CIRCDEL
LET Y1M=Y1-CIRCDEL
LET Y2M=Y2-CIRCDEL
LET Y3M=Y3-CIRCDEL
LET Y4M=Y4-CIRCDEL
.
LET X1P=X1+CIRCDEL
LET X2P=X2+CIRCDEL
LET X3P=X3+CIRCDEL
LET X4P=X4+CIRCDEL
LET Y1P=Y1+CIRCDEL
LET Y2P=Y2+CIRCDEL
LET Y3P=Y3+CIRCDEL
LET Y4P=Y4+CIRCDEL
.
ERASE
.
BOX X1 Y1 X2 Y2
.
. ---------------------------------
. STEP 4--
. FILL IN THE CIRCLES AT THE 4 VERTICES
.
REGION FILL ON
CIRCLE X1M Y1M X1P Y1P
CIRCLE X2M Y1M X2P Y1P
CIRCLE X1M Y2M X1P Y2P
CIRCLE X2M Y2M X2P Y2P
REGION FILL OFF
.
. ---------------------------------
. STEP 5--
. DRAW IN THE 2 AXIS ARROWS
.
HW .5 2.5
LET X11 = (2*X1+X2)/3
LET X12 = (X1+2*X2)/3
LET Y11 = Y1 - 3
LET Y12 = Y1 - 3
ARROW X11 Y11 X12 Y12
.
LET X31 = X1 - 3
LET X32 = X1 - 3
LET Y31 = (2*Y1+Y2)/3
LET Y32 = (Y1+2*Y2)/3
ARROW X31 Y31 X32 Y32
.
. ---------------------------------
. STEP 6--
. WRITE IN THE 2 AXIS IDENTIFIERS
.
FONT ^F
JUSTIFICATION CENTER
HW 1.5 1
ANGLE UNITS DEGREES
.
LET X5 = (X1+X2)/2
LET Y5 = Y1-10
MOVE X5 Y5
TEXT ^A1
.
ANGLE 90
LET X7 = X1-8
LET Y7 = (Y1+Y2)/2
MOVE X7 Y7
TEXT ^A2
.
ANGLE 0
.
. ---------------------------------
. STEP 7--
. WRITE IN THE 4 DATA POINTS
. (1 DATA POINT PER VERTEX)
.
. HW 1.25 .8
LET WD = HD/2
HEIGHT HD
WIDTH WD
HW HD WD
.
JUSTIFICATION RIGHT
.
LET DELX1 = -1.5
LET DELY1 = -2.5
LET DELX2 = +1.5
LET DELY2 = -2.5
LET DELX3 = -1.5
LET DELY3 = +2.0
LET DELX4 = +1.5
. LET DELY4 = -1.5
LET DELY4 = +2.0
.
LET XNEW = X1 + DELX1
LET YNEW = Y1 + DELY1
MOVE XNEW YNEW
TEXT ^D1
JUSTIFICATION LEFT
LET XNEW = X2 + DELX2
LET YNEW = Y1 + DELY2
MOVE XNEW YNEW
TEXT ^D2
JUSTIFICATION RIGHT
LET XNEW = X1 + DELX3
LET YNEW = Y2 + DELY3
MOVE XNEW YNEW
TEXT ^D3
JUSTIFICATION LEFT
LET XNEW = X2 + DELX4
LET YNEW = Y2 + DELY4
MOVE XNEW YNEW
TEXT ^D4
.
. ---------------------------------
. STEP 8--
. WRITE OUT THE VARIOUS TITLES
.
JUSTIFICATION CENTER
.
MOVE 50 92
LET WT = HT/2
HEIGHT HT
WIDTH WT
TEXT ^T
.
MOVE 50 86
LET WST = HST/2
HEIGHT HST
WIDTH WST
TEXT ^ST
.
MOVE 50 10
LET WBT = HBT/2
HEIGHT HBT
WIDTH WBT
TEXT ^BT
.
MOVE 50 5
LET WSBT = HSBT/2
HEIGHT HSBT
WIDTH WSBT
TEXT ^SBT
.
. CALL LOGO3.DP
.
. ---------------------------------
. STEP 9--
. REVERT TO ORIGINAL SETTINGS
.
SET IO
SET IRD 5
FEEDBACK ON
