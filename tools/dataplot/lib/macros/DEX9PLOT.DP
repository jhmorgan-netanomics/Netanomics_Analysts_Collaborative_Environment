. This is Dataplot program file    DEX9PLO2.DP
. Purpose--Generate 9 plots essential to the analysis
.       of 2-level factorial designs
. Date--December 2000
.
. Assumptions:
.    1. Data is in Y
.    2. Factors are in X1, X2, X3, ...
.    3. Total number of factors is in K
.
. -----start point-----
.
device 1 x11
device 2 ps
dimension 100 variables
.
skip 25
read SPLETT.DAT Y X1 X2 X3
let k = 3
. read BOXREAC2.DAT Y X1 X2 X3 X4 X5
. let k = 5
let kp1 = k+1
let kp2 = k+2
let n = number y
let index = 1 1 n
.
. -----step 1: ordered data plot-----
.
. -----step 1: ordered data plot-----
.
device 1 off; device 2 off
ytic offset 0 0
plot y
probe fy1min; let mintic = probeval
probe fy1max; let maxtic = probeval
let rangetic = maxtic-mintic
ytic offset 5 5
device 1 on; device 2 on
.
loop for j = 1 1 k
   let z^j = x^j
end loop
if k = 1; let y2 = sortc y z1; end if
if k = 2; let y2 = sortc y z1 z2; end if
if k = 3; let y2 = sortc y z1 z2 z3; end if
if k = 4; let y2 = sortc y z1 z2 z3 z4; end if
if k = 5; let y2 = sortc y z1 z2 z3 z4 z5; end if
if k = 6; let y2 = sortc y z1 z2 z3 z4 z5 z6; end if
if k = 7; let y2 = sortc y z1 z2 z3 z4 z5 z6 z7; end if
char x
lines blank
title Ordered Data Plot
y1label Response Y
x1label Settings
x1tics off
x1tic labels off
xmin -1
let yoffset = 2*k + 2
y1tic offset yoffset 5
plot y2 index
let yplot2 = yplot
let xplot2 = xplot
limits freeze
pre-erase off
probe fy1min; let ybottom = probeval
probe fy1max; let ytop = probeval
let yrange = ytop-ybottom
let yinc = (yrange-rangetic)/(k+3)
let ybase = mintic
loop for j = 1 1 k
   char automatic sign z^j
   let ypos = ybase-(j-.5)*yinc
   let yplot2 = ypos for i = 1 1 n
   plot yplot2 xplot2 xplot2
   let ypos2 = ypos-(yinc/2)
   movedata -1 ypos2
   text X^j:
end loop
pre-erase on
x1tics on
x1tic labels on
limits
y1tic offset 5 5
title
y1label
x1label
.
. -----step 2: dex scatter plot-----
.
. xtic offset 10 10
xtic offset 0 0
. xlimits 1 k
xlimits 0 kp1
. major x1tic number k
major x1tic number kp2
minor x1tic number 0
x1tic label format alphabetic
let string junk = sp() cr()
loop for j = 1 1 k
   let string junk = ^junk X^j
end loop
x1tic label contents ^junk
.
char x blank
lines blank dotted
title Scatter Plot
y1label Response Y
x1label Factors
dex scatter plot y x1 to x^k
.
limits freeze
pre-erase off
char bl all
lines solid all
plot yplot xplot xplot subset tagplot 1 to k
pre-erase on
limits
.
let dxplot = distinct xplot
let dxplot = sort dxplot
let ndxplot = number dxplot
let b = 18
just cece
loop for j = 1 1 ndxplot
   let a = dxplot(j)
   moveds a b
   let evenodd = mod(j,2)
   if evenodd = 1; text -; end if
   if evenodd = 0; text +; end if
end loop
.
limits
xtic offset 5 5
tic label format default
minor xtic number default
major xtic number default
just left
title
y1label
x1label
.
. -----step 3: dex mean plot-----
.
. xtic offset 10 10
xtic offset 0 0
. xlimits 1 k
xlimits 0 kp1
. major x1tic number k
major x1tic number kp2
minor x1tic number 0
x1tic label format alphabetic
let string junk = sp() cr()
loop for j = 1 1 k
   let string junk = ^junk X^j
end loop
x1tic label contents ^junk
.
char blank blank
lines solid dotted
char circle blank
char hw 1 1
char fill on off
title Main Effects Plot
y1label Average Response
x1label Factors
dex mean plot y x1 to x^k
save
.
let dxplot = distinct xplot
let dxplot = sort dxplot
let ndxplot = number dxplot
let b = 18
just cece
loop for j = 1 1 ndxplot
   let a = dxplot(j)
   moveds a b
   let evenodd = mod(j,2)
   if evenodd = 1; text -; end if
   if evenodd = 0; text +; end if
end loop
.
limits
char blank all
char fill off
char hw 2 1
lines solid all
xtic offset 5 5
tic label format default
minor xtic number default
major xtic number default
just left
title
y1label
x1label
.
. -----step 4: interaction effects matrix-----
.
device 1 off; device 2 off
dex mean plot y x1 to x^k
device 1 on; device 2 on
.
let numfac = k
.
let ybar = mean y
let ybar2 = abs(ybar)
let lybar2 = log10(ybar2)
let lybar3 = int(lybar2)
let dp = round(lybar3) + 2
let dp = round(lybar3)
if lybar3 < 0; let dp = 0; end if
.
let string stat = mean
.
. probe fy1min; let ybottom = probeval
. probe fy1max; let ytop = probeval
. let a = ybottom
. let b = ytop
. ylimits a b
ylimits freeze
.
let string s0 = Average Y
y1label offset 15
loop for j = 1 1 k
   let string s^j = X^j
end loop
.
erase
call dexstat2.dp
.
move 50 93
hw 2 1
justification center
text Interaction Effects Matrix
.
ylimits
y1label offset default
title
y1label
x1label
.
. -----step 5: block plot-----
.
char blank all
char - +
lines solid all
lines bl bl
x1tics off
x1tic labels off
.
char hw 4 3 4 3
legend 1 hw 4 3
tic label size 4
label size 4
.
y1label Average Response
y1label offset 20
x1label Factor Combinations
.
multiplot 2 3
legend 1 coordinates 18 85
legend 1 x1
mean block plot y x2 x3 x1
legend 1 x2
mean block plot y x1 x3 x2
legend 1 x3
mean block plot y x1 x2 x3
legend 1 x4
mean block plot y x1 x2 x4
legend 1 x5
mean block plot y x1 x2 x5
multiplot off
.
move 50 93
hw 2 1
justification center
text Block Plots
.
legend 1
x1tics on
x1tic labels on
char hw 2 1 all
legend 1 hw 2 1
label size 2
tic label size 2
title
y1label
y1label offset default
x1label
.
. -----step 6: Youden plot-----
.
yates y
skip 0
read dpst1f.dat tag coef
.
device 1 off; device 2 off
dex mean plot y x1 to x^k
device 1 on; device 2 on
.
.
let numtag = number tag
loop for j = 1 1 numtag
   let posint = tag(j)
   call digit.dp
   let prod = ^sdigit
   let ymeanp(j) = mean y subset prod +1
   let ymeanm(j) = mean y subset prod -1
end loop
let effect = ymeanp - ymeanm
char blank all
char automatic tag
lines blank all
probe fy1min; let ybottom = probeval
probe fy1max; let ytop = probeval
. let a = 50
. let b = 80
let ydel = ytop-ybottom
let a = ybottom + (1/14)*ydel; . 1/14 is correct for offset 5 5
let b = ytop - (1/14)*ydel
ylimits a b
xlimits a b
title Youden Plot
y1label Average Response for + Setting
x1label Average Response for - Setting
let numeffec = number tag
let index2 = sequence 1 1 numeffec
plot ymeanp ymeanm index2
lines solid
draw data ybar a ybar b
draw data a ybar b ybar
.
limits
title
y1label
x1label
.
. -----step 7: |effects| plot-----
.
yates y
skip 0
read dpst1f.dat tag coef
.
title |Effects| Plot
y1label |Effects|
x1label Factor
ygrid
x1tics off
x1tic label off
y1tic offset 10
char circle
char hw 1 1
char fill on
lines blank
spike on
let s = sd y
let coef2 = abs(coef)
plot coef2 index2
.
hw 2 1
just cece
loop for j = 1 1 numeffec
   let x0 = xplot(j)
   let evenodd = mod(j,2)
   let y0 = 23
   if evenodd = 0; let y0 = 27; end if
   moveds x0 y0
   let tagj = tag(j)
   text ^tagj
end loop
.
let x0 = 57
let y0 = 86-2*numeffec-2
box fill on
box fill pattern solid
box fill color g80
box x0 y0 85 90
lines dotted
draw x0 87 85 87
.
just left
let y0 = 88
let x0 = 60
move x0 y0
text Factor
let x0 = 70
move x0 y0
text Effect
let y0 = 86
loop for j = 1 1 numeffec
   let x0 = 60
   let y0 = y0-2
   move x0 y0
   let tagj = tag(j)
   text ^tagj
   let x0 = 70
   move x0 y0
   let coefj = coef(j)
   text : ^coefj
end loop
.
let x0 = 40
let y0 = 87
box 15 90 x0 y0
let y0 = 88
let x0 = 17
move x0 y0
let ybar = average y
text Average = ^ybar
.
grid off
y1tic offset 5
x1tics on
x1tic label on
char blank all
char hw 2 1
lines solid all
spike off
just left
title
y1label
x1label
tic offset 5 5
.
. -----step 8: halfnormal probability plot-----
.
yates y
skip 0
read dpst1f.dat tag coef
.
let coef2 = abs(coef)
char x
lines bl
title Halfnormal Probability Plot of Effects
y1label Ordered |Effects|
x1label Halfnormal Distribution Order Statistic Medians
halfnormal probability plot coef2
.
let numyplot = number yplot
just lece
loop for j = 1 1 numyplot
   let yplotj = yplot(j)
   movesd 90 yplotj
   let jrev = numyplot-j+1
   let tagjrev = tag(jrev)
   text ^tagjrev
end loop
.
just left
title
y1label
x1label
.
. -----step 9: cumulative residual sd plot-----
.
yates y
skip 0
read dpst1f.dat tag coef tcoef indrsd cumrsd
.
title Cumulative Residual SD Plot
y1label Residual SD
x1label Cumulative Model
ygrid
x1tics off
x1tic label off
y1tic offset 10
char circle
char hw 1 1
char fill on
lines blank
spike on
let numeffec = number tag
let numeffp1 = numeffec+1
let index2 = sequence 1 1 numeffec
let s = sd y
let cumrsd(numeffp1) = s
let index2(numeffp1) = 0
plot cumrsd index2
.
hw 2 1
just cece
loop for j = 1 1 numeffec
   let x0 = xplot(j)
   let evenodd = mod(j,2)
   let y0 = 23
   if evenodd = 0; let y0 = 27; end if
   moveds x0 y0
   let tagj = tag(j)
   text ^tagj
end loop
moveds 0 27
text Average
.
grid off
y1tic offset 5
x1tics on
x1tic label on
char blank all
char hw 2 1
lines solid all
spike off
just left
title
y1label
x1label
tic offset 5 5
.
 . -----step 10: contour plot-----
 .
device 1 off; device 2 off
ytic offset 0 0
plot y
probe fy1min; let mintic = probeval
probe fy1max; let maxtic = probeval
let rangetic = maxtic-mintic
let sigdig = msd(rangetic)
let inc = rangetic/sigdig
let numinc = rangetic/inc
let inc2 = inc
if numinc = 1; let inc2 = inc/10; end if
if numinc = 2; let inc2 = inc/5; end if
if numinc = 3; let inc2 = inc/3; end if
if numinc = 4; let inc2 = inc/2; end if
if numinc = 5; let inc2 = inc/2; end if
if numinc = 6; let inc2 = inc/2; end if
ytic offset 5 5
device 1 on; device 2 on
.
yates y
skip 0
read dpst1f.dat tag coef
.
let a = tag(1)
let b = tag(2)
let u1 = x^a
let u2 = x^b
let start = mintic - 3*inc2
let stop = maxtic + 3*inc2
let ycont = sequence start inc2 stop
. erase
title: Contour Plot of the 2 Dominant Factors: X^a and X^b
y1label X^a
x1label X^b
call dexcont.dp
.
