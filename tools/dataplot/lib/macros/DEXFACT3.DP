. THIS IS THE DATAPLOT MACRO FILE     DEXFACT3.DP
. PURPOSE--CREATE ALL 2-TERM AND 3-TERM INTERACTION FACTORS
.          FROM THE ORIGINAL NUMFAC FACTORS
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.      NUMFAC = NUMBER OF FACTORS
. EXAMPLE--DIMENSION 20 VARIABLES
.          SKIP 25
.          READ BOXCHEM.DAT Y X1 TO X4
.          LET NUMFAC = 4
.          CALL DEXFACT3.DP
.          DEXMEANPLOT Y X1 TO X14
.
. -----START POINT-----
.
FEEDBACK OFF
.
LET NUMME = NUMFAC
WRITE "NUMBER OF MAIN EFFECTS                  = ^NUMME "
.
LET L0 = NUMFAC
LOOP FOR L1 = 1 1 NUMFAC
LOOP FOR L2 = L1 1 NUMFAC
IF L2 > L1
LET L0 = L0+1
LET X^L0 = X^L1 * X^L2
END OF IF
END OF LOOP
END OF LOOP
LET NUM2TI = L0-NUMME
WRITE "NUMBER OF 2-TERM INTERACTIONS           = ^NUM2TI "
.
LOOP FOR L1 = 1 1 NUMFAC
LOOP FOR L2 = L1 1 NUMFAC
LOOP FOR L3 = L2 1 NUMFAC
LET DEL21 = L2-L1
LET DEL32 = L3-L2
LET DELPROD = DEL21*DEL32
IF DELPROD > 0
LET L0 = L0+1
LET X^L0 = X^L1 * X^L2 * X^L3
END OF IF
END OF LOOP
END OF LOOP
END OF LOOP
LET NUM3TI = L0-NUM2TI
WRITE "NUMBER OF 3-TERM INTERACTIONS           = ^NUM3TI "
.
WRITE "THE TOTAL (OLD + NEW) NUMBER OF FACTORS = ^L0 "
WRITE "THE EXECUTION OF THE DEXFACT3.DP MACRO IS DONE."
