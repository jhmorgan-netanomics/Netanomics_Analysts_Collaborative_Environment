. THIS IS THE DATAPLOT PROGRAM FILE     SUBSERIESPLOT.DP
. PURPOSE--GENERATE A SUBSERIES PLOT OF THE RAW DATA
. (THE SPIKE BASE WILL BE AT THE LOCAL MEAN FOR EACH SET).
. THE ANALYST MUST EXTERNALLY DEFINE (BEFORE CALLING THIS MACRO)--
.    1) DIMENSION (TYPICALLY MORE THAN 10)
.    2) NX1 = NUMBER OF DISTINCT X1 VALUES
.    3) FEEDBACK OFF (IF DESIRED)
. INPUT--Y  = RESPONSE VARIABLE
.        X1 = HORIZONTAL AXIS VARIABLE
.        X2 = WITHIN-GROUP VARIABLE
. NOTE--THIS CODE IS SET UP FOR NX1 = 8 DISTINCT SETS,
.       IF NX1 IS OTHER THAN 8, THEN ONLY THE CODE
.       IN STEP 3 BELOW NEEDS TO BE CHANGED.
. DATE--NOVEMBER 1988
.
. -----START POINT-----------------------------------
.
. STEP 1--
. CREATE THE DUMMY VARIABLE X1PRIME FOR HORIZONTAL POSITIONING.
.
LET X2MEAN = MEAN X2
LET X2RANGE = RANGE X2
LET X1PRIME = X1+(X2-X2MEAN)/(3*X2RANGE)
.
. STEP 2--
. COMPUTE THE NX1 X1 MEANS.
. PLACE THEM IN THE NX1 PARAMETERS M1, M2, M3, ...
. PLACE THEM IN THE (LONG) VARIABLE MEANX1
. (MEANX1 WILL HAVE THE SAME LENGTH AS Y, X1, AND X2).
.
LOOP FOR KDUMMY = 1 1 NX1
LET M^KDUMMY = MEAN Y SUBSET X1 KDUMMY
LET MEANX1 = M^KDUMMY SUBSET X1 KDUMMY
LET RESX2 = Y-MEANX1
END OF LOOP
.
. STEP 3--
. DEFINE PROPER LINE AND SPIKE X1TINGS
. FOR THE UPCOMING PLOT.
. THE PLOT WILL MAKE USE OF THE FIRST 2*NX1 SETTINGS
. OF THE CHARACTERS, LINES, AND SPIKE COMMANDS,
. OF WHICH THE FIRST NX1 SETTINGS ARE CRITICAL.
. THE CODE BELOW HAS SETTINGS FOR NX1 = 8.
. CHANGE THE CHARACTERS, LINES, AND SPIKES COMMANDS
. IF NX1 IS OTHER THAN 8.
.
SPIKE OFF ALL
SPIKE ON ON ON ON ON ON ON ON
SPIKE BASE M1 M2 M3 M4 M5 M6 M7 M8
CHARACTERS BLANK ALL
CHARACTERS CIRC CIRC CIRC CIRC CIRC CIRC CIRC CIRC
CHARACTER HW 1 .75 ALL
CHARACTER FILL ON ALL
LINES SOLID ALL
LINES BL BL BL BL BL BL BL BL
.
. STEP 4--
. GENERATE THE PLOT.
.
PLOT Y X1PRIME X1 AND
PLOT MEANX1 X1PRIME X1
