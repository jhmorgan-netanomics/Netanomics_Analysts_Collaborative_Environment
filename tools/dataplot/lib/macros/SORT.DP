. THIS IS THE DATAPLOT PROGRAM FILE     SORT.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN
.          WILL GENERATE A SORTED (BY NUMERICAL PRIORITY) LIST.
. DATE--JUNE 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
ERASE
.
SET IO TERMINAL
.
WRITE " "
WRITE "               *************************************"
WRITE "               **          DATAPLOT MENU          **"
WRITE "               **    SORTED (BY PRIORITY) LIST    **"
WRITE "               *************************************"
WRITE " "
.
. STEP 1--
. READ IN THE LIST ITEMS
. AFTER EACH ITME ENTER A TIME
.
WRITE " "
WRITE "NUMBER OF ITEMS = ?"
READ PARAMETER N
.
WRITE " "
WRITE "ENTER ALPHABETIC LIST ITEMS (ONE ITEM PER LINE)"
WRITE "AND ENTER ASSOCIATED PRIORITY (ON THE NEXT LINE)"
.
LOOP FOR K = 1 1 N
WRITE "ITEM ^K = ?"
READ STRING S^K
WRITE "PRIORITY OR TIME FOR ITEM ^K = ?"
READ PARAMETER X^K
LET X(K)=X^K
END OF LOOP
.
WRITE " "
WRITE "DO YOU WANT A DESCENDING OR ASCENDING SORT?"
WRITE "IF WANT ASCENDING,  ENTER 1"
WRITE "IF WANT DESCENDING, ENTER 2"
READ PARAMETER AD
.
WRITE " "
WRITE "TITLE = ?"
READ STRING T
.
LET DUMMY = 1 1 N
IF AD = 2
LET X = -X
END OF IF
LET X2 = SORTC X DUMMY
IF AD = 2
LET X2 = -X2
END OF IF
.
CURSOR SIZE 3
ERASE
WRITE "^T "
WRITE " "
WRITE "ORDER--PRIORITY (CUM. PRIORITY)   ITEM"
LET LSUM = 0
LOOP FOR K = 1 1 N
LET L = X2(K)
LET LSUM = LSUM + L
LET L2 = DUMMY(K)
WRITE "^K    --     ^L   (^LSUM )      ^S^L2 "
END OF LOOP
SAVE 11 TO 1
