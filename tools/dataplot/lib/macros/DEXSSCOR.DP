. THIS IS THE DATAPLOT MACRO FILE     DEXSSCOR.DP
. DEXSSCOR (DESIGN OF EXPERIMENT SUPERSTATURATED CORRELATION)
. PURPOSE--TO ANSWER THE QUESTION:
.          WHAT MAIN EFFECTS ARE PARTIALLY CONFOUNDED
.          (VERSUS UNCONFOUNDED) WITH
.          OTHER MAIN EFFECTS (IN SUPERSATURATED DESIGNS)
.          IF PARTIALLY CONFOUNDED, THEN THE BOX WILL BE FILLED BLACK
.          IF UNCOUNFOUNDED, THEN THE BOX WILL BE EMPTY.
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y = THE RESPONSE VARIABLE
.       2. X1 X2 X3 X4 ETC. = FACTORS (CONSTRAINT: 2 LEVELS: -1 AND +1)
.       3. NUMFAC = THE NUMBER OF FACTORS
.       4. STAT = THE STRING WITH THE DESIRED STATISTIC
.              EXAMPLE--LET STRING STAT = MEAN
.              EXAMPLE--LET STRING STAT = SD
.              EXAMPLE--LET STRING STAT = ANOP
.       5. YLIMITS = ? ?  (USE SAME LIMITS AS FROM    DEX MEAN PLOT)
.       6. DP = THE NUMBER OF DECIMAL PLACES THE
.              COMPUTED SLOPE SHOULD BE ROUNDED TO
.              EXAMPLE--LET DP = 1
.       7. S0         = THE STRING  WITH THE DESIRED VERT. AXIS LABEL
.              EXAMPLE--LET STRING S0 = PERCENT ACCEPTABLE
.       8. S1, S2, S3 = THE STRINGS WITH THE DESIRED HOR. AXIS LABELS
.              EXAMPLE--LET STRING S1 = OVEN TEMPERATURE
.       9. ANOP LIMITS (ONLY NEEDED IF STAT = ANOP)
.              EXAMPLE--ANOP LIMITS 0 65
.              EXAMPLE--ANOP LIMITS 80 100
. NOTE--THE USER SHOULD PRE-SET YLIMITS
.       FOR MEAN PLOT, YLIMITS SHOULD BE ABOUT
.       DATA YMIN TO YMAX, OR ROUGHLY 1/2 THE RANGE
.       FOR ANOP PLOT, YLIMITS SHOULD = 0 100
. EXAMPLE--SKIP 25
.          READ BOOTHCOX.DAT X1 TO X24
.          LET NUMFAC = 12
.          LET STRING STAT = MEAN
.          LET DP = 1
.          YLIMITS 50 100
.          CALL DEXSSCOR.DP
.
. -----START POINT--------------------
.
FEEDBACK OFF
.
. ------------------------------------
. STEP 1--
. CHECK EXISTENCE OF INPUT VARIABLES
 
IF NUMFAC NOT EXIST
LET NUMFAC = 3
END OF IF
.
IF STAT NOT EXIST
LET FUNCTION STAT = MEAN
END OF IF
.
IF DP NOT EXIST
LET DP = 2
END OF IF
.
. ------------------------------------
. STEP 2--
. SET UP SPECIAL PLOT FORMAT
.
XLIMITS -2 2
CHAR CIRCLE BLANK
LET NUMFAC2 = NUMFAC*1.5
LET JUNK2 = NUMFAC*2
CHAR HW NUMFAC2 NUMFAC
CHAR FILL ON
LINES SOLID DOTTED
XTICS OFF
XTIC LABELS OFF
TIC LABEL SIZE JUNK2
LABEL SIZE JUNK2
LEGEND 1 SIZE JUNK2
IF NUMFAC >= 10
   LET LEGY0 = 100-1.7*NUMFAC
   LEGEND 1 COORDINATES 5 LEGY0
END OF IF
FRAME CORNER COORDINATES 0 0 100 100
.
BOX SHADOW HW 0 0
.
. ------------------------------------
. STEP 3--
. GENERATE THE PLOTS
.
MULTIPLOT NUMFAC NUMFAC
.
Y1LABEL ^S0
LOOP FOR K = 1 1 NUMFAC
LOOP FOR L = K 1 NUMFAC
.
MULTIPLOT NUMFAC NUMFAC K L
.
IF K = L
   LET PROD = X^K
   LET PROD2 = Y*PROD
   LET SLOPE = MEAN PROD2
   LET SLOPE = 2*SLOPE
   LET SLOPE = ROUND(SLOPE,DP)
   LEGEND 1 ^K :  ^SLOPE
   X1LABEL ^S^K
   BOX FILL PATTERN OFF
   ^STAT PLOT Y PROD
   IF K = 1
      YTICS OFF
      YTIC LABELS OFF
   END OF IF
   Y1LABEL
   X1LABEL
END OF IF
.
. THE FOLLOWING LINE APPEARS NOT TO WORK
IF K <> L
   LET JUNKCORR = CORRELATION X^K X^L
   PLOT
   BOX FILL PATTERN OFF
   IF JUNKCORR <> 0
      BOX FILL PATTERN SOLID
   END OF IF
. THE FOLLOWING 3 LINES ARE TO CIRCUMVENT AN IF BUG
   IF K = L
      BOX FILL PATTERN OFF
   END OF IF
   BOX 0 0 100 100
   .
END OF IF
BOX FILL PATTERN OFF
.
END OF LOOP
END OF LOOP
.
. ------------------------------------
. STEP 4--
. REVERT BACK TO THE ORIGINAL PLOT FORMAT
.
XLIMITS
CHAR BLANK ALL
CHAR SIZE 2 ALL
LINES SOLID ALL
Y1TICS ON
X1TICS ON
Y1TIC LABELS ON
X1TIC LABELS ON
TIC LABEL SIZE DEFAULT
LABEL SIZE DEFAULT
LEGEND SIZE DEFAULT
FRAME CORNER COORDINATES
LEGEND 1
MULTIPLOT OFF
.
FEEDBACK ON
