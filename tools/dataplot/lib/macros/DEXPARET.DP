. THIS IS THE DATAPLOT MACRO FILE     DEXPARET.DP
. PURPOSE--GENERATE A  PARETO PLOT OF
.          ABSOLUTE ESTIMATED EFFECTS
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       Y = THE RESPONSE VARIABLE
.       X1 X2 X3 X4 ETC. = FACTORS
.       NUMFAC = NUMBER OF FACTORS
. EXAMPLE--DIMENSION 20 VARIABLES
.          SKIP 25
.          READ BOXBIKE2.DAT Y X1 TO X7
.          LET NUMFAC = 7
.          CALL DEXPARET.DP
.
. -----START POINT-----
.
FEEDBACK OFF
.
LET COUNT = 0
LOOP FOR K = 1 1 NUMFAC
LOOP FOR L = K 1 NUMFAC
.
IF K = L
LET PROD = X^K
LET PROD2 = Y*PROD
LET SLOPE = MEAN PROD2
LET SLOPE = 2*SLOPE
. LET SLOPE = ROUND(SLOPE,R)
LET COUNT = COUNT+1
LET COEF(COUNT) = SLOPE
LET TAG(COUNT) = K
END OF IF
.
IF K <> L
LET PROD = X^K * X^L
LET PROD2 = Y*PROD
LET SLOPE = MEAN PROD2
LET SLOPE = 2*SLOPE
. LET SLOPE = ROUND(SLOPE,R)
LET COUNT = COUNT+1
LET COEF(COUNT) = SLOPE
LET TAG(COUNT)=10*K+L
END OF IF
.
END OF LOOP
END OF LOOP
.
LET COEF2 = ABS(COEF)
.
CHARACTERS AUTOMATIC DISTINCT TAG
CHAR FONT SIMPLEX ALL
CHAR SIZE 1 ALL
CHARACTER OFFSET 0 2 ALL
LINES BLANK ALL
BAR ON ALL
BAR WIDTH .5 ALL
.
PARETO PLOT COEF2
.
CHAR BLANK ALL
CHAR FONT TEKTRONIX ALL
CHAR SIZE 2 ALL
CHAR OFFSET 0 0 ALL
LINES SOLID ALL
BAR OFF ALL
BAR WIDTH 1 ALL
