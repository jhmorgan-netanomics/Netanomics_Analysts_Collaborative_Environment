.  Macro for cannonical correlation.
.
.  This macro needs:
.  1) Matrix containing the response variables            ZZZZX
.  2) Define number of variables belonging to group 1     P1
.  3) To use covariance matrx, LET ZZZZFLAG = 1,
.     to use correlation matrix, LET ZZZZFLAG = 0
.
FEEDBACK OFF
.
DELETE S TAG TAG2 SA SB SD RA RAINV RAINVT RD RDINV CZ C LEV SV REV
DELETE WILK TEMPV TEMP GAMMA1 GAMMA2 SADIAG COEFR1 COEFR2
IF ZZZZFLAG NOT EXIST
  LET ZZZZFLAG = 0
END OF IF
IF ZZZZFLAG = 1
  LET S = VARIANCE-COVARIANCE MATRIX ZZZZX
END OF IF
IF ZZZZFLAG <> 1
  LET S = CORRELATION MATRIX ZZZZX
END OF IF
.
LET N = MATRIX NUMBER OF COLUMNS ZX
LET P2 = N - P1
.
.  Remainder of macro is not dependent on the particualar data set
.  being used.
.
LET P21 = P1 + 1
LET TAG = SEQUENCE 1 1 P1
LET TAG2 = SEQUENCE 1 1 P2
LET SA = MATRIX DEFINITION S1 P1 P1
LET SB = MATRIX DEFINITION S^P21 P1 P2
LET SD = MATRIX DEFINITION S^P21 N P2 P21
.
.  CALCULATE C = (S11**(-1/2))'S12(S22**(-1/2))
.  CHOLESKY DECOMPOSITION IS THE MATRIX SQUARE ROOT
.
LET RA = CHOLESKY DECOMP SA
LET RAINV = TRIANGULAR INVERSE RA
LET RAINVT = MATRIX TRANSPOSE RAINV
LET RD = CHOLESKY DECOMP SD
LET RDINV = TRIANGUILAR INVERSE RD
LET CZ = MATRIX MULTIPLY RAINVT SB
LET C = MATRIX MULTIPLY CZ RDINV
.
.  USE SINGULAR VALUE DECOMPOSITION TO COMPUTE CANONICAL CORRELATIONS
.  AND CANONICAL COEFFICIENTS.  CALCULATE WILK'S LAMBDA.
.
LET LEV SV REV = SINGULAR VALUE DECOMPOSITION C
LET TEMPV = 1 - SV*SV FOR I = 1 1 P2
LOOP FOR K = 1 1 P2
    LET A = PRODUCT TEMPV SUBSET TAG2 >= K
    LET WILK(K) = A
END OF LOOP
.
.  CALCULATE CANONICAL COEFFICENTS
.
LET GAMMA1 = MATRIX MULTIPLY RAINV LEV
LET GAMMA2 = MATRIX MULTIPLY RDINV REV
.
.  CALCULATE CORRELATIONS BETWEEN VARIABLES AND CANONICAL VARIABLES
.
LET RA = MATRIX TRANSPOSE RA
LET RA = MATRIX MULTIPLY RA LEV
LET TEMP = MATRIX DIAGONAL SA
LET TEMP = 1/TEMP
LET SADIAG = DIAGONAL MATRIX TEMP
LET SADIAG = MATRIX MULTIPLY SADIAG RA
LET COEFR1 = MATRIX DEFINITION SADIAG1 P1 P2
DELETE TEMP
LET TEMP = MATRIX DIAGONAL SD
LET TEMP = 1/TEMP
LET SDDIAG = DIAGONAL MATRIX TEMP
LET RD = MATRIX TRANSPOSE RD
LET RD = MATRIX MULTIPLY RD REV
LET COEFR2 = MATRIX MULTIPLY SDDIAG RD
CAPTURE ZZZZJUNK.DAT
PRINT " "
PRINT "CANANICAL CORRELATIONS AND WILKS LAMBDA"
PRINT SV WILK FOR I = 1 1 P2
PRINT " "
PRINT "GROUP 1 CANNONICAL COEFFICIENTS"
PRINT GAMMA1
PRINT " "
PRINT "GROUP 2 CANNONICAL COEFFICIENTS"
PRINT GAMMA2
PRINT " "
PRINT "CORRELATIONS BETWEEN GROUP 1 VARIABLES AND "
PRINT "AND GROUP 1 CANONICAL SCORES"
PRINT COEFR1
PRINT " "
PRINT "CORRELATIONS BETWEEN GROUP 2 VARIABLES AND "
PRINT "AND GROUP 2 CANONICAL SCORES"
PRINT COEFR2
END OF CAPTURE
.
FEEDBACK ON
LIST ZZZZJUNK.DAT
