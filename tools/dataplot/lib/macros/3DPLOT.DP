. THIS IS THE DATAPLOT PROGRAM FILE     3DPLOT.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN
.          WILL GENERATE A 3D-PLOT IN A CUBE.
. ASSUMPTION--DATA IS IN Y, X1, AND X2.
. DATE--JUNE 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
SET IO TERMINAL
.
ERASE
.
WRITE " "
WRITE "               *****************************************"
WRITE "               **           DATAPLOT MENU             **"
WRITE "               **          3DPLOT IN CUBE             **"
WRITE "               **  (DATA ASSUMED TO BE IN Y, X1, X2)  **"
WRITE "               *****************************************"
WRITE " "
.
. STEP 1--
. NORMALIZE THE DATA TO 0 1
.
LET X3MAX = MAXIMUM Y
LET X3MIN = MINIMUM Y
LET X1MAX = MAXIMUM X1
LET X1MIN = MINIMUM X1
LET X2MAX = MAXIMUM X2
LET X2MIN = MINIMUM X2
LET Y = (Y-X3MIN)/(X3MAX-X3MIN)
LET X1 = (X1-X1MIN)/(X1MAX-X1MIN)
LET X2 = (X2-X2MIN)/(X2MAX-X2MIN)
.
. STEP 2--
. READ IN THE CUBE FRAME
.
READ [FILLIB.DATA]CUBEARROW.DAT U V W TAG
.
. STEP 3--
. READ IN THE EYE COORDINATES
.
WRITE " "
WRITE "EYE COORDINATES (NORMALIZED (X,Y,Z)) = ?"
WRITE "(RECOMMENDED VALUES ARE 1.3 1.5 1.7)"
READ PARAMETER XEYE YEYE ZEYE
.
. STEP 4--
. GATHER ANNOTATION INFORMATION
.
WRITE " "
WRITE "NAME FOR VARIABLE 1 (FIRST  HORIZ. AXIS) = ?"
READ STRING SX1
WRITE "NAME FOR VARIABLE 2 (SECOND HORIZ. AXIS) = ?"
READ STRING SX2
WRITE "NAME FOR VARIABLE 3 (VERTICAL AXIS) = ?"
READ STRING SX3
.
WRITE " "
WRITE "ANGLE (-90 TO 90) FOR FIRST  HORIZ. LABEL = ?"
READ PARAMETER ANG1
WRITE "ANGLE (-90 TO 90) FOR SECOND HORIZ. LABEL = ?"
READ PARAMETER ANG2
WRITE "ANGLE (-90 TO 90) FOR VERTICAL AXIS LABEL = ?"
READ PARAMETER ANG3
.
WRITE " "
WRITE "SCREEN POSITION FOR CENTER OF FIRST  HORIZ. LABEL = ?"
READ PARAMETER PX1 PY1
WRITE "SCREEN POSITION FOR CENTER OF SECOND HORIZ. LABEL = ?"
READ PARAMETER PX2 PY2
WRITE "SCREEN POSITION FOR CENTER OF VERTICAL AXIS LABEL = ?"
READ PARAMETER PX3 PY3
.
WRITE " "
WRITE "SCREEN POSITION FOR FIRST  HORIZ. AXIS MINIMUM = ?"
READ PARAMETER PX1MIN PY1MIN
WRITE "SCREEN POSITION FOR FIRST  HORIZ. AXIS MAXIMUM = ?"
READ PARAMETER PX1MAX PY1MAX
WRITE "SCREEN POSITION FOR SECOND HORIZ. AXIS MINIMUM = ?"
READ PARAMETER PX2MIN PY2MIN
WRITE "SCREEN POSITION FOR SECOND HORIZ. AXIS MAXIMUM = ?"
READ PARAMETER PX2MAX PY2MAX
WRITE "SCREEN POSITION FOR VERTICAL      AXIS MINIMUM = ?"
READ PARAMETER PX3MIN PY3MIN
WRITE "SCREEN POSITION FOR VERTICAL      AXIS MAXIMUM = ?"
READ PARAMETER PX3MAX PY3MAX
.
WRITE " "
WRITE "TITLE          (POSITIONED AT TOP OF PAGE) = ?"
READ STRING T
WRITE "SUB-TITLE      (POSITIONED UNDER THE TITLE) = ?"
READ STRING ST
WRITE "BASE TITLE     (POSITIONED 3/4 DOWN PAGE) = ?"
READ STRING BT
WRITE "SUB-BASE TITLE (POSITIONED UNDER BASE TITLE) = ?"
READ STRING SBT
.
WRITE " "
WRITE "CHARACTER SIZE FOR TITLE = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HT
WRITE "CHARACTER SIZE FOR SUB-TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HST
WRITE "CHARACTER SIZE FOR BASE TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HBT
WRITE "CHARACTER SIZE FOR SUB-BASE TITLE = ?"
WRITE "(1 IS A RECOMMENDED CHOICE)"
READ PARAMETER HSBT
.
WRITE " "
WRITE "FONT = ?"
READ STRING F
.
WRITE " "
WRITE "THE 3DPLOT IN A CUBE IS NOW BEING FORMED..."
WRITE "GENERATING SUCCEEDING PLOTS MAY BE DONE BY ENTERING /"
WRITE "TO GENERATE THE FIRST PLOT, HIT A CARRIAGE RETURN NOW..."
PAUSE
.
. STEP 6--
. GENERATE THE PLOT.
. WRITE IN THE 3 AXIS IDENTIFIERS.
. WRITE OUT THE VARIOUS TITLES.
.
ANGLE UNITS DEGREES
.
EYE COORDINATES XEYE YEYE ZEYE
3DPLOT Y X1 X2 X1 AND
3DPLOT Y X1 X2 X2 AND
3DPLOT U V W TAG
FONT ^F
JUSTIFICATION CENTER
HW 1.5 1
ANGLE ANG1
MOVE PX1 PY1
TEXT ^SX1
MOVE PX1MIN PY1MIN
TEXT ^X1MIN
MOVE PX1MAX PY1MAX
TEXT ^X1MAX
ANGLE ANG2
MOVE PX2 PY2
TEXT ^SX2
MOVE PX2MIN PY2MIN
TEXT ^X2MIN
MOVE PX2MAX PY2MAX
TEXT ^X2MAX
ANGLE ANG3
MOVE PX3 PY3
TEXT ^SX3
MOVE PX3MIN PY3MIN
TEXT ^X3MIN
MOVE PX3MAX PY3MAX
TEXT ^X3MAX
JUSTIFICATION CENTER
ANGLE 0
MOVE 50 92
LET WT = HT/2
HEIGHT HT
WIDTH WT
TEXT ^T
MOVE 50 86
LET WST = HST/2
HEIGHT HST
WIDTH WST
TEXT ^ST
MOVE 50 10
LET WBT = HBT/2
HEIGHT HBT
WIDTH WBT
TEXT ^BT
MOVE 50 5
LET WSBT = HSBT/2
HEIGHT HSBT
WIDTH WSBT
TEXT ^SBT
SAVE 50 TO 1
.
CALL LOGO3.DP
.
SET IO
SET IRD 5
