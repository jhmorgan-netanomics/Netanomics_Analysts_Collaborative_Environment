. THIS IS THE DATAPLOT PROGRAM FILE     PIECHART.DP
. PURPOSE--GENERATE AND EXECUTE MENU FOR SIMPLE PIE CHART
. DATE--MARCH 27, 1987
. MODIFIED JANUARY 1998. 
.     DATAPLOT METHOD FOR IDENTIFYING TRACES
.     MODIFED 1993 TO ALLOW REGIONS TO BE FILLED.  NEEDED TO MODIFY
.     THIS MACRO ACCORDINGLY.
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
SET IO TERMINAL
.
WRITE " "
WRITE "               ***********************"
WRITE "               **   DATAPLOT MENU   **"
WRITE "               **     PIE CHART     **"
WRITE "               ***********************"
WRITE " "
.
WRITE "----------------------------------------"
WRITE " "
WRITE " "
.
. STEP 1--
. READ IN THE RESPONSE VARIABLE INFO (UNORDERED OR ORDERED) INFORMATION
.
WRITE " "
WRITE "STEP 1--DEFINE THE RESPONSE VALUES
WRITE "        NUMBER OF DATA VALUES (N) = ?"
READ PARAMETER N
.
LOOP FOR K = 1 1 N
WRITE "        VALUE ^K = ?"
READ PARAMETER YTEMP^K
LET Y(K) = YTEMP^K
END OF LOOP
SET WRITE DECIMALS 6
PRINT Y
.
WRITE " "
WRITE "STEP 2--DEFINE THE IDENTIFIER STRINGS FOR EACH DATA VALUE"
WRITE " "
LOOP FOR K = 1 1 N
WRITE "IDENTIFIER STRING FOR VALUE ^K (Y^K ) = ?"
READ STRING M^K
END OF LOOP
.
WRITE " "
WRITE "FORMING THE PIE CHART..."
.
.
SET IO
.
.  X VARIABLE NEEDED TO AVOID "EMPTY" CLASSES
.
LET X = SEQUENCE 1 1 N
PIE CHART Y X
.
.  FOLLOWING IS TO ACCOUNT FOR THE FACT THAT PIE CHART IS
.  SCALED ON THE X-AXIS ACCORDING TO THE ASPECT RATIO OF THE
.  DEVICE.  FOLLOWING ASSUMES A LANDSCAPE ORIENTATION (I.E.,
.  ABOUT 4/3).
.
LET XPCENT=41.8
LET XPMAX=68.6
LET YPCENT=55
LET YPMAX=90
.
DEGREES
LET THETANEW=180
JUSTIFICATION CENTER
. LET R = 1.25
LET R = 0.5
HW 2 1
.
.  XMID AND YMID ARE THE CENTER OF THE INTERIOR.  IF YOU WANT
.  LABELS EXTERNAL TO THE PIE SLICES, NOTE THAT:
.    1) FOR Y COORDINATE, FIRST AND LAST POINT ARE THE ORGIN,
.       SO IGNORE IN CALCULATING MIN AND MAX Y VALUE.
.    2) FOR X COORDINATE, WANT THE ORGIN TO BE A POTENTIAL
.       MIN OR MAX VALUE, SO INCLUDE.
.  TO IMPLEMENT OTHER ALGORITHMS FOR LABELING, THE FIRST AND LAST
.  POINTS IN THE SET ARE THE ORGIN, ALL OTHER POINTS ARE THE POINTS
.  DRAWN ON THE OUTER CIRCLE OF THE PIE IN THE ORDER THAT THE ARE
.  DRAWN (I.E., CLOCKWISE).
.
LOOP FOR K = 1 1 N
.
.  Extract the coordinates for a given slice
.
   LET XTEMP = XPLOT
   LET YTEMP = YPLOT
   RETAIN XTEMP YTEMP SUBSET TAGPLOT = K
   LET NK = SIZE YTEMP
   LET NK2 = NK - 1
.
.  Determine min and max points, and then the mid-point,
.  of slice for each direction
.
   LET AX1 = MINIMUM XTEMP
   LET AX2 = MAXIMUM XTEMP
   LET XMID = AX1 + (AX2-AX1)/2
   LET AY1 = MINIMUM YTEMP  FOR I = 2 1 NK2
   LET AY2 = MAXIMUM YTEMP  FOR I = 2 1 NK2
   LET YMID = AY1 + (AY2-AY1)/2
.
   LET THETAMID=ARCTAN(XMID/ABS(YMID))
   LET THETAMID=90-THETAMID IF YMID >= 0
   LET THETAMID=270+THETAMID IF YMID < 0
   LET XT=R*COS(THETAMID)
   LET YT=R*SIN(THETAMID)
.
.  Following is to account for the fact that PIE CHART X
.  coordinates are scaled according to the aspect ratio of the
.  output device.
.
   LET XPT=XPCENT+(XPMAX-XPCENT)*XT
   LET YPT=YPCENT+(YPMAX-YPCENT)*YT
   MOVE XPT YPT
   TEXT ^M^K
.
END LOOP
.
