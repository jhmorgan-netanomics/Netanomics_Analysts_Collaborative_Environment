. THIS IS THE DATAPLOT MENU MACRO FILE     ISHIKAWA.DP
. PURPOSE--GENERATE AN ISHIKAWA DIAGRAM
. DATE--MAY 16, 1988
. UPDATED--JUNE 15, 1988 (TITLE AND BASE-TITLE)
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
DEFINE ER ESC FF
ER
.
SET IO TERMINAL
.
WRITE " "
WRITE "               ************************"
WRITE "               **   DATAPLOT MENU    **"
WRITE "               **  ISHIKAWA DIAGRAM  **"
WRITE "               ************************"
WRITE " "
.
. STEP 1--
. READ IN TITLE INFORMATION
.
WRITE " "
WRITE "----------------------------------------"
WRITE " "
WRITE "DO YOU WISH A    DEVICE 2 POSTSCRIPT    COMMAND EXECUTED NOW?"
WRITE "ENTER 0 IF NO     ENTER 1 IF YES"
READ PARAMETER POSTYN
IF POSTYN = 1
DEVICE 2 POSTSCRIPT
END OF IF
WRITE " "
.
WRITE "TITLE      (POSITIONED AT TOP    OF PAGE) = ?"
READ STRING T1
WRITE "BASE TITLE (POSITIONED AT BOTTOM OF PAGE) = ?"
READ STRING T2
WRITE "CHARACTER SIZE FOR TITLE = ?"
WRITE "(3 IS A RECOMMENDED CHOICE)"
READ PARAMETER HT1
WRITE "CHARACTER SIZE FOR BASE TITLE = ?"
WRITE "(2 IS A RECOMMENDED CHOICE)"
READ PARAMETER HT2
WRITE "FONT FOR TITLE AND BASE TITLE = ?"
WRITE "(RECOMMENDED FONT IS TRIPLEX)"
READ STRING F
.
. WRITE "SIZE FOR TITLE (RECOMMENDED SIZE = 3) = ?"
. READ IN THE TITLE AND GOAL INFORMATION
.
. STEP 2--
. READ IN GOAL INFORMATION
.
WRITE " "
WRITE "----------------------------------------"
WRITE "DEFINE THE GOAL BOX."
WRITE "        THE GOAL BOX WILL APPEAR ON"
WRITE "        THE FAR RIGHT."
WRITE "        DESIRED CONTENTS OF GOAL BOX = ?"
READ STRING G1
.
WRITE " "
WRITE "DO YOU WANT EXPLICIT ARROWS TO BE DRAWN?"
WRITE "ENTER 0 IF NO     ENTER 1 IF YES"
READ PARAMETER ARROWYN
.
. STEP 3--
. READ IN THE BRANCH INFORMATION
.
WRITE " "
WRITE "----------------------------------------"
WRITE "HOW MANY MAJOR BRANCHES (2, 3, 4, . . ., 19, 20) ?"
READ PARAMETER NUMBRA
LET NUMBRAH=NUMBRA/2
LET NUMBRAHP = NUMBRAH+1
LET NUMBR2 = 2*INT((NUMBRA+1.1)/2)
.
LOOP FOR K = 1 1 NUMBRA
WRITE "        NAME FOR BRANCH ^K = ?"
READ STRING B^K
END OF LOOP
.
LET HMAX = 1.8
LET HMIN = 0.8
LET WMAX = 0.9
LET WMIN = 0.4
.
. STEP 4--
. READ IN THE SUB-BRANCH INFORMATION
.
LOOP FOR K = 1 1 NUMBRA
.
WRITE " "
WRITE "----------------------------------------"
WRITE "MAJOR BRANCH ^K = ^B^K "
WRITE "        HOW MANY ITEMS FOR THIS BRANCH?"
READ PARAMETER NUMSUB^K
.
LET NUMSUBK = NUMSUB^K
LOOP FOR L = 1 1 NUMSUBK
WRITE "        ITEM ^L = ?"
LET KL = K*100+L
READ STRING SB^KL
END OF LOOP
END OF LOOP
.
ERASE
.
. STEP 5--
. COMPUTE INTERMEDIATE SETTINGS BASED ON NUMBER OF BRANCHES
.
IF NUMBR2 <= 4
LET X0 = 25
LET BRANCHDEL = 45
LET LINELEN = 35
LET BOXWIDTH = 25
END OF IF
.
IF NUMBR2 = 6
LET X0 = 20
LET BRANCHDEL = 30
LET BOXWIDTH = 25
LET LINELEN = 25
END OF IF
.
IF NUMBR2 = 8
LET X0 = 20
LET BRANCHDEL = 20
LET BOXWIDTH = 18
LET LINELEN = 17
END OF IF
.
IF NUMBR2 = 10
LET X0 = 20
LET BRANCHDEL = 15
LET BOXWIDTH = 14
LET LINELEN = 13
END OF IF
.
IF NUMBR2 = 12
LET X0 = 20
LET BRANCHDEL = 12
LET BOXWIDTH = 11
LET LINELEN = 11
END OF IF
.
IF NUMBR2 = 14
LET X0 = 20
LET BRANCHDEL = 10
LET BOXWIDTH = 9
LET LINELEN = 9
END OF IF
.
IF NUMBR2 = 16
LET X0 = 20
LET BRANCHDEL = 8.5
LET BOXWIDTH = 7.5
LET LINELEN = 7.5
END OF IF
.
IF NUMBR2 = 18
LET X0 = 20
LET BRANCHDEL = 7.5
LET BOXWIDTH = 6.5
LET LINELEN = 6.5
END OF IF
.
IF NUMBR2 = 20
LET X0 = 20
LET BRANCHDEL = 6.5
LET BOXWIDTH = 5.5
LET LINELEN = 5.5
END OF IF
.
. STEP 6--
. WRITE OUT TITLE AND BASE TITLE
.
FONT ^F
LET WT1 = HT1/2
HW HT1 WT1
JUSTIFICATION CENTER
MOVE 50 96
TEXT ^T1
LET WT2 = HT2/2
HW HT2 WT2
MOVE 50 3
TEXT ^T2
.
.
. STEP 7--
. FORM THE GOAL BOX AND MAIN STEM
.
FONT SIMPLEX
JUSTIFICATION CENTER
LET HG = HMAX
LET WG = HMIN
HEIGHT HG
WIDTH WG
LET BOXWIDG=18
LET X2 = 91
LET Y2 = 50
LET Y3 = Y2-HG/2
MOVE X2 Y3
TEXT ^G1
LET X5 = X2-BOXWIDG/2
LET X6 = X2+BOXWIDG/2
. LET Y5 = Y3-HG/2
. LET Y6 = Y3+HG+HG/2
. LET MARGIN = 0.75*HG
LET MARGIN = 1
LET Y5 = Y3-MARGIN
LET Y6 = Y3+HG+MARGIN
BOX X5 Y5 X6 Y6
DRAW 10 Y2 X5 Y2
.
IF ARROWYN = 1
LET X7 = X5-1.5
LET X8 = X5-1.5
LET Y7 = Y2 + .75
LET Y8 = Y2 - .75
DRAW X7 Y7 X5 Y2
DRAW X8 Y8 X5 Y2
END OF IF
.
. STEP 8--
. FORM THE BRANCHES
.
. LET H = 2
. LET W = 1
LET W = BOXWIDTH/25
LET W = WMAX IF W > WMAX
LET W = WMIN IF W < WMIN
LET H = W*2
LET H = HMAX IF H > HMAX
LET H = HMIN IF H < HMIN
HEIGHT H
WIDTH W
. LET MARGIN = 0.75*H
LET MARGIN = 1
.
LET X = X0
LET X = X-BRANCHDEL
LET Y1 = 50
LOOP FOR K = 1 1 NUMBRA
LET X = X+BRANCHDEL
LET X = X0 IF K = NUMBRAHP
LET X1 = X
. LET X2 = X1-15
LET X2 = X1-10
LET Y2 = 90
LET Y2 = 10 IF K >= NUMBRAHP
DRAW X1 Y1 X2 Y2
. LET MARGIN = H/2
. LET MARGIN = MARGIN2 IF K >= NUMBRAHP
LET Y3 = Y2+MARGIN
IF K >= NUMBRAHP
LET Y3 = Y2-(MARGIN+H)
END OF IF
MOVE X2 Y3
TEXT ^B^K
LET X5 = X2-BOXWIDTH/2
LET X6 = X2+BOXWIDTH/2
LET Y5 = Y2
LET BOXHEIGHT = (MARGIN+H+MARGIN)
LET Y6 = Y2+BOXHEIGHT
IF K >= NUMBRAHP
LET Y6 = Y2-BOXHEIGHT
END OF IF
BOX X5 Y5 X6 Y6
LET YSIGN = 1
LET YSIGN = -1 IF K >= NUMBRAHP
IF ARROWYN = 1
LET X7 = X1 + .2
LET X8 = X1 - 0.9
LET Y7 = Y1 + 1.5*YSIGN
LET Y8 = Y1 + (1.5-0.4)*YSIGN
DRAW X7 Y7 X1 Y1
DRAW X8 Y8 X1 Y1
END OF IF
END OF LOOP
.
. STEP 9--
. FORM THE SUB-BRANCHES
.
LET H = H/2
LET W = W/2
LET H = HMAX IF H > HMAX
LET H = HMIN IF H < HMIN
LET W = WMAX IF W > WMAX
LET W = WMIN IF W < WMIN
HEIGHT H
WIDTH W
JUSTIFICATION LEFT
.
LET X = X0
LET X = X-BRANCHDEL
LOOP FOR K = 1 1 NUMBRA
LET X = X+BRANCHDEL
LET X = X0 IF K = NUMBRAHP
LET X1 = X
. LET X2 = X1-15
LET X2 = X1-10
LET XT = X2
LET XB = X1
LET YT = 90
LET YB = 50
LET XT = X1 IF K >= NUMBRAHP
LET XB = X2 IF K >= NUMBRAHP
LET YT = 50 IF K >= NUMBRAHP
LET YB = 10 IF K >= NUMBRAHP
LET NUMSUBK = NUMSUB^K
LOOP FOR L = 1 1 NUMSUBK
LET P = L/(NUMSUBK + 1)
LET X3 = XT-P*(XT-XB)
LET X4 = X3+LINELEN
LET X5 = X3+2
LET Y3 = YT-P*(YT-YB)
LET Y4 = Y3
LET Y5 = Y3+H/2
DRAW X3 Y3 X4 Y4
MOVE X5 Y5
LET KL = K*100+L
TEXT ^SB^KL
IF ARROWYN = 1
LET X7 = X3+1.5
LET X8 = X3+1.5
LET Y7 = Y3 + .75
LET Y8 = Y3 - .75
DRAW X7 Y7 X3 Y3
DRAW X8 Y8 X3 Y3
END OF IF
.
END OF LOOP
END OF LOOP
.
. CALL LOGO3.DP
.
SET IRD 5
SET IO
