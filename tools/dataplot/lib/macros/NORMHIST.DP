. THIS IS THE DATAPLOT PROGRAM FILE     NORMHIST.DP
. PURPOSE--GENERATE A MENU WHICH IN TURN WILL
.          SUPERIMPOSE A NORMAL DENSITY FUNCTION
.          ATOP A HISTOGRAM.
. NOTE--AFTER PASS 1,
.       THE FOLLOWING 5 PARAMETERS
.       ARE CONTROLLABLE BY THE USER
.       (VIA THE LET COMMAND)--
.          HISTMAX = THE EYEBALL SMOOTHED
.                    MAXIMUM OF THE HISTOGRAM
.                    AT THE CENTER.
.                    DEFAULT VALUE = N/8
.                    WHERE N = SAMPLE SIZE.
.             FMIN = THE SMALLEST VALUE AT WHICH THE
.                    NORMAL PDF SHOULD BE EVALUATED.
.                    DEFAULT VALUE =
.                    DATA MIN - (DATA MAX - DATA MIN)
.             FMAX = THE SMALLEST VALUE AT WHICH THE
.                    NORMAL PDF SHOULD BE EVALUATED.
.                    DEFAULT VALUE =
.                    DATA MAX + (DATA MAX - DATA MIN)
.             LSL  = LOWER SPECIFICATION LIMIT
.                    (IF NOT DESIRED, THEN SET TO -999)
.             USL  = UPPER SPECIFICATION LIMIT
.                    (IF NOT DESIRED, THEN SET TO -999)
. NOTE--AFTER PASS 1,
.       THE PLOT MAY BE AUTOMATICALLY REGENERATED
.       BY ENTERING THE    /    COMMAND.
. DATE--JUNE 1988
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
ERASE
.
SET IO TERMINAL
.
WRITE " "
WRITE "               ****************************************"
WRITE "               **           DATAPLOT MENU            **"
WRITE "               **    SUPERIMPOSE A NORMAL DENSITY    **"
WRITE "               **           ON A HISTOGRAM           **"
WRITE "               ****************************************"
WRITE " "
. STEP 1--
. DETERMINE WHERE THE RESPONSE VARIABLE IS
.
WRITE " "
WRITE "WHAT IS THE NAME OF THE VARIABLE"
WRITE "FOR WHICH A HISTOGRAM IS TO BE GENERATED?"
READ STRING SV
.
LET N = NUMBER ^SV
LET HISTMAX0 = N/8
.
LET XMIN = MINIMUM ^SV
LET XMAX = MAXIMUM ^SV
LET XRANGE = RANGE ^SV
LET C = 1
LET FMIN0 = XMIN-C*XRANGE
LET FMAX0 = XMAX+C*XRANGE
.
LET M = MEAN ^SV
LET S = STANDARD DEVIATION ^SV
.
WRITE " "
WRITE "WHAT IS YOUR EYEBALL SMOOTHED ESTIMATE OF THE"
WRITE "MAXIMUM VALUE OF THE HISTOGRAM AT THE CENTER?"
WRITE "(IF YOU WANT THE DEFAULT VALUE = "
WRITE "SAMPLE SIZE / 8 = ^HISTMAX0 , THEN ENTER -999 )."
READ PARAMETER HISTMAX
LET HISTMAX = HISTMAX0 IF HISTMAX = -999
.
WRITE " "
WRITE "WHAT IS THE SMALLEST VALUE AT WHICH"
WRITE "YOU WISH THE NORMAL DENSITY FUNCTION"
WRITE "TO BE EVALUATED?  (IF YOU WANT THE DEFAULT "
WRITE "VALUE = ^FMIN0 , THEN ENTER -999 )."
READ PARAMETER FMIN
LET FMIN = FMIN0 IF FMIN = -999
.
WRITE " "
WRITE "WHAT IS THE LARGEST  VALUE AT WHICH"
WRITE "YOU WISH THE NORMAL DENSITY FUNCTION"
WRITE "TO BE EVALUATED?  (IF YOU WANT THE DEFAULT "
WRITE "VALUE = ^FMAX0 , THEN ENTER -999 )."
READ PARAMETER FMAX
LET FMAX = FMAX0 IF FMAX = -999
.
WRITE " "
WRITE "LOWER SPEC LIMIT = ?"
WRITE "(IF YOU DO NOT WANT A LOWER SPEC LIMIT, ENTER -999)."
READ PARAMETER LSL
.
WRITE " "
WRITE "UPPER SPEC LIMIT = ?"
WRITE "(IF YOU DO NOT WANT A UPPER SPEC LIMIT, ENTER -999)."
READ PARAMETER USL
.
WRITE " "
WRITE "TITLE (POSITIONED AT TOP OF PAGE) = ?"
READ STRING T
WRITE "LEFT   VERTICAL   AXIS LABEL      = ?"
READ STRING Y1L
WRITE "FIRST  HORIZONTAL AXIS LABEL      = ?"
READ STRING X1L
WRITE "SECOND HORIZONTAL AXIS LABEL      = ?"
READ STRING X2L
WRITE "THIRD  HORIZONTAL AXIS LABEL      = ?"
READ STRING X3L
.
WRITE " "
WRITE "FONT = ?"
READ STRING F
.
WRITE " "
WRITE "THE PLOT IS NOW BEING FORMED..."
.
PRE-ERASE OFF
.
.
FONT ^F
TITLE ^T
Y1LABEL ^Y1L
X1LABEL ^X1L
X2LABEL ^X2L
X3LABEL ^X3L
LET K = HISTMAX/.39894
LET FINC = (FMAX-FMIN)/101
ERASE
HISTOGRAM ^SV
LIMITS FREEZE
FRAME OFF
TICS OFF
TIC LABELS OFF
TITLE
LABEL
PLOT K*NORPDF((X-M)/S) FOR X = FMIN FINC FMAX
IF LSL <> -999
LET XTEMP(1) = LSL
LET XTEMP(2) = LSL
LET YTEMP(1) = 0
LET YTEMP(2) = 10*HISTMAX
LINES DOTTED
PLOT YTEMP XTEMP
END OF IF
IF USL <> -999
LET XTEMP(1) = USL
LET XTEMP(2) = USL
LINES DOTTED
PLOT YTEMP XTEMP
END OF IF
LIMITS
FRAME ON
Y1TICS ON
Y1TIC LABELS ON
X1TICS ON
X1TIC LABELS ON
SAVE 37 TO 1
.
CALL [FILLIB.DATA]LOGO3.DP
.
SET IRD 5
SET IO
