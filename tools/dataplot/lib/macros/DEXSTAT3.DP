. THIS IS THE DATAPLOT MACRO FILE     DEXSTAT3.DP
. PURPOSE--GENERATE A MULTIPLOT OF STATISTIC PLOTS
.          FOR A USER-DEFINED STATISTIC (MEAN, MEDIAN,
.          SD, ANOP, ETC.)
.          THIS MACRO GENERATES PLOTS
.          FOR MAIN EFFECTS AND
.          FOR 2-TERM INTERACTIONS AND
.          FOR 3-TERM INTERACTIONS
. NOTE--THE FOLLOWING MUST PRE-EXIST--
.       1. Y = THE RESPONSE VARIABLE
.       2. X1 X2 X3 X4 ETC. = FACTORS
.       3. NUMFAC = THE NUMBER OF FACTORS
.       4. STAT = THE STRING WITH THE DESIRED STATISTIC
.              EXAMPLE--LET STRING STAT = MEAN
.              EXAMPLE--LET STRING STAT = SD
.              EXAMPLE--LET STRING STAT = ANOP
.       5. DP = THE NUMBER OF DECIMAL PLACES THE
.              COMPUTED SLOPE SHOULD BE ROUNDED TO
.              EXAMPLE--LET DP = 1
.       6. ANOP LIMITS (ONLY NEEDED IF STAT = ANOP)
.              EXAMPLE--ANOP LIMITS 0 65
.              EXAMPLE--ANOP LIMITS 80 100
. NOTE--THE USER SHOULD PRE-SET YLIMITS
.       FOR MEAN PLOT, YLIMITS SHOULD BE ABOUT
.       DATA YMIN TO YMAX, OR ROUGHLY 1/2 THE RANGE
.       FOR ANOP PLOT, YLIMITS SHOULD = 0 100
. EXAMPLE--DIMENSION 20 VARIABLES
.          SKIP 25
.          READ BOXBIKE2.DAT Y X1 TO X7
.          LET NUMFAC = 7
.          LET STRING STAT = MEAN
.          YLIMITS 50 100
.          LET DP = 1
.          CALL DEXSTAT3.DP
.
. -----START POINT--------------------
.
FEEDBACK OFF
.
. ------------------------------------
. STEP 1--
. SET UP SPECIAL PLOT FORMAT
.
CHAR X BLANK
LINES SOLID DOTTED
TICS OFF
TIC LABELS OFF
FRAME CORNER COORDINATES 0 0 100 100
.
. ------------------------------------
. STEP 2--
. GENERATE THE PLOTS
.
LET NUMFACM1=NUMFAC-1
LET NUMFACM2=NUMFAC-2
LET NUMFACP1=NUMFAC+1
MULTIPLOT NUMFACP1 NUMFAC
.
XLIM -2 2
LOOP FOR K = 1 1 NUMFAC
LET JUNK1 = X^K
LET JUNK1 = CODE JUNK1
LET JUNK1 = 2*JUNK1-3
LET XAXIS = JUNK1
LET XAXIS2 = Y*XAXIS
LET SLOPE = MEAN XAXIS2
LET SLOPE = 2*SLOPE
LET SLOPE = ROUND(SLOPE,DP)
LEGEND 1 ^K  ^SLOPE
^STAT PLOT Y XAXIS
END OF LOOP
.
XLIM -4 4
LOOP FOR K = 1 1 NUMFACM1
LET KP1 = K+1
LOOP FOR L = KP1 1 NUMFAC
LET JUNK1 = X^K
LET JUNK2 = X^L
LET JUNK1 = CODE JUNK1
LET JUNK2 = CODE JUNK2
LET JUNK1 = 2*JUNK1-3
LET JUNK2 = 2*JUNK2-3
LET XAXIS = 2 * JUNK1 + JUNK2
LEGEND 1 ^K&-^L
^STAT PLOT Y XAXIS
END OF LOOP
END OF LOOP
.
XLIM -8 8
LOOP FOR K = 1 1 NUMFACM2
LET KP1 = K+1
LOOP FOR L = KP1 1 NUMFACM1
LET LP1 = L+1
LOOP FOR M = LP1 1 NUMFAC
LET JUNK1 = X^K
LET JUNK2 = X^L
LET JUNK3 = X^M
LET JUNK1 = CODE JUNK1
LET JUNK2 = CODE JUNK2
LET JUNK3 = CODE JUNK3
LET JUNK1 = 2*JUNK1-3
LET JUNK2 = 2*JUNK2-3
LET JUNK3 = 2*JUNK3-3
LET XAXIS = 4 * JUNK1 + 2 * JUNK2 + JUNK3
LEGEND 1 ^K&-^L&-^M
^STAT PLOT Y XAXIS
END OF LOOP
END OF LOOP
END OF LOOP
.
. ------------------------------------
. STEP 3--
. REVERT BACK TO THE ORIGINAL PLOT FORMAT
.
XLIMITS
CHAR BLANK ALL
LINES SOLID ALL
Y1TICS ON
X1TICS ON
Y1TIC LABELS ON
X1TIC LABELS ON
FRAME CORNER COORDINATES
LEGEND 1
MULTIPLOT OFF
