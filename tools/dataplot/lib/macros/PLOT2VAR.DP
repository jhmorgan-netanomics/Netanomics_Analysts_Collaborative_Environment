. THIS IS THE DATAPLOT PROGRAM FILE     PLOT2VAR.DP
. PURPOSE--GENERATE AND EXECUTE MENU FOR 2-D PLOT
. READ 2 VARIABLES FROM A FILE
. GENERATE A Y VERSUS X PLOT
. ALONG WITH TITLES AND LABELS
. DATE--JULY 1, 1986
.
. -----START POINT-----------------------------------
.
FEEDBACK OFF
.
ERASE
.
SET IO TERMINAL
.
WRITE " NAME OF DATA FILE = ?"
READ STRING SFN
.
WRITE "NUMBER OF HEADER (NON-DATA) LINES TO SLIP OVER = ?"
READ PARAMETER NLS
SKIP NLS
.
WRITE "NAME OF THE 2 VARIABLES BEING READ IN = ?"
READ STRING SV1 SV2
.
WRITE "YOUR DATA FILE IS NOW BEING READ IN..."
.
FEEDBACK ON
READ ^SFN ^SV1 ^SV2
FEEDBACK OFF
.
WRITE " "
WRITE "VARIABLE TO BE PLOTTED VERTICALLY = ?"
READ STRING SVV
.
WRITE "VARIABLE TO BE PLOTTED HORIZONTALLY = ?"
READ STRING SVH
.
WRITE " "
WRITE "TITLE FOR PLOT = ?"
READ STRING STI
TITLE ^STI
.
WRITE "VERTICAL LABEL ON PLOT = ?"
READ STRING SYL
Y1LABEL ^SYL
.
WRITE "HORIZONTAL LABEL ON PLOT = ?"
READ STRING SXL
X1LABEL ^SXL
.
SET IO
.
PLOT ^SVV ^SVH
