. Two Moms: QQ Plot
. Jonathan H. Morgan, Ph.D.
. 24 January 2025

.   Setting Directory
    cd /mnt/d/GitHub_Repositories/Netanomics_Analysts_Collaborative_Environment/Workflow_Demonstration/Dataplot_functions
.   dataplot
.   PWD

. -------------------- .
.    IMPORTING DATA    .
. -------------------- .

RESET

CALL read_two_moms_map.DP

. ---------------------------- .
.    CREATING REFERENCE LINE   .
. ---------------------------- .

.   Isolating X & Y Cooridnates
    NORMAL PROBABILITY PLOT resid
    x_ref = XPLOT
    y_ref = YPLOT

    LET x_1 = MINIMUM x_ref
    LET y_1 = MINIMUM y_ref

    LET x_max = MAXIMUM x_ref
    y_max = MAXIMUM y_ref

. ---------------------- .
.    CREATING QQ PLOT    .
. ---------------------- .

COMMENT Output Device Settings: Score Comparison
    DEVICE 2 CLOSE
    LET STRING fplot = Two_Moms_QQPlot.ps
    LET STRING outplot = Two_Moms_QQPlot.png
    SET ipl1na ^fplot
    SET POSTSCRIPT COLOR DEFAULT ON
    SET POSTSCRIPT FONT PALATINO
    DEVICE 2 POSTSCRIPT

COMMENT Plot Settings
    X2FRAME OFF; Y2FRAME OFF; X1FRAME ON; Y1FRAME ON
    FRAME THICKNESS = 0.1 
    FRAME CORNER COORDINATES 20 15 90 90
    YGRID ON; XGRID OFF
    GRID COLOR g50
    TIC OFFSET UNITS SCREEN
    TIC POSITION THROUGH
    label case asis
    title case asis
    TITLE SIZE 4
    X1LABEL SIZE 3
    Y1LABEL SIZE 3
    TIC LABEL SIZE 2

COMMENT Plotting
    LINE BLANK 
    CHARACTER CIRCLE 
    CHARACTER FILL ON
    CHARACTER COLOR blue
    CHARACTER HW 0.5 0.375
    TITLE AUTOMATIC
    TITLE OFFSET 2
    LABEL CASE ASIS
    Y1LABEL Residuals of Daughters' Family Size
    X1LABEL Unit Normal Quantile
    y1LABEL COORDINATES 5 50
    TITLE Normal Probability Plot of the Model Residuals
    NORMAL PROBABILITY PLOT resid

    LINE SOLID; LINE COLOR brown
    DRAWDATA x_1 y_1 x_max y_max

    LINE COLOR black
    DRAWDATA 0 y_1 0 y_max
    DRAWDATA x_1 0 x_max 0

    LET PPA0 = ROUND(PPA0,3)
    LET PPA1 = ROUND(PPA1,3)
    LET PPCC = ROUND(PPCC,3)
    JUSTIFICATION LEFT
    MOVE 25 87; TEXT A0: ^PPA0
    MOVE 25 83; TEXT A1: ^PPA1
    MOVE 25 79; TEXT PPCC: ^PPCC

COMMENT Writing to File
    DEVICE 2 CLOSE 
    SYSTEM gs -dBATCH -dNOPAUSE -sDEVICE=png16m -r300 -sOutputFile=^outplot ^fplot 